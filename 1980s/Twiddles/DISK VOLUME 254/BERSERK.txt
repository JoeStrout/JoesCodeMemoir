1  IF  PEEK (103) <  > 16 OR  PEEK (104) <  > 64 THEN  POKE 16399,0: POKE 103,16: POKE 104,64: PRINT  CHR$ (4)"RUN BERSERK"
10  REM 

   BERSERK


11  REM 

  BY JOE STROUT
12  REM      12/25/87


15  DIM PS(4,4),CL(50)
20  TEXT : HOME :D$ =  CHR$ (4):SH = 3072: SCALE= 1: ROT= 0
30  GOSUB 10000: REM  OPENING SEQUENCE
40  HGR : TEXT : FOR I = 1 TO 30: XDRAW I AT 100,100:CL(I) =  PEEK (234): XDRAW I AT 100,100: NEXT 
50 PS(1,0) = 2:PS(1,1) = 3:PS(1,2) = 4:PS(2,0) = 5:PS(2,1) = 6:PS(2,2) = 7
60 PS(3,0) = 2:PS(3,1) = 3:PS(3,2) = 11:PS(4,0) = 8:PS(4,1) = 9:PS(4,2) = 10
70 PS(2,3) = 12:PS(2,4) = 13:PS(4,3) = 15:PS(4,4) = 14
75 RS = 20:RW = 21:X1 = 22:X2 = 25:BD = 1
90 PX = 140:PY = 130:PD = 1:PL = 2:BL = 1
100  HGR : GOSUB 5000
110 SC = 0: VTAB 22: HTAB 25: PRINT "SCORE: 0"
120 B1 = 26:B2 = 27:B3 = 28:B4 = 29
130 D1 = 16:D2 = 17: REM  'DEAD' SHAPES
140 ES = 19
400  GOSUB 500: GOSUB 800: IF BX THEN  GOSUB 700
410  GOSUB 1500: GOTO 400
499  END 
500  REM 

   MOVE PLAYER


505  IF  PEEK (49249) > 127 OR  PEEK (49250) > 127 THEN 600
510 JX =  INT ( PDL (0) / 51) - 2:JX = JX * 2:JY =  INT ( PDL (1) / 51) - 2:JY = JY * 2: IF  NOT JX AND  NOT JY THEN  RETURN 
520 X = PX + JX:Y = PY + JY:D = (JX = 0) * (3 * (JY > 0) + (JY < 0)) + (4 * (JX < 0) + 2 * (JX > 0))
530  XDRAW PS(PD,PF) AT PX,PY: XDRAW PS(D, NOT PF) AT X,Y:PX = X:PY = Y:PD = D:PF =  NOT PF
535 DX = JX:DY = JY: REM  FOR THE BULLET'S SAKE
540  IF  PEEK (234) <  > CL(PS(PD,PF)) THEN 1300
550  IF PX > 5 AND PX < 267 AND PY > 5 AND PY < 154 THEN  RETURN 
560  GOTO 5500: REM  NEW BOARD
600  REM 
  SHOOT!

610  IF (DX AND  NOT DY) OR (DY AND  NOT DX) THEN F = 2: GOTO 615
613 F = 3 * (DY < 0) + 4 * (DY > 0)
615  XDRAW PS(PD,PF) AT PX,PY: XDRAW PS(PD,F) AT PX,PY:PF = F
620  IF BX THEN U =  PEEK (49200) +  PEEK (49200): RETURN 
630  ON PD GOTO 631,632,635,636
631 BX = PX + 5:BY = PY - 7:BS = B2: GOTO 640
632  IF DY > 0 THEN BX = PX + 9:BY = PY + 7:BS = B3: GOTO 640
633  IF DY < 0 THEN BX = PX + 9:BY = PY - 6:BS = B4: GOTO 640
634 BX = PX + 9:BY = PY + 3:BS = B1: GOTO 640
635 BX = PX:BY = PY + 7:BS = B2: GOTO 640
636  IF DY = 0 THEN BX = PX - 7:BY = PY + 3:BS = B1: GOTO 640
637  IF DY < 0 THEN BX = PX - 7:BY = PY - 7:BS = B3: GOTO 640
638 BX = PX - 6:BY = PY + 7:BS = B4: GOTO 640
640  XDRAW BS AT BX,BY:BI = DX * 3.5:BJ = DY * 3.5: GOTO 4100
700  REM 
  MOVE PLAYER'S BULLET

710  XDRAW BS AT BX,BY:BX = BX + BI:BY = BY + BJ: IF BX < 0 OR BX > 279 OR BY < 0 OR BY > 160 THEN BX = 0: RETURN 
720  XDRAW BS AT BX,BY: IF  PEEK (234) = CL(BS) THEN  RETURN 
730  GOTO 1200
800  REM 

   MOVE ROBOT


805  IF MX THEN 1000
810  IF  NOT NR THEN  RETURN 
820  IF RB THEN 900
825  IF ED THEN  RETURN 
830 R =  INT (NR *  RND (1) + 1): IF  ABS (RX(R) - PX) < 10 OR  ABS (RY(R) - PY) < 10 THEN 850
840  XDRAW RS AT RX(R),RY(R): XDRAW RW AT RX(R),RY(R) - 2:RB = R:RY(R) = RY(R) - 2: RETURN 
850  REM  FIRE MISSLE
860  IF  ABS (RX(R) - PX) < 10 THEN MI = 0:MJ =  SGN (PY - RY(R)) * 8:MX = RX(R) + 5:MY = RY(R) - 9 * (MJ < 0) + 14 * (MJ > 0): GOTO 880
870 MI =  SGN (PX - RX(R)) * 8:MJ = 0:MX = RX(R) - 9 * (MI < 0) + 12 * (MI > 0):MY = RY(R) + 5
880 MS = 22 + (MI = 0): XDRAW MS AT MX,MY: GOTO 4350
900  REM 
  CONTINUE TO MOVE ROBOT #RB

905 A =  ABS (PX - RX(RB)):B =  ABS (PY - RY(RB)): IF A < 7 OR B < 7 THEN 950
910  IF A < B THEN NX = RX(RB) + 5 *  SGN (PX - RX(RB)):NY = RY(RB): GOTO 930
920 NX = RX(RB):NY = RY(RB) + 5 *  SGN (PY - RY(RB))
930  XDRAW RW AT RX(RB),RY(RB): XDRAW RW AT NX,NY:RX(RB) = NX:RY(RB) = NY: IF  PEEK (234) = CL(RW) THEN  RETURN 
940  GOTO 1300
950  XDRAW RW AT RX(RB),RY(RB): XDRAW RS AT RX(RB),RY(RB):RB = 0: IF  PEEK (234) = CL(RS) THEN  RETURN : REM STOP ROBOT; SET DOWN
960  GOTO 940
1000  REM 
 MOVE ROBOT'S MISSLE

1010  XDRAW MS AT MX,MY:MX = MX + MI:MY = MY + MJ: IF MX < 0 OR MX > 279 OR MY < 0 OR MY > 170 THEN MX = 0: RETURN 
1020  XDRAW MS AT MX,MY: IF  PEEK (234) = CL(MS) THEN  RETURN 
1030  GOSUB 1300: XDRAW MS AT MX,MY:MX = 0: RETURN 
1100  REM 

   COLLISION SCANS


1110  REM  SCAN ROBOTS
1115  IF  NOT NR THEN  RETURN 
1120 RH = 0: FOR I = 1 TO NR: IF RB = I THEN  XDRAW RW AT RX(I),RY(I): XDRAW RW AT RX(I),RY(I): GOTO 1140
1130  XDRAW RS AT RX(I),RY(I): XDRAW RS AT RX(I),RY(I)
1140  IF  PEEK (234) = CL(RS + (RB = I)) THEN 1160
1150 RH = RH + 1:HT(RH) = I: IF RB = I THEN  XDRAW RW AT RX(I),RY(I): XDRAW RS AT RX(I),RY(I)
1160  NEXT 
1170  RETURN 
1200  REM 
    CHECK FOR PLAYER'S BULLET

1205  IF  NOT NR THEN RH = 0: GOTO 1220
1210  GOSUB 1100: REM  SCAN ROBOTS
1220  XDRAW BS AT BX,BY:BX = 0: REM  REMOVE BULLET
1230  IF  NOT RH THEN  RETURN 
1250  REM    EXPLODE HIT ROBOTS
1255  IF  NOT RH OR  NOT NR THEN  RETURN 
1260  FOR I = 1 TO RH: XDRAW RS AT RX(HT(I)),RY(HT(I)): XDRAW X1 AT RX(HT(I)),RY(HT(I)):U =  PEEK (49200): NEXT 
1270  FOR I = 1 TO RH: XDRAW X1 AT RX(HT(I)),RY(HT(I)): XDRAW X2 AT RX(HT(I)),RY(HT(I)):U =  PEEK (49200) +  PEEK (49200): NEXT 
1280  FOR I = 1 TO RH: XDRAW X2 AT RX(HT(I)),RY(HT(I)):RX(HT(I)) = RX(NR):RY(HT(I)) = RY(NR): IF RB = HT(I) THEN RB = 0
1281  FOR J = 1 TO RH: IF HT(J) = NR AND J <  > I THEN HT(J) = HT(I)
1282  NEXT : REM  MOVE HIT RECORD ALONG WITH OTHER DATA
1283  IF RB = NR THEN RB = HT(I)
1285  IF NR = 0 OR RB > NR THEN  STOP 
1290 NR = NR - 1:U =  PEEK (49200) +  PEEK (49200) ^ 3:SC = SC + 50: VTAB 22: HTAB 32: PRINT SC: GOSUB 4250: NEXT : RETURN 
1300  REM 
  SCAN ALL OBJECTS

1310  GOSUB 1100: REM  SCAN ROBOTS
1320  XDRAW PS(PD,PF) AT PX,PY: XDRAW PS(PD,PF) AT PX,PY: IF  PEEK (234) <  > CL(PS(PD,PF)) THEN  GOSUB 1250: GOTO 6000
1330  GOTO 1250: REM  REMOVE ROBOTS
1500  REM 

  EVIL EDDIE!


1510  IF  NOT ED AND  RND (1) > BD / 500 THEN  RETURN 
1520  IF ED THEN 1600
1530  IF  ABS (PX - EX) < 20 AND  ABS (PY - EY) < 20 THEN  RETURN 
1540  XDRAW ES AT EX,EY:U =  PEEK (49200) +  PEEK (49200) * 7
1550 ED = .1: GOTO 4150
1600  REM 

  CONTINUE TO MOVE EDDIE
1610 ED =  INT (ED + 1):NX = EX + 3 *  SGN (PX - EX):NY = EY + 3 *  SGN (PY - EY) + ED
1620  XDRAW ES AT EX,EY: XDRAW ES AT NX,NY:EX = NX:EY = NY: IF ED > 6 THEN ED =  - ED: HCOLOR= 5: DRAW ES - 1 AT EX + 3,EY + 11: HCOLOR= 3: GOSUB 4300
1630  IF  NOT ED THEN ED = .1
1640  RETURN 
4000  REM 

   SOUND SUBROUTINES
4010  POKE 1,3: POKE 2,70: POKE 3,1: POKE 799,198: POKE 789,230: CALL 782: RETURN 
4050  POKE 1,4: POKE 799,198: POKE 789,198: POKE 2,25: POKE 3,30: CALL 782: RETURN 
4100  POKE 1,2: POKE 2,200: POKE 3,1: POKE 799,230: POKE 789,230: CALL 782: RETURN 
4150  POKE 1,3: POKE 2,250: POKE 3,5: POKE 799,198: POKE 789,230: CALL 782: RETURN 
4200  POKE 1,6: POKE 2,150: POKE 3,200: POKE 4,10: CALL 837: RETURN 
4250  POKE 1,1: POKE 2,100: POKE 3,120: POKE 799,198: POKE 789,198: CALL 782: RETURN 
4300  POKE 1,2: POKE 2,40: POKE 3,120: POKE 799,198: POKE 789,198: CALL 782: RETURN 
4350  POKE 1,1: POKE 2,150: POKE 3,1: POKE 799,230: POKE 789,230: CALL 782: RETURN 
5000  REM 

  SLOW ROUTINES


5010  REM 

   DRAW BORDER


5020  HCOLOR= 3
5030  FOR I = 0 TO 5: HPLOT I,I TO 279 - I,I TO 279 - I,159 - I TO I,159 - I TO I,I: NEXT 
5050 X(1) = 95:X(2) = 185:Y(1) = 50:Y(2) = 100:X(0) = 5:X(3) = 274:Y(0) = 5:Y(3) = 160
5060  FOR X = 1 TO 2: FOR Y = 1 TO 2: GOSUB 5300: NEXT : NEXT 
5070 D = 0: HCOLOR= 0: FOR R = 1 TO 4: IF  RND (1) < .5 THEN  GOSUB 5200:D = D + 1
5080  NEXT : IF  NOT D THEN 5070
5090 RB = 0
5100  HCOLOR= 3: DRAW PS(PD,PF) AT PX,PY:NR =  INT (7 *  RND (1) + 2): IF NR > BD + 2 THEN 5100
5110  FOR I = 1 TO NR:RX(I) =  INT (260 *  RND (1) + 10):RY(I) = (130 *  RND (1) + 10): XDRAW RS AT RX(I),RY(I): IF  PEEK (234) <  > CL(RS) THEN  XDRAW RS AT RX(I),RY(I):I = I - 1
5120  NEXT :BP = NR * 10
5130 EX = PX:EY = PY:ED = 0
5135  IF SC =  > BL * 3000 THEN PL = PL + 1:BL = BL + 1: IF PL > 5 THEN PL = 5
5140  IF PL THEN  FOR I = 1 TO PL: XDRAW 2 AT 200 + I * 10,155: NEXT 
5199  RETURN 
5200  REM    DRAW DOOR
5210  FOR I = 0 TO 5: ON R GOSUB 5220,5230,5240,5250: NEXT : RETURN 
5220  HPLOT X(1) + 5,0 + I TO X(2) - 1,0 + I: RETURN 
5230  HPLOT X(1) + 5,159 - I TO X(2) - 1,159 - I: RETURN 
5240  HPLOT 0 + I,Y(1) + 5 TO 0 + I,Y(2) - 1: RETURN 
5250  HPLOT 279 - I,Y(1) + 5 TO 279 - I,Y(2) - 1: RETURN 
5300  REM    DRAW WALL
5310 R =  INT (4 *  RND (1) + 1)
5320  ON R GOTO 5330,5340,5350,5360
5330  FOR I = 0 TO 4: HPLOT X(X) + I,Y(Y) TO X(X) + I,Y(Y - 1): NEXT : RETURN 
5340  FOR I = 0 TO 4: HPLOT X(X) + I,Y(Y) TO X(X) + I,Y(Y + 1) + 4: NEXT : RETURN 
5350  FOR I = 0 TO 4: HPLOT X(X),Y(Y) + I TO X(X + 1) + 4,Y(Y) + I: NEXT : RETURN 
5360  FOR I = 0 TO 4: HPLOT X(X),Y(Y) + I TO X(X - 1),Y(Y) + I: NEXT : RETURN 
5500  REM 

  NEW BOARD


5510  POKE 49233,0: CALL  - 3086: POKE 230,32
5515  IF PX < 10 THEN PX = 264: GOTO 5540
5520  IF PX > 260 THEN PX = 15
5525  IF PY < 10 THEN PY = 140: GOTO 5540
5530  IF PY > 140 THEN PY = 15
5540 BD = BD + 1: VTAB 12: HTAB 15: INVERSE : PRINT " BOARD "BD" ": NORMAL 
5545 BX = 0:MX = 0
5550  IF  NOT NR THEN  VTAB 22: PRINT "BONUS: ";BP:SC = SC + BP: VTAB 22: HTAB 32: PRINT SC
5560  GOSUB 5000: CALL  - 3100: POKE 230,32: VTAB 22: PRINT "              ";: RETURN 
6000  REM 

  DEAD PLAYER


6010 S = PS(PD,PF): FOR I = 1 TO 5: XDRAW S AT PX,PY: XDRAW D1 AT PX,PY:U =  PEEK (49200) +  PEEK (49200)
6020  XDRAW D1 AT PX,PY: IF I < 5 THEN  XDRAW S AT PX,PY
6025  FOR J = 1 TO 50: NEXT : REM  PUT SOUND HERE
6030 U =  PEEK (49200): NEXT 
6040  FOR I = 1 TO 20: XDRAW D1 AT PX,PY: GOSUB 4050: XDRAW D1 AT PX - 2,PY - 2: GOSUB 4050: NEXT 
6050  XDRAW D2 AT PX,PY + 2
6055  IF SC =  > BL * 3000 THEN PL = PL + 1:BL = BL + 1: IF PL > 5 THEN PL = 5
6060 PL = PL - 1: IF PL < 0 THEN  VTAB 23: HTAB 15: INVERSE : PRINT " GAME OVER ": WAIT 49152,128: NORMAL : END 
6070 BD = BD - 1:PX = 140:PY = 130:PD = 1: FOR J = 1 TO 1000: NEXT 
6080  GOSUB 5500
6100  POP : GOTO 400
7000  REM  CENTER STRING A$
7010  HTAB 20 -  LEN (A$) / 2: PRINT A$: RETURN 
10000  REM 

 TITLE SCREEN & INSTRUCTIONS


10010  TEXT : HOME : PRINT  CHR$ (4)"PR#0"
10020  NORMAL : VTAB 4:A$ = "THE": GOSUB 7000
10030 A$ = "USELESS TWIDDLES ASSOCIATION": GOSUB 7000:A$ = "PRESENTS...": GOSUB 7000
10040  VTAB 2: IF  NOT FL THEN  PRINT D$"BLOADBERSERK.SNDS"
10050  PRINT "****  **** ****   **** **** ****  **  ******* **** ***** ****  **** ***** ** **"
10060  PRINT "** ** **   ** ** **    **   ** ** ****  ****  ***  ****   ***  ***  ****  ****  ** ** **   ** **";
10070  PRINT "    ** **   ** ** ** ** ***** **** ** **  **** **** ** ** **  ******  **** ** ** ****  **** ** ** **  **
10080  PRINT :A$ = "BY JOE J. STROUT": GOSUB 7000
10085  IF  NOT FL THEN  PRINT D$"BLOADBERSERK.FONT,A"SH: POKE 232,SH -  INT (SH / 256) * 256: POKE 233, INT (SH / 256)
10090  VTAB 20:A$ = "DO YOU WANT INSTRUCTIONS?": GOSUB 7000: GET A$: IF A$ = "N" OR A$ = "n" THEN 10500
10095  HOME :A$ = " BERSERK ": INVERSE : GOSUB 7000: PRINT : PRINT : NORMAL 
10100 A$ = "USE THE JOYSTICK TO MOVE/SHOOT": GOSUB 7000:A$ = "THE EVIL ROBOTS.": GOSUB 7000: PRINT 
10110 A$ = "WATCH OUT FOR THEIR EVIL CREATOR,": GOSUB 7000: INVERSE :A$ = " EVIL EDDIE ": GOSUB 7000
10120 A$ = "HE IS INVINCIBLE!": NORMAL : GOSUB 7000: PRINT 
10130 A$ = "EVERYTHING IN THIS EVIL PLACE IS": GOSUB 7000:A$ = "DEADLY TO THE TOUCH, SO STAY CLEAR!": GOSUB 7000: PRINT 
10140 A$ = "SCORING: 50 PTS. EACH DESTROYED ROBOT": GOSUB 7000:A$ = "PLUS BONUS PTS. IF ALL ARE DESTROYED.": GOSUB 7000
10150  PRINT :A$ = "YOU GET A BONUS LIFE EVERY 3,000 PTS.": GOSUB 7000
10500  REM 
10510  VTAB 22:A$ = "GOOD LUCK!!!": GOSUB 7000
10520 A$ = "PRESS ANY KEY TO BEGIN:": GOSUB 7000
10530  WAIT 49152,128: HOME : RETURN 
