10  REM 

>>>>  DRAGON'S BREATH!  <<<<


11  REM 
 BY JOE STROUT  1/18/86

20  TEXT : HOME :D$ =  CHR$ (4):SH = 25000
30  IF  NOT FLAG THEN  PRINT D$"BLOADDRAGON.SHAPES,A"SH: POKE 232,SH -  INT (SH / 256) * 256: POKE 233, INT (SH / 256)
40  HGR : HCOLOR= 3: SCALE= 1: ROT= 0: HPLOT 0,0 TO 256,0 TO 256,155 TO 0,155 TO 0,0
50 DY = 100: XDRAW 3 AT 1,DY
60 PX = 251:PY = 75:PS = 1: XDRAW 1 AT PX,PY
70 NH(5) = 21: REM  "NO HIT" VALUE FOR SHAPE 5
99  VTAB 22
100  GOSUB 500: GOSUB 600: GOSUB 700
110  GOTO 100
500  REM 

  MOVE PLAYER


505  IF  PEEK (49249) > 127 THEN  XDRAW PS AT PX,PY: XDRAW 2 AT PX,PY:PS = 2: RETURN 
510 JX =  INT ( PDL (0) / 51) - 2:NX = PX + JX * 2:JY =  INT ( PDL (1) / 51) - 2:NY = PY + JY * 2
515  IF  NOT JX AND  NOT JY THEN  RETURN 
520  XDRAW PS AT PX,PY: XDRAW 1 AT NX,NY:PS = 1:PX = NX:PY = NY: RETURN 
600  REM 

  MOVE DRAGON


610  IF FC THEN 650
620  IF  NOT FX AND  RND (1) < .3 AND  ABS (DY - PY) < 20 THEN  XDRAW 3 AT 1,DY: XDRAW 4 AT 1,DY:FC = 1: RETURN 
630  XDRAW 3 AT 1,DY:DY = DY +  SGN (PY - DY): XDRAW 3 AT 1,DY: RETURN 
650 FC = FC + 1: IF FC = 5 THEN FX = 14:FY = DY: XDRAW 5 AT FX,FY: RETURN 
660  IF FC = 6 THEN  XDRAW 4 AT 1,DY: XDRAW 3 AT 1,DY:FC = 0
670  RETURN 
700  REM 

  MOVE FIREBALL


705  IF  NOT FX THEN  RETURN 
710  XDRAW 5 AT FX,FY:FX = FX + 10: IF FX > 252 THEN FX = 0: RETURN 
720  XDRAW 5 AT FX,FY: IF  PEEK (234) = NH(5) THEN  RETURN 
730  IF PS = 2 THEN  XDRAW 5 AT FX,FY:FX = 0: RETURN 
740  XDRAW 5 AT FX,FY: GOTO 800
800  REM 

  DEATH ROUTINE


810  FOR I = 1 TO 10: XDRAW PS AT PX,PY:U =  PEEK (49200): FOR J = 1 TO I * 10: NEXT J,I
820  HCOLOR= 0: DRAW PS AT PX,PY: HCOLOR= 3: END 
