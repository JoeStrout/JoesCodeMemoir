10  REM 

  5-MINUTE MYSTERIES


11  REM 
  BASE PROGRAM


12  REM  12/12/88
13  REM  BY JOE STROUT


19  DIM IW$(30),WD$(200),WN(200)
20  TEXT : HOME : PRINT  CHR$ (4)"PR#3": PRINT : HOME : NORMAL 
30 SW = 80: REM  SCREEN WIDTH
40 UP = 0: REM  UP=1 FOR ALL UPPER CASE
50  GOSUB 40000: GOTO 500
99  REM 

  SUBROUTINES


100  REM 
 PRINT A$ CENTERED
110  IF UP THEN  GOSUB 150
120  HTAB (SW / 2) -  INT ( LEN (A$) / 2 + 1): PRINT A$: RETURN 
150  REM 
 CONVERT A$ TO UPPER CASE
160 A$ = " " + A$
170  FOR S = 1 TO  LEN (A$): IF  MID$ (A$,S,1) > "`" AND  MID$ (A$,S,1) < "{" THEN A$ =  LEFT$ (A$,S - 1) +  CHR$ ( ASC ( MID$ (A$,S,1)) - 32) +  MID$ (A$,S + 1, LEN (A$))
180  NEXT :A$ =  MID$ (A$,2, LEN (A$))
190  RETURN 
200  REM 
  GET NEXT WORD OF A$
201  REM (SETS A$ TO ONLY THE FIRST WORD)
210  FOR S = 1 TO  LEN (A$): IF  MID$ (A$,S,1) = " " THEN B$ =  MID$ (A$,S + 1, LEN (A$)):A$ =  LEFT$ (A$,S - 1):I = 256
220  NEXT : RETURN 
300  REM 
 PRINT A$ WITH WRAPAROUND
305 MR$ = "[ MORE ]"
308  IF UP THEN  GOSUB 150
310 H =  PEEK (36) + 1: IF  LEN (A$) < SW - H THEN  PRINT A$;:SC = SC + 1: GOTO 340
320  FOR X = SW - H TO 1 STEP  - 1: IF  MID$ (A$,X,1) <  > " " THEN  NEXT 
330  PRINT  LEFT$ (A$,X - 1):A$ =  MID$ (A$,X + 1):SC = SC + 1: IF SC < 15 THEN 310
335  GOSUB 180: GOTO 310
340  IF  NOT NL THEN  RETURN 
350  FOR X = 1 TO NL: PRINT :SC = SC + 1: IF SC < 15 THEN  NEXT :NL = 0: RETURN 
360  GOSUB 180: NEXT :NL = 0: RETURN 
380  PRINT MR$: WAIT 49152,128: POKE 49168,0: VTAB  PEEK (37): PRINT  SPC( 10): PRINT : VTAB  PEEK (37):SC = 1: RETURN 
400  REM 
 CONVERT A$ TO COMMON SYNONYM
410  IF  RIGHT$ (A$,1) = "." OR  RIGHT$ (A$,1) = " " THEN A$ =  LEFT$ (A$, LEN (A$) - 1)
470  FOR S = 1 TO NW: IF A$ = WD$(S) THEN A$ = IW$(WN(S)):S = NW + 1
480  NEXT : RETURN 
500  REM 

  MAIN LOOP


510  INPUT "==> ";A$: GOSUB 150:C1$ = A$
520  GOSUB 200: GOSUB 400:V$ = A$
530 S1$ = B$:A$ = B$: GOSUB 200: IF A$ = "THE" OR A$ = "A" OR A$ = "HER" OR A$ = "HIS" OR A$ = "MY" OR A$ = "YOUR" THEN 530
540  IF A$ = "FOR" OR A$ = "AT" OR A$ = "OUT" THEN A$ = V$ + " " + A$: GOSUB 400:V$ = A$: GOTO 530
550 A$ = S1$: GOSUB 400:S$ = A$
580  PRINT "VERB: '"V$"'": PRINT "SUBJECT: '"S$"'"
590  GOTO 500
39999  END 
40000  REM 

  INITIALIZATION


40800  READ A$: IF  STR$ ( VAL (A$)) = A$ THEN ID =  VAL (A$): READ A$:IW$(ID) = A$
40810  IF A$ <  > "end" THEN NW = NW + 1:WD$(NW) = A$:WN(NW) = ID: GOTO 40800
40990  RETURN 
41000  REM 
   DATA
41010  DATA 1,LOOK,EXAMINE,LOOK AT,INSPECT
41020  DATA 2,LOOK FOR
41030  DATA 3,ACCUSE,IMPLICATE,POINT,ACCUSE AT,ACCUSE TO,ACCUSE OUT
41100  DATA 10,VICTIM,DEAD MAN,BODY,CADAVER,STIFF
41110  DATA 11,CROWD,WITNESSES,PEOPLE,PASSERS-BY
41500  DATA 30,BEADS,JEWELRY,NECKLACE,PEARLS
49999  DATA end
