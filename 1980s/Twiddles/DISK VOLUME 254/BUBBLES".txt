10  REM 

BUBBLES


15  DIM SC(28,16)
20  ROT= 0: SCALE= 1:SH = 25000:D$ =  CHR$ (4): HOME : GOSUB 700
28  IF  NOT FLAG THEN  PRINT D$"BLOAD SOUNDS"
30  IF  NOT FLAG THEN  PRINT D$"BLOAD BUBBLES.SHAPES,A"SH: POKE 232,SH -  INT (SH / 256) * 256: POKE 233, INT (SH / 256)
31  REM SHAPE 1 = BUBBLE
32  REM SHAPE 12 = ANT
33  REM SHAPE 11 = SMUDGE
34  REM SHAPE 10 = CRUMBS
35  REM SHAPE 9 = ROACH
36  REM SHAPE 8 = DRAIN
40  HGR : HCOLOR= 3: HPLOT 0,0: CALL  - 3082: XDRAW 8 AT 140,75: FOR X = 12 TO 15: FOR Y = 7 TO 8:SC(X,Y) = 8: NEXT Y,X:SC(12,7) = 0:SC(15,7) = 0:SC(12,8) = 0:SC(15,8) = 0
50 BX = 140:BY = 140:BS = 1: XDRAW BS AT BX,BY
60 NA = 15: FOR I = 1 TO NA: GOSUB 80:SC(X,Y) = 12: XDRAW 12 AT X * 10,Y * 10: NEXT :NS = 7: FOR I = 1 TO NS: GOSUB 80:SC(X,Y) = 11: XDRAW 11 AT X * 10,Y * 10: NEXT 
70 NC = 10: FOR I = 1 TO NC: GOSUB 80:SC(X,Y) = 10: XDRAW 10 AT X * 10,Y * 10: NEXT : GOTO 90
80 X =  INT (28 *  RND (1)):Y =  INT (16 *  RND (1)): IF SC(X,Y) <  > 0 THEN 80
85  RETURN 
90 X =  INT (27 *  RND (1)):Y =  INT (16 *  RND (1)): IF SC(X,Y) <  > 0 OR SC(X + 1,Y) <  > 0 THEN 90
100  XDRAW 9 AT X * 10,Y * 10:SC(X,Y) = 9:SC(X + 1,Y) = 9
200  REM === MAIN LOOP ===
205  GOSUB 300
210 JX =  INT ( PDL (0) / 52) - 2:DX = DX + JX:JY =  INT ( PDL (1) / 52) - 2:DY = DY + JY
212 DX = DX -  SGN (DX) / 20:DY = DY -  SGN (DY) / 20:DX = DX - .1 *  SGN (BX - 140):DY = DY - .1 *  SGN (BY - 80)
215  IF  ABS (DX) > 9 THEN DX = DX -  SGN (DX): GOTO 215
218  IF  ABS (DY) > 9 THEN DY = DY -  SGN (DY): GOTO 218
220  XDRAW BS AT BX,BY:BX = BX + DX:BY = BY + DY: IF BX > 20 AND BX < 265 AND BY > 20 AND BY < 160 THEN  XDRAW BS AT BX,BY: GOTO 200
230  IF (BX < 21 AND DX < 0) OR (BX > 264 AND DX > 0) THEN DX =  - DX: GOSUB 520
240  IF (BY < 21 AND DY < 0) OR (BY > 159 AND DY > 0) THEN DY =  - DY: GOSUB 520
250  XDRAW BS AT BX,BY: GOTO 200
300  REM === COLLISION CHECK ===
310  FOR X =  INT (BX / 10) - 1 TO  INT (BX / 10): FOR Y =  INT (BY / 10) - 1 TO  INT (BY / 10):SC = SC(X,Y): IF SC = 0 THEN 390
320  IF SC = 12 THEN  XDRAW 12 AT X * 10,Y * 10:SC(X,Y) = 0: GOSUB 500:SR = SR + 10: GOSUB 700: GOTO 390
330  IF SC = 9 THEN  GOSUB 550: VTAB 22: END 
340  IF SC = 11 THEN  XDRAW 11 AT X * 10,Y * 10:SC(X,Y) = 0: GOSUB 570:SR = SR + 20: GOSUB 700: GOTO 390
350  IF SC = 10 THEN  XDRAW 10 AT X * 10,Y * 10:SC(X,Y) = 0: GOSUB 590:SR = SR + 15: GOSUB 700: GOTO 390
360  IF SC = 8 THEN  GOSUB 610: VTAB 22: END 
390  NEXT Y,X: RETURN 
499  END 
500  REM === KILLED ANT SOUND ===
510  POKE 1,5: POKE 2,20: POKE 3,50: POKE 799,198: POKE 789,198: CALL 782: RETURN 
520  REM === BOUNCE SOUND ===
530  POKE 1,2: POKE 2,100: POKE 3,120: POKE 799,198: POKE 789,198: CALL 782: RETURN 
540  POKE 2,25: POKE 3,50: CALL 782: RETURN 
550  REM === HIT ROACH SOUND ===
560  POKE 1,3: POKE 2,50: POKE 3,1: POKE 799,230: POKE 789,230: CALL 782: RETURN 
570  REM === SMUDGE SOUND ===
580  POKE 1,3: POKE 2,100: POKE 3,150: POKE 799,198: POKE 789,198: CALL 782: RETURN 
590  REM === CRUMB SOUND ====
600  POKE 1,8: POKE 799,198: POKE 789,198: FOR I = 1 TO 4: POKE 2,25: POKE 3,30: CALL 782: NEXT : RETURN 
610  REM === DRAIN SOUND ===
620  POKE 1,10: POKE 2,150: POKE 3,200: POKE 4,50: CALL 837: RETURN 
700  REM === PRINT SCORE ===
710  VTAB 21: CALL  - 868: HTAB 20: INVERSE : PRINT "SCORE==>";SR: NORMAL : RETURN 
