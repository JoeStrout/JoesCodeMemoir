1 L = 16384: IF  PEEK (103) <  > L -  INT (L / 256) * 256 OR  PEEK (104) <  >  INT (L / 256) THEN  POKE L - 1,0: POKE 103,L -  INT (L / 256) * 256: POKE 104, INT (L / 256): PRINT  CHR$ (4)"RUNDOGGY NUMBERS
10  REM 

DOGGY NUMBERS



11  REM BY JOE STROUT



15  DEF  FN DX(P) = 70 + 30 * (P / 2 =  INT (P / 2)): DEF  FN DY(P) = 30 *  INT ((P + 1) / 2): DEF  FN BX(P) = 165 + 30 * (P / 2 =  INT (P / 2)): DEF  FN BY(P) = 40 + 15 *  INT ((P + 1) / 2)
20 L = 2048:L1(1) = L -  INT (L / 256) * 256:L2(1) =  INT (L / 256):D$ =  CHR$ (4): PRINT D$"BLOADDOGGY.SHAPES,A"L:L2 =  PEEK (43616) +  PEEK (43617) * 256 + L + 5
30  PRINT D$"BLOADNUMBERS.SHAPES,A"L2:L1(2) = L2 -  INT (L2 / 256) * 256:L2(2) =  INT (L2 / 256)
40  POKE 232,L1(1): POKE 233,L2(1)
45  VTAB 23: HTAB 8: PRINT "PRESS ANY KEY TO BEGIN:";: GET A$: HOME 
50  HGR : SCALE= 1: POKE 49234,0
60 LM = 4: REM  LAST MISS (A GOVERNOR)
70  GOSUB 5300:HX = 241:HY = 25: REM   DOG HOUSE X,Y 
100  REM 

  MAIN LOOP


110 ND =  INT (10 *  RND (1) + 1): IF ND > LM + 2 THEN 110
120 D = 1:P = 1: GOSUB 5010: REM  DRAW DOG
130  FOR N = 1 TO ND: HCOLOR= 3: GOSUB 5200: FOR J = 1 TO 200: NEXT : IF N < ND THEN  HCOLOR= 0: GOSUB 5200
140  NEXT : FOR I = 1 TO 500: NEXT :N = ND: FOR I = 1 TO ND: HCOLOR= 2: GOSUB 5200: FOR J = 1 TO 500: NEXT : HCOLOR= 3: GOSUB 5200: NEXT 
150  REM 

 GET INPUT


160  GET A$: IF A$ <  > " " AND A$ <  >  CHR$ (13) AND A$ <  >  CHR$ (27) THEN 160
170  IF A$ =  CHR$ (27) THEN  TEXT : HOME : PRINT "BYE-BYE!": END 
180  IF A$ =  CHR$ (13) THEN 250
190 NB = NB + 1: IF NB = 11 THEN 250
200 P = NB: GOSUB 5100: GOTO 150
250  REM 

  DONE WITH INPUT; MOVE DOGS
260 D = 1:P = 1: GOSUB 5010: FOR P = 1 TO ND: GOSUB 5010: NEXT 
270  FOR I = ND TO 1 STEP  - 1:S = 1:SP = 10
280 X1 =  FN DX(I):Y1 =  FN DY(I): IF I = ND AND I / 2 <  >  INT (I / 2) THEN B = I:X2 =  FN BX(B) - 8:Y2 =  FN BY(B): GOTO 300
290 B = I + (I / 2 <  >  INT (I / 2)) - (I / 2 =  INT (I / 2)):X2 =  FN BX(B) - 8:Y2 =  FN BY(B)
300  GOSUB 5400: REM  ^MOVE DOG TO BONE^
305  IF B > NB THEN 400
310  XDRAW 1 AT X1,Y1: XDRAW 2 AT X1,Y1:WAG =  INT (5 *  RND (1) + 1) * 2: FOR T = 1 TO WAG: XDRAW 4 AT X1 - 9,Y1 + 7: FOR J = 1 TO 100: NEXT J,T
320  XDRAW 2 AT X1,Y1:P = B: GOSUB 5100: XDRAW 7 AT X1,Y1:X2 = HX:S = 7: GOSUB 5400
330  FOR J = 1 TO 100: NEXT :Y2 = HY: GOSUB 5400: XDRAW 7 AT X1,Y1
340  NEXT I
350  GOTO 500
400  REM /// NO BONE FOUND!
410  XDRAW 1 AT X1,Y1: XDRAW 3 AT X1,Y1: FOR J = 1 TO 1000: NEXT : XDRAW 4 AT X1 - 9,Y1 + 7: FOR J = 1 TO 1000: NEXT 
420  XDRAW 4 AT X1 - 9,Y1 + 7:X2 =  FN DX(I):Y2 =  FN DY(I)
430 SP = 8:S = 3: GOSUB 5400: XDRAW 4 AT X1 - 9,Y1 + 7: GOTO 340
500  REM 

  DONE WITH SINGLE ROUND!
510  IF NB <  > ND THEN 550
520  IF ND =  > LM THEN LM = LM + 1
530  XDRAW 6 AT 139,75: FOR I = 1 TO 3000: NEXT 
550  HCOLOR= 0:N = ND: GOSUB 5200: POKE 232,L1(1): POKE 233,L2(1)
560  FOR I = 1 TO 10: DRAW 3 AT  FN DX(I), FN DY(I): DRAW 4 AT  FN DX(I) - 9, FN DY(I) + 7: DRAW 5 AT  FN BX(I), FN BY(I): NEXT 
580  DRAW 6 AT 139,75:NB = 0: GOTO 100
4999  END 
5000  REM 

SUBROUTINES


5010  REM 
 DRAW DOG #D AT POS. P
5020  POKE 232,L1(1): POKE 233,L2(1)
5030  XDRAW D AT  FN DX(P), FN DY(P): RETURN 
5100  REM 
 DRAW BONE AT POS. P
5110  POKE 232,L1(1): POKE 233,L2(1)
5120  XDRAW 5 AT  FN BX(P), FN BY(P): RETURN 
5200  REM 

 DRAW NUMBER #N IN PROPER POSITION
5210  POKE 232,L1(2): POKE 233,L2(2)
5220  IF N < 10 THEN  DRAW N AT 35,25: RETURN 
5230  DRAW 1 AT 24,25: DRAW 10 AT 35,25: RETURN 
5300  REM 

 DRAW DOGHOUSE
5310 X = 240: HCOLOR= 5: FOR Y = 1 TO 15: HPLOT X - Y * 2,Y TO X + Y * 2,Y: NEXT : REM  ROOF  
5320  FOR Y = 15 TO 39: IF Y / 5 =  INT (Y / 5) THEN  NEXT : GOTO 5340
5330  HPLOT 215,Y TO 265,Y: NEXT 
5340  HCOLOR= 0: FOR Y = 39 TO 25 STEP  - 3: DRAW 8 AT 257,Y: NEXT 
5350  RETURN 
5400  REM 
 MOVE SHAPE S FROM X1,Y1 TO X2,Y2 AT SPEED SP
5410  IF  ABS (X2 - X1) < SP THEN NX = X1 + (X2 - X1): GOTO 5430
5420 NX = X1 + SP *  SGN (X2 - X1)
5430  IF  ABS (Y2 - Y1) < SP THEN NY = Y1 + (Y2 - Y1): GOTO 5450
5440 NY = Y1 + SP *  SGN (Y2 - Y1)
5450  XDRAW S AT X1,Y1: XDRAW S AT NX,NY:X1 = NX:Y1 = NY: IF X1 <  > X2 OR Y1 <  > Y2 THEN 5410
5460  RETURN 
