5  DIM X(500),Y(500)
10  HOME : PRINT "ENTER CONTROL KEYS:": PRINT "UP=";: GET UP$: PRINT UP$: PRINT "LEFT=";: GET LF$: PRINT LF$
20  PRINT "RIGHT=";: GET RT$: PRINT RT$: PRINT "DOWN=";: GET DN$: PRINT DN$:LN = 25: GR 
30  HOME : COLOR= 15: HLIN 0,39 AT 0: HLIN 0,39 AT 39: VLIN 0,39 AT 0: VLIN 0,39 AT 39
40  FOR I = 2 TO LN:X(I) = 1:Y(I) = 1: NEXT :X(1) = 5:Y(1) = 38:HD = 1:TL = LN:DY =  - 1:DX = 0
50  HOME : VTAB 21: PRINT "LIVES:"3 - LV;: PRINT  TAB 30);"SCORE:"SC
60  GOSUB 500
90 MX = 20:MY = 20
100  REM 
110 K =  PEEK ( - 16384): IF K < 128 THEN 150
120 K$ =  CHR$ (K - 128): IF K$ = UP$ AND DY < 1 THEN DX = 0:DY =  - 1: GOTO 150
125  IF K$ = DN$ AND DY >  - 1 THEN DX = 0:DY = 1: GOTO 150
130  IF K$ = LF$ AND DX < 1 THEN DX =  - 1:DY = 0: GOTO 150
135  IF K$ = RT$ AND DX >  - 1 THEN DX = 1:DY = 0
150 H = HD:T = TL:HD = TL:TL = TL - 1 + (TL = 1) * LN
160  COLOR= 0: PLOT X(T),Y(T)
170 X(HD) = X(H) + DX:Y(HD) = Y(H) + DY: IF  SCRN X(HD),Y(HD)) <  > 0 THEN 300
180  COLOR= 6: PLOT X(HD),Y(HD)
190  REM   MOVE MOUSE
200  COLOR= 0: PLOT MX,MY
210  IF  RND (1) < .7 THEN 230
220 ZX =  INT (3 *  RND (1) - 1):ZY =  INT (3 *  RND (1) - 1)
230  IF  SCRN MX + ZX,MY + ZY) <  > 0 THEN 220
240 MX = MX + ZX:MY = MY + ZY: COLOR= 5: PLOT MX,MY
250  GOTO 100
300  REM 
310  IF  SCRN X(HD),Y(HD)) <  > 5 THEN 350
320 SC = SC + 1: VTAB 21: HTAB 36: PRINT SC:LN = LN + 2: GOSUB 410: COLOR= 6: PLOT MX,MY
322 Q =  INT (40 *  RND (1)):Z =  INT (40 *  RND (1)): IF  SCRN Q,Z) <  > 0 THEN 322
325  COLOR= 15: PLOT Q,Z
330 MX =  INT (40 *  RND (1)):MY =  INT (40 *  RND (1)): IF  SCRN MX,MY) <  > 0 THEN 330
340  COLOR= 5: PLOT MX,MY:X(LN - 1) = 1:X(LN) = 1:Y(LN - 1) = 1:Y(LN) = 1: GOTO 100
350  GOSUB 420: COLOR= 0: FOR I = 1 TO LN: PLOT X(I),Y(I): NEXT : PLOT MX,MY:LV = LV + 1: IF LV < 3 THEN 30
360  END 
400  REM    GULP SOUND
410  FOR I = 1 TO 10: POKE 768,230 - I * 4: POKE 769,5: CALL 770: NEXT 
415  RETURN 
420  REM   DEAD SOUND
430  FOR I = 50 TO 200 STEP 50: FOR J = I - 45 TO I + 45 STEP 5: POKE 768,J: POKE 769,5: CALL 770: NEXT J,I
435  RETURN 
500  REM  INIT SOUND
510  RESTORE : FOR I = 770 TO 790: READ B: POKE I,B: NEXT : RETURN 
520  DATA 173,48,192,136,208,5,206,1,3,240,9,202,208,245,174,0,3,76,2,3,96
