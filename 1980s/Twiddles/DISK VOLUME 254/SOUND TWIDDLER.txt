10  REM 

  SYNTH TWIDDLER


11  REM 
  BY JOSEPH J. STROUT
12  REM 
     JULY 1988


20  TEXT : HOME : PRINT  CHR$ (4)"PR#0": POKE 33,40
30 R$ =  CHR$ (13):D$ =  CHR$ (4): GOSUB 1000
40  PRINT 
50 TP$ = "S":P(4) = 20:P(6) = 10
60 A$ = "+----------------+": GOSUB 2800:A$ = "| SOUND TWIDDLER |": GOSUB 2800:A$ = "| by  Joe Strout |": GOSUB 2800:A$ = "+----------------+": GOSUB 2800
70  POKE 34,5
100  REM 
  MAIN LOOP
110  HOME : PRINT : GOSUB 2700
120  PRINT : PRINT "<H>EAR  <E>DIT  <R>ANDOM  <D>ISK  <Q>UIT"
130  PRINT "PLEASE SELECT AN OPTION: ";
140  GET A$: IF A$ <  > "H" AND A$ <  > "E" AND A$ <  > "R" AND A$ <  > "D" AND A$ <  > "Q" THEN 140
150  IF A$ = "Q" THEN  PRINT "QUIT": END 
160  IF A$ <  > "H" THEN 200
170  PRINT "HEAR": FOR L = 1 TO LP: FOR I = 0 TO 6: POKE I,P(I) + 256 * (P(I) < 0): NEXT 
180  CALL (835 - 28 * (TP$ = "N")): NEXT 
190  GOTO 100
200  IF A$ = "E" THEN  PRINT "EDIT": VTAB 7: GOSUB 2000: GOTO 100
210  IF A$ <  > "R" THEN 300
220  PRINT "RANDOM": FOR I = 0 TO 6
230  IF (I = 1 OR I = 3) AND  RND (1) < .7 THEN P(I) =  INT (21 *  RND (1) - 10): GOTO 260
240  IF I = 6 THEN P(I) =  INT (13 *  RND (1) + 1): GOTO 260
250 P(I) =  INT (256 *  RND (1))
260  NEXT 
270 P(4) = 20
280  GOTO 100
300  PRINT "DISK"
310  PRINT : PRINT "<C>ATALOG, <S>AVE, OR <L>OAD? ";
320  GET A$: IF A$ <  > "C" AND A$ <  > "S" AND A$ <  > "L" THEN 320
330  PRINT A$: IF A$ = "C" THEN  PRINT : PRINT D$"CATALOG": PRINT : PRINT "ANY KEY: ";: GET A$: GOTO 100
340  IF A$ <  > "S" THEN 400
350  PRINT "ENTER NAME (HIT RETURN TO CANCEL)": INPUT "==> ";A$
355  IF A$ = "" THEN 100
360  PRINT D$"OPEN"A$: PRINT D$"DELETE"A$: PRINT D$"OPEN"A$: PRINT D$"WRITE"A$
370  PRINT TP$: FOR I = 0 TO 6: PRINT P(I): NEXT : PRINT LP
380  PRINT D$"CLOSE": GOTO 100
400  PRINT "ENTER NAME (HIT RETURN TO CANCEL)": INPUT "==> ";A$
405  IF A$ = "" THEN 100
410  ONERR  GOTO 500
420  PRINT D$"VERIFY"A$: POKE 216,0
430  PRINT D$"OPEN"A$: PRINT D$"READ"A$
440  INPUT TP$: FOR I = 0 TO 6: INPUT P(I): NEXT : INPUT LP
450  PRINT D$"CLOSE": GOTO 100
500  PRINT : PRINT "FILE NOT FOUND": PRINT : PRINT "ANY KEY: ";: GET A$: POKE 216,0: GOTO 100
999  END 
1000  REM  INIT    SYNTH    ROUTINE
1010 L = 768
1020  READ A: IF A < 256 THEN  POKE L,A:L = L + 1: GOTO 1020
1030  DATA 169,20
1031  DATA 56,229,4,24,101,7,133,7,165,6
1032  DATA 133,8,173,48,192,164,4,136,208,253
1033  DATA 173,48,192,166,7,202,208,253,198,6
1034  DATA 208,236,165,8,133,6,96,173,48,192
1035  DATA 164,4,136,208,253,173,48,192,164,5
1036  DATA 185,0,240,24,101,7,170,202,208,253
1037  DATA 198,5,208,229,96,165,0,133,7,32
1038  DATA 0,3,165,1,24,101,0,133,0,165
1039  DATA 2,133,7,32,0,3,165,3,24,101
1040  DATA 2,133,2,198,5,208,224,96
1041  DATA 999
1999  RETURN 
2000  REM 
  INPUT DATA

2010  PRINT "TYPE (SOUND/NOISE): ";: IF TP$ = "N" THEN  PRINT "NOISE";: GOTO 2020
2015  PRINT "SOUND";
2020  HTAB  PEEK (36) - 4
2030  GET A$: IF A$ <  > "S" AND A$ <  > "N" AND A$ <  > R$ THEN 2030
2040  IF A$ = R$ THEN  PRINT : GOTO 2050
2042 TP$ = A$: IF A$ = "S" THEN  PRINT "SOUND": GOTO 2050
2045  PRINT "NOISE"
2050 D = P(0): PRINT "FREQUENCY ONE: ";: GOSUB 2900:P(0) = A
2060 D = P(1): PRINT "   SLIDE RATE: ";: GOSUB 2900:P(1) = A
2070 D = P(2): PRINT "FREQUENCY TWO: ";: GOSUB 2900:P(2) = A
2080 D = P(3): PRINT "   SLIDE RATE: ";: GOSUB 2900:P(3) = A
2090 D = P(4): PRINT "       VOLUME: ";: GOSUB 2900:P(4) = A
2100 D = P(5): PRINT "     DURATION: ";: GOSUB 2900:P(5) = A
2105 D = P(6): PRINT "         RATE: ";: GOSUB 2900:P(6) = A
2110 D = LP: PRINT "        LOOPS: ";: GOSUB 2900:LP = A
2199  RETURN 
2700  REM   PRINT DATA
2710  PRINT "TYPE (SOUND/NOISE): ";: IF TP$ = "N" THEN  PRINT "NOISE": GOTO 2720
2715  PRINT "SOUND"
2720  PRINT "FREQUENCY ONE: "P(0): PRINT "   SLIDE RATE: "P(1): PRINT "FREQUENCY TWO: "P(2)
2730  PRINT "   SLIDE RATE: "P(3): PRINT "       VOLUME: "P(4): PRINT "     DURATION: "P(5): PRINT "         RATE: "P(6): PRINT "        LOOPS: "LP
2740  RETURN 
2800  REM   CENTER STRING A$
2810  HTAB 21 -  LEN (A$) / 2: PRINT A$: RETURN 
2899  END 
2900  REM  INPUT 0-255 W/DEFAULT
2910 H =  PEEK (36) + 1: PRINT D;: HTAB H
2920  INPUT "";A$: VTAB  PEEK (37): IF A$ = "" THEN A$ =  STR$ (D)
2930 A =  VAL (A$): HTAB H: PRINT A; SPC( 10): IF A >  - 129 AND A < 256 THEN  PRINT : RETURN 
2940  HTAB 20: PRINT "<-- MUST BE 0 TO 255";: HTAB H: GOTO 2920
