1  PRINT  CHR$ (4)"MAXFILES2"
10  REM 


  GRUNT.SCORES


15  DIM NM$(12),SC(12)
20 SR =  PEEK (8150) +  PEEK (8151) * 256:D$ =  CHR$ (4)
30  ONERR  GOTO 500
40  PRINT D$"VERIFYGRNT.HISCRS"
50  POKE 216,0: PRINT D$"OPENGRNT.HISCRS": PRINT D$"READGRNT.HISCRS"
60  INPUT NS: FOR I = 1 TO NS
70  INPUT NM$(I): INPUT SC(I)
80  NEXT : PRINT D$"CLOSE"
90 PP = 0: FOR I = NS TO 1 STEP  - 1: IF SR =  > SC(I) THEN NM$(I + 1) = NM$(I):SC(I + 1) = SC(I):PP = I
100  NEXT 
110  TEXT : HOME : IF  NOT PP THEN 150
120  PRINT : INVERSE : PRINT "CONGRATULATIONS!";: NORMAL : PRINT "  YOUR SCORE IS WITHIN"
130  PRINT : PRINT "THE TOP TEN ALL-TIME SCORES!"
140  PRINT : PRINT : PRINT "ENTER YOUR NAME, FOR THE HISTORY BOOKS:": INPUT "==>";NM$(PP):NM$(PP) =  LEFT$ (NM$(PP),21):SC(PP) = SR
150  HOME : PRINT : HTAB 15: PRINT "G.R.U.N.T.": PRINT : HTAB 15: PRINT "HIGH SCORES": HTAB 15: PRINT "---- ------"
160  PRINT : PRINT : FOR I = 1 TO 10: HTAB 5: IF I = 1 THEN  INVERSE 
170  PRINT I". "NM$(I); TAB 35 -  LEN ( STR$ (SC(I))));SC(I): IF I = 1 THEN  NORMAL : PRINT 
180  PRINT : NEXT 
190  IF PP THEN  GOSUB 600
200  VTAB 24: HTAB 4: PRINT "<< PRESS ANY KEY TO CONTINUE >>";: WAIT 49152,128: GET A$:
210  POKE 34,13: HOME 
220  PRINT : PRINT "WOULD YOU LIKE TO PLAY AGAIN (Y/N) ? ";
230  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 230
240  IF A$ = "Y" THEN  PRINT : PRINT D$"RUNGRUNT"
250  PRINT : END : REM  GOTO SOFTDISK MENU FROM HERE
499  END 
500  REM   FILE DOESN'T EXIST
510  FOR I = 1 TO 10: READ NM$(I),SC(I): NEXT 
520 NS = 10: GOTO 90
530  DATA MORTEMENT BOUCHER,120,HAL ITOSIS,105
540  DATA GARSHF,100,BEN DOVER,90
550  DATA WHA SISNAIM,85,FINN AGLE,77
560  DATA JOE COSE,70,JOCK ULARITY (JOE'S BRO),60
570  DATA LIGHTFOOT,43,WIM POE,12
600  REM 
  SAVE NEW HIGH SCORES

610  PRINT D$"OPENGRNT.HISCRS": PRINT D$"CLOSE": PRINT D$"OPENGRNT.HISCRS": PRINT D$"WRITEGRNT.HISCRS"
620  PRINT "10"
630  FOR I = 1 TO 10: PRINT NM$(I): PRINT SC(I)
640  NEXT 
650  PRINT D$"CLOSE": RETURN 
