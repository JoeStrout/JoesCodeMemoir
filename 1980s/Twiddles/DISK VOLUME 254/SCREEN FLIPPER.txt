10  REM 

  SCREEN FLIPPER


20  TEXT : HOME :D$ =  CHR$ (4):SH = 25000: SCALE= 1: ROT= 0
30  PRINT D$"BLOAD DRAWING.SHAPES,A"SH: REM  USE DIFFERENT TABLE LATER
32  GOSUB 1000
35  POKE 49232,0: POKE 49234,0: POKE 49236,0: POKE 49239,0
40  POKE 232,SH -  INT (SH / 256) * 256: POKE 233, INT (SH / 256)
100  FOR X = 0 TO 139: FOR Y = 0 TO 191: XDRAW 1 AT X,Y:CR = 3 * ( PEEK (234) = 0): XDRAW 1 AT 279 - X,Y:CL = 3 * ( PEEK (234) = 0): HCOLOR= CL: DRAW 1 AT X,Y: HCOLOR= CR: DRAW 1 AT 279 - X,Y: NEXT Y,X
150  POKE 49235,0: HOME : VTAB 22: HTAB 18: INVERSE : PRINT "DONE!": NORMAL 
160  PRINT : PRINT "PICTURE IS ON HI-RES PAGE 1.": PRINT "PRESS <S> TO SAVE, OR <ESC> TO END:";
170  GET A$: IF A$ <  > "S" AND A$ <  >  CHR$ (27) THEN 170
180  IF A$ = "S" THEN  PRINT "SAVE": INPUT "ENTER NEW NAME==>";F$: IF F$ <  > "" THEN  PRINT D$"BSAVE"F$",A$2000,L$2000"
190 A$ = "S": PRINT : PRINT "TYPE <GOTO 180> IF YOU DECIDE TO SAVE."
1000  REM 
 START-UP
1010  HOME : PRINT : PRINT : HTAB 14: INVERSE : PRINT "SCREEN FLIPPER": NORMAL : PRINT : PRINT 
1020  PRINT "IS PICTURE ALREADY LOADED? ";
1030  GET A$: IF A$ <  > "Y" AND A$ <  > "N" AND A$ <  >  CHR$ (27) THEN 1030
1035  IF A$ =  CHR$ (27) THEN  PRINT "ESC... END.": END 
1040  IF A$ = "N" THEN  PRINT "NO": PRINT : INPUT "ENTER NAME OF PICTURE==>";F$: PRINT D$"BLOAD"F$",A$2000": PRINT : PRINT 
1050  IF A$ = "Y" THEN  PRINT "YES"
1060  VTAB 24: POKE 49232,0: POKE 49235,0: POKE 49236,0: POKE 49239,0: PRINT "IS THIS IT? ";
1070  GET A$: IF A$ <  > "Y" AND A$ <  > "N" AND A$ <  >  CHR$ (27) THEN 1070
1075  IF A$ =  CHR$ (27) THEN  PRINT "ESC... END.": END 
1080  IF A$ = "Y" THEN  PRINT "YES": RETURN 
1090  IF A$ = "N" THEN  PRINT "NO": GOTO 1000
