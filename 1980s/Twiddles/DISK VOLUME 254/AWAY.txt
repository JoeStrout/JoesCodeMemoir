1  GOTO 10
5  PRINT  CHR$ (4)"SAVEAWAY": END 
10  REM 

 AWAY



11  REM 

  (VARIABLE SETUP PROGRAM)


20  TEXT : HOME 
25  DIM WD$(150),WT(150),WM(150),RN$(57),NV(57),DR(57,10),OB$(50),OS(50),OL(50),OT(50),OV(50),OW(50),O1(50),O2(50)
26  DIM CN$(30),CD(30),CR(30),CS(30),CM(30),CH(30),CA(30),CO(30),CP(15),SP(20)
50  GOSUB 100: PRINT D$"RUNPARSER"
100  REM 

  **** INITIALIZE ***


105 TW = 0:D$ =  CHR$ (4):QT$ =  CHR$ (34)
110  READ WD$(TW + 1): IF WD$(TW + 1) <  > "endlist" THEN TW = TW + 1: READ WT(TW): READ WM(TW): GOTO 110
118  REM 
  VERBS


120  DATA N,1,3,S,1,3,E,1,3,W,1,3,GET,1,1,I,1,4,ATTACK,1,20,LOOK,1,5,Z,1,17,WAIT,1,17,CAST,1,21,WEAR,1,6,AT,3,5,AND,4,1,U,1,3,D,1,3,IT,2,0
122  DATA DROP,1,2,GO,1,3,NORTH,1,3,SOUTH,1,3,EAST,1,3,WEST,1,3,NE,1,3,SE,1,3,SW,1,3,NW,1,3,NORTHEAST,1,3,SOUTHEAST,1,3,SOUTHWEST,1,3,NORTHWEST,1,3
123  DATA UP,1,3,DOWN,1,3,DN,1,3,INVENTORY,1,4,EXAMINE,1,5,DON,1,6,REMOVE,1,7
124  DATA UNLOCK,1,8,FREE,1,9,SELL,1,10,BRIEF,1,11,SUPERBRIEF,1,11,VERBOSE,1,11
125  DATA BUY,1,12,SAY,1,13,CHANT,1,13,GIVE,1,14,EAT,1,15,DRINK,1,15,CONSUME,1,15,READ,1,16
126  DATA READY,1,18,DIAGNOSE,1,19,STATUS,1,19
127  DATA FIGHT,1,20,INVOKE,1,21,PUT,1,22,VOLUME,1,23,SOUND,1,23
130  DATA TAKE,1,1
140  DATA ALL,2,0,THEN,4,2
150  DATA ON,3,1,IN,3,2,INSIDE,3,2,INTO,3,2,TO,3,3,BETWEEN,3,4,FROM,3,6,WITH,3,7
180  DATA A,5,1,AN,5,1,THE,5,1,HER,5,2,MY,5,3,HIS,5,4,THEIR,5,5
200  DATA MALHALITOS,6,1,SERVOME,6,2,ZAPPO,6,3,MEDIHEP,6,6,ZAZUP,6,7,ZIPPO,6,8,PORTAMAZ,6,9,KOPPAUT,6,10
210  DATA POOLAY,6,11,NUDAMON,6,12,CHAUBAIBZ,6,13,FROOD,6,14,EPHOKE,6,16,SEZME,6,17,ANIMUS,6,18
220  DATA DOOR,2,0,GATE,2,0
299  DATA endlist
300  REM     SCREEN SET-UP
310  PRINT D$"PR#3": PRINT : HOME : INVERSE : PRINT  TAB 5)"<< Twin Gates >> by Daniel French";: PRINT  TAB 44);"Experience: 0"; TAB 64)"Moves: 0"; TAB 79)" ": NORMAL 
320  POKE 34,1:RD$ = "ROOM DESC":OD$ = "OBJ DESC":CD$ = "CHAR DESC":EF$ = "EFFECTS":SW = 80
330 RM = 1:ST$ = "You feel fine.":MC = 5
340 R = 1:F$ = EF$: GOSUB 5100: PRINT 
350 MR$ = "[ MORE ]":RM = 1
400  FOR D = 1 TO 10: READ DI$(D): NEXT : DATA north,east,south,west,northeast,southeast,southwest,northwest,up,down
500  REM   LOAD INFO
510  PRINT D$"OPENROOM INFO": PRINT D$"READROOM INFO":NR = 1
530  INPUT RN$(NR): IF RN$(NR) = "END" THEN 550
540  FOR D = 1 TO 10: INPUT DR(NR,D): NEXT :NR = NR + 1: IF NR < 60 THEN 530
550 NR = NR - 1: PRINT D$"CLOSEROOM INFO": PRINT D$"OPENOBJ INFO": PRINT D$"READOBJ INFO"
560  INPUT NO: FOR I = 1 TO NO
570  INPUT OB$(I): INPUT OL(I): INPUT OT(I): INPUT OV(I): INPUT OW(I): INPUT O1(I): INPUT O2(I)
580  NEXT : PRINT D$"CLOSEOBJ INFO"
590  PRINT D$"OPENCHAR INFO": PRINT D$"READCHAR INFO"
600  INPUT NC: FOR I = 1 TO NC
610  INPUT CN$(I): INPUT CD(I): INPUT CR(I): INPUT CM(I): INPUT CH(I): INPUT CA(I): INPUT CO(I)
615  IF CR(I) = 1 THEN CP = CP + 1:CP(CP) = I
620  NEXT : PRINT D$"CLOSECHAR INFO":EN = 0
630  PRINT D$"OPENPLAYER": PRINT D$"READPLAYER"
640  INPUT CN$: INPUT RC,CS,AG,HE: INPUT HN,HP,AC: INPUT ST,IN,DX,CN,CH: INPUT EX,LV
650  INPUT GP,GB: INPUT WA(1),WA(2),WA(3),WA(4),WA(5): INPUT SA(1),SA(2),SA(3),SA(4): INPUT NP
660  IF  NOT NP THEN 700
670  FOR I = 1 TO NP:NO = NO + 1: INPUT OB$(NO),OT(NO),OV(NO),OW(NO),O1(NO),O2(NO)
680 OL(NO) = 101: IF OT(NO) = 2 THEN OL(NO) = 102:EN = EN + O2(NO)
685  IF OT(NO) = 8 THEN RW = NO
690  NEXT 
700  FOR I = 1 TO 20: INPUT SP(I): NEXT 
710  PRINT D$"CLOSEPLAYER": IF LV < 1 THEN 780
720  PRINT : PRINT "As you step towards the Gates, Joe comes out from the caves and tells you that  this adventure is for level 0 characters only.": ONERR  GOTO 730
730  PRINT : PRINT : HTAB 28: PRINT "Insert the Shire Main Disk.": HTAB 27: PRINT "Press <C> to continue: ";
740  GET A$: IF A$ <  > "C" THEN 740
750  PRINT "Continue": PRINT : PRINT D$"RUN BACK"
760  END 
780  FOR I = 1 TO NO: FOR J = 1 TO  LEN (OB$(I)): IF  MID$ (OB$(I),J,1) <  > " " THEN  NEXT J,I: GOTO 800
790 TW = TW + 1:WD$(TW) =  LEFT$ (OB$(I),J - 1):WT(TW) = 7:WM(TW) = I: NEXT J,I
800  FOR I = 1 TO NC: FOR J = 1 TO  LEN (CN$(I)): IF  MID$ (CN$(I),J,1) <  > " " THEN  NEXT J,I: GOTO 830
810 TW = TW + 1:WD$(TW) =  LEFT$ (CN$(I),J - 1):WT(TW) = 7:WM(TW) = I: NEXT J,I
830  REM 
840 MC = 9 +  INT (ST / 5)
900  GOSUB 5200:NV(RM) = 1: PRINT 
910 SR(2) = 20:CR = NP:VM = 20
920  FOR I = 1 TO NP: FOR J = 1 TO NO: IF OB$(NO + I) <  > OB$(J) THEN 950
930 OB$(NO + I) = OB$(NO + I) + "#"
950  NEXT J,I
1000  REM  INIT    SYNTH    ROUTINE
1010 L = 768
1020  READ A: IF A < 256 THEN  POKE L,A:L = L + 1: GOTO 1020
1030  DATA 169,20
1031  DATA 56,229,4,24,101,7,133,7,165,6
1032  DATA 133,8,173,48,192,164,4,136,208,253
1033  DATA 173,48,192,166,7,202,208,253,198,6
1034  DATA 208,236,165,8,133,6,96,173,48,192
1035  DATA 164,4,136,208,253,173,48,192,164,5
1036  DATA 185,0,240,24,101,7,170,202,208,253
1037  DATA 198,5,208,229,96,165,0,133,7,32
1038  DATA 0,3,165,1,24,101,0,133,0,165
1039  DATA 2,133,7,32,0,3,165,3,24,101
1040  DATA 2,133,2,198,5,208,224,96
1041  DATA 999
1999  RETURN 
5000  REM 

  SUBROUTINES


5001  REM 
  PRINT STRING A$
5010 H =  PEEK (36) + 1: IF  LEN (A$) < SW - H THEN  PRINT A$;:SC = SC + 1: GOTO 5050
5020  FOR X = SW - H TO 1 STEP  - 1: IF  MID$ (A$,X,1) <  > " " THEN  NEXT 
5030  PRINT  LEFT$ (A$,X - 1):A$ =  MID$ (A$,X + 1):SC = SC + 1: GOTO 5010
5050  IF  NOT NL THEN  RETURN 
5060  FOR I = 1 TO NL: PRINT :SC = SC + 1: NEXT : RETURN 
5100  REM 
  PRINT REC  R  OF FILE  F$
5110  PRINT : PRINT D$"OPEN"F$",L207"
5120 H =  PEEK (1403) + 1: PRINT : PRINT D$"READ"F$",R"R: INPUT A$: VTAB  PEEK (37): HTAB H: IF  RIGHT$ (A$,1) = "@" THEN 5140
5130  GOSUB 5000: PRINT : PRINT D$"CLOSE"F$: RETURN 
5140  REM 
5190 R =  VAL ( MID$ (A$, LEN (A$) - 2)):A$ =  LEFT$ (A$, LEN (A$) - 3): GOSUB 5000: GOTO 5120
5200  REM 
  DESCRIBE ROOM RM
5210 R = RM:F$ = RD$: GOSUB 5100
5220  FOR O = 1 TO NO: IF OL(O) <  > RM THEN  NEXT : GOTO 5270
5230 OS(O) = 1:R = O:F$ = OD$: GOSUB 5100
5260  NEXT 
5270  FOR I = 1 TO NC: IF CR(I) <  > RM THEN  NEXT : RETURN 
5280 CS(I) = 1:R = I:F$ = CD$: GOSUB 5100
5290  NEXT : RETURN 
50000  REM 


    VARIABLE LIST:
50005  REM ==== PLAYER DATA ====
50010  REM  RM = CURRENT ROOM #
50015  REM  ST$ = STATUS (HEALTH)
50018  REM  GP = GOLD PIECES
50020  REM  CR = # OF OBJECTS BEING CARRIED
50025  REM  MC = MAX. # OF OBJECTS YOU CAN CARRY
50030  REM  SN, SD = SCORE, # OF MOVES (Numerator, Denominator)
50050  REM ==== ROOM DATA ====
50055  REM  NR = # OF ROOMS
50058  REM  RN$(R) = ROOM NAME
50060  REM  DR(R,1-10) = DOORS (0=NONE, 1-100=NORMAL)
50061  REM            = -# IF SECRET
50062  REM            = 100+ROOM IF LOCKED
50064  REM            = 200+EFFECT FOR SPECIAL THINGS
50065  REM  DIRECTION ORDER = N,E,S,W,NE,SE,SW,NW,U,D
50100  REM ==== OBJECT DATA ====
50103  REM  NO = # OF OBJECTS
50105  REM  OB$(O) = OBJECT NAME
50110  REM  OL(O) = OBJECT'S LOCATION
50111  REM        = 0 IF NOT IMMEDIATELY ACCESSABLE
50112  REM        = 1-100 IF LYING OPENLY IN ROOM
50113  REM        = -1to-100 IF HIDDEN IN ROOM
50114  REM        = 101 IF CARRIED BY PLAYER
50115  REM        = 102 IF BEING WORN
50116  REM        = 200+N IF CONTAINED IN OBJECT N
50117  REM        = 300+N IF CARRIED BY CHARACTER N
50125  REM  OT(O) = OBJECT TYPE
50126  REM        = 0 IF NORMAL
50127  REM        = 1 IF CONTAINER
50128  REM        = 2 IF GARMENT (WEARABLE)
50129  REM        = 3 IF READABLE
50130  REM        = 4 IF FOOD / DRINK
50131  REM        = 5 IF LIQUID (ELIXER, VENOM, ETC.)
50132  REM        = 6 IF KEY
50133  REM        = 7 IF BOUND CHARACTER
50134  REM          > 10 IF SELLABLE
50135  REM  OW(O) = OBJECT WEIGHT/SIZE
50140  REM  OI(O) = OBJECT INFO
50141  REM        = CAPACITY OF CONTAINER
50142  REM        = RECORD NUMBER FOR READABLE OBJECTS
50143  REM        = SIPS/BITES LEFT OF FOOD, DRINK
50144  REM        = LOCATION OF DOOR FOR KEY
50145  REM        = KEY # FOR BOUND CHARS
50150  REM ==== CHARACTER DATA ====
50151  REM  (NOT TO BE CONFUSED WITH PLAYER)
50155  REM  CN$(C) = CHARACTER NAME
50160  REM  CD(C) = CHARACTER DISPOSITION
50161  REM     = 0 IF IMPARTIAL
50162  REM     = 1 IF LOYAL FRIEND
50163  REM     = 2 IF UNFRIENDLY
50170  REM  CR(C) = CHARACTER'S PRESENT ROOM #
50200  REM ==== OTHER DATA ====
50205  REM  TW = TOTAL WORDS KNOWN BY PARSER
50210  REM  V$,S$ = VERB, SUBJECT
50220  REM  P$,OP$ = PREPOSITION, OBJECT
50230  REM  SR(A) = 0 IF ACTION A NOT SCORED YET;
