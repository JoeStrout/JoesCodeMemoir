1  PRINT  CHR$ (4)"MAXFILES1"
5  HOME : IF  PEEK (103) <  > 1 OR  PEEK (104) <  > 64 THEN  POKE 16384,0: POKE 103,1: POKE 104,64: PRINT "ONE SEC...": PRINT  CHR$ (4)"RUN GRUNT"
10  REM 


G.R.U.N.T.



20  DIM SC(3,28,18),MS(10),MX(10),MY(10),SM(4,10),XM(4,10),YM(4,10)
30  GOSUB 30000: REM  START-UP
99  GOTO 500
100  REM 

   SYSTEM ROUTINES



110  REM   CENTER STRING A$
120  HTAB 20 -  LEN (A$) / 2: PRINT A$: RETURN 
150  REM    DRAW CENTERED BOX: WIDTH=W, HEIGHT-2=H
160 Q = 20 - W / 2:Z = 19 + W / 2: INVERSE : HTAB Q: PRINT  SPC( W): PRINT : FOR I = 1 TO H: HTAB Q: PRINT " ";: HTAB Z: PRINT " ": NEXT 
170  HTAB Q: PRINT  SPC( W): NORMAL : RETURN 
200  REM   REMOVE MONSTER Z
210  HCOLOR= 0: ON MS(Z) - 6 GOSUB 5010,5050
220  ROT= 0: DRAW MS(Z) AT MX(Z) * 10,MY(Z) * 10:SC(BD,MX(Z),MY(Z)) = 0:MX(Z) = MX(NM):MY(Z) = MY(NM):MS(Z) = MS(NM)
230 NM = NM - 1: HCOLOR= 3: VTAB 21: HTAB 8: PRINT SR: RETURN 
500  REM 

   MAIN LOOP
510  ON JS + 1 GOSUB 1000,900: IF  NOT NM THEN  FOR J = 1 TO 10: NEXT : GOTO 510
520 M = M + 1: IF M > NM THEN M = 1
530  GOSUB 2000: GOTO 510
900  REM 
   JOYSTICK CONTROL
901  REM  (INTERCEPT ROUTINE)
910 DX =  INT ( PDL (0) / 86) - 1:DY =  INT ( PDL (1) / 86) - 1: IF DX AND DY THEN DY = 0
920  IF DX OR DY THEN NR = 32 * (DY = 1) + 48 * (DX =  - 1) + 16 * (DX = 1): GOTO 1200
930  IF  PEEK (49249) > 127 THEN 1500
940  IF  PEEK (49250) > 127 THEN 1400
950  IF  PEEK (49152) - 128 = 27 THEN  GET K$: GOTO 1100
960  RETURN 
1000  REM   

ACTION ROUTINES


1010  REM 
  GET KEYPRESS + EXECUTE COMMAND
1020 K =  PEEK (49152): IF K < 128 THEN  RETURN 
1030 K$ =  CHR$ (K - 128): POKE 49168,0
1040  IF K$ = UP$ THEN NR = 0:DX = 0:DY =  - 1: GOTO 1200
1050  IF K$ = DN$ THEN NR = 32:DX = 0:DY = 1: GOTO 1200
1060  IF K$ = LF$ THEN NR = 48:DX =  - 1:DY = 0: GOTO 1200
1070  IF K$ = RT$ THEN NR = 16:DX = 1:DY = 0: GOTO 1200
1080  IF K$ = MG$ THEN 1400
1090  IF K$ = FR$ THEN 1500
1100  IF K$ =  CHR$ (27) THEN  HOME : VTAB 22:A$ = "PRESS ANY KEY": INVERSE : GOSUB 110: VTAB 21:W =  LEN (A$) + 2:H = 1: GOSUB 150: WAIT 49152,128: GET A$: GOTO 3010
1190  RETURN 
1200  REM 

   MOVE PLAYER


1210  IF NR <  > PR THEN DX = 0:DY = 0
1220  IF  NOT SC(BD,PX + DX,PY + DY) THEN  ROT= PR: XDRAW 5 + PS AT PX * 10 + 5,PY * 10 + 5: ROT= NR:PS =  NOT PS:PX = PX + DX:PY = PY + DY: XDRAW 5 + PS AT PX * 10 + 5,PY * 10 + 5:PR = NR: RETURN 
1230 S = SC(BD,PX + DX,PY + DY): IF S < 0 THEN 1600
1250  IF S = 7 THEN 1700
1260  IF S = 3 THEN 1800
1290 DX = 0:DY = 0: GOTO 1220: REM  WALL,DEMON, OR UNKNOWN OBSTACLE
1400  REM 
 MAGIC SPELL
1410  IF  NOT NM OR  NOT MP THEN  RETURN 
1420 MP = MP - 1: VTAB 23: HTAB 16: PRINT MP" "
1430 NZ =  INT (NM *  RND (1) + 1): IF (NZ < 2 AND NM > 1) OR (NZ = NM AND NM > 2) THEN 1430: REM  # OF MONS ZAPPED 
1440  HCOLOR= 5: GOSUB 1490: GOSUB 5550
1450  FOR Z = 1 TO NZ:SR = SR + 5: GOSUB 200: NEXT : REM  REMOVE 'EM
1460 NM = NM * (NM =  > 0): RETURN 
1490  FOR I = 1 TO NZ: DRAW MS(I) AT MX(I) * 10,MY(I) * 10: NEXT : RETURN 
1500  REM  
FIRE CROSSBOW
1510  IF  NOT CB THEN  RETURN 
1520 CB = CB - 1: VTAB 23: HTAB 38: PRINT CB" ";
1530  ROT= PR: XDRAW 5 + PS AT PX * 10 + 5,PY * 10 + 5: XDRAW 12 AT PX * 10 + 5,PY * 10 + 5: REM  DRAW BOW
1535  GOSUB 5600
1540 DX = (PR = 16) - (PR = 48):DY = (PR = 32) - (PR = 0):AX = PX + DX:AY = PY + DY
1550 S = SC(BD,AX,AY): IF  NOT S THEN AX = AX + DX:AY = AY + DY: GOTO 1550
1560  IF S < 7 OR  RND (1) < LV / 20 THEN  GOSUB 5650: GOTO 1590
1570  FOR Z = 1 TO NM: IF MX(Z) = AX AND MY(Z) = AY THEN SR = SR + 10 + 5 * (MS(Z) - 7): GOSUB 200:Z = NM + 1: ROT= PR
1580  NEXT Z
1590  XDRAW 12 AT PX * 10 + 5,PY * 10 + 5: XDRAW 5 + PS AT PX * 10 + 5,PY * 10 + 5
1595  RETURN 
1600  REM 
 NEXT BOARD
1610  ROT= PR: XDRAW 5 + PS AT PX * 10 + 5,PY * 10 + 5: POKE SN,(BD + 1) * 32: CALL PUTPIC
1613  IF S =  - 5 THEN 20000
1615 NM(BD) = NM: FOR I = 1 TO NM:SM(BD,I) = MS(I):XM(BD,I) = MX(I):YM(BD,I) = MY(I): NEXT 
1620 BD =  - S - 1:PX = PX * (PX > 2 AND PX < 24) + (PX > 24) + 25 * (PX < 2):PY = PY * (PY > 2 AND PY < 12) + (PY > 12) + 14 * (PY < 2)
1625  IF QQ THEN PY = 16
1630  POKE SN,(BD + 1) * 32: CALL BRINGPIC: XDRAW 5 + PS AT PX * 10 + 5,PY * 10 + 5
1640 NM = NM(BD): FOR I = 1 TO NM:MS(I) = SM(BD,I):MX(I) = XM(BD,I):MY(I) = YM(BD,I): NEXT 
1699  RETURN 
1700  REM 
  HAND-TO-HAND COMBAT
1710 NX = PX + DX:NY = PY + DY: FOR Z = 1 TO NM: IF MX(Z) <  > NX OR MY(Z) <  > NY THEN  NEXT : RETURN 
1720  IF  RND (1) < LV / 10 AND  RND (1) < .8 THEN DX = 0:DY = 0: GOTO 1220
1730 SR = SR + 15 + 5 * (MS(Z) - 7): GOTO 200
1800  REM 
  TREASURE CHEST!
1810 R =  INT (5 *  RND (1) + 1): ON R GOTO 1820,1830,1840,1850,1860
1820 A$ = "ASSORTED TREASURE!":SR = SR + 50 +  INT (50 *  RND (1) + 1): GOTO 1870
1830 A$ = "FOOD AND DRINK!":LF = LF + 20 +  INT (30 *  RND (1) + 1):SR = SR + 20: GOTO 1870
1840 A$ = "MAGIC POTION!!":MP = MP + 1:SR = SR + 30: GOTO 1870
1850 A$ = "CROSSBOW BOLTS!":CB = CB + 5 +  INT (10 *  RND (1) + 1):SR = SR + 25: GOTO 1870
1860 A$ = "EMPTY."
1870  HOME : VTAB 22: GOSUB 100:W =  LEN (A$) + 2:H = 1: VTAB 21: GOSUB 150
1880  GOSUB 5500: ROT= 0: XDRAW 3 AT (PX + DX) * 10,(PY + DY) * 10:SC(BD,PX + DX,PY + DY) = 0
1890  GOSUB 3000: GOTO 1210
2000  REM 

  MOVE MONSTER  M
2010 DX =  SGN (PX - MX(M)):DY =  SGN (PY - MY(M)): IF  NOT DX AND  NOT DY THEN 2100
2015  IF MX(M) + DX = PX AND MY(M) + DY = PY THEN 2100
2020  IF SC(BD,MX(M) + DX,MY(M) + DY) THEN DX =  INT (3 *  RND (1) - 1):DY =  INT (3 *  RND (1) - 1): IF SC(BD,MX(M) + DX,MY(M) + DY) THEN  RETURN 
2030 SC(BD,MX(M),MY(M)) = 0: ROT= 0
2040  FOR Z = 1 TO 2:OX = MX(M):OY = MY(M):MX(M) = MX(M) + DX / 2:MY(M) = MY(M) + DY / 2: XDRAW MS(M) AT MX(M) * 10,MY(M) * 10: XDRAW MS(M) AT OX * 10,OY * 10: NEXT 
2050 SC(BD,MX(M),MY(M)) = MS(M): RETURN 
2100  REM 

  ATTACK PLAYER!  NAIL 'EM!
2110 LF = LF - 5 - 5 * (MS(M) - 7) -  INT (LV *  RND (1)): IF LF < 0 THEN LF = 0
2120  VTAB 21: HTAB 37: PRINT LF"  ": IF LF = 0 THEN  GOSUB 5700: POP : GOTO 25000
2130  GOTO 5700
3000  REM 

  DISPLAY ROUTINES
3010  REM 

  TEXT WINDOW DISPLAY
3020  HOME : VTAB 21: PRINT "SCORE: "SR;: HTAB 25: PRINT "LIFE FORCE: ";LF: VTAB 23: PRINT "MAGIC POTIONS: "MP;: HTAB 25: PRINT "X-BOW BOLTS: "CB: RETURN 
5000  REM 

   SOUND ROUTINES


5010  REM 

 === KILLED GHOST SOUND ===
5020  POKE 1,5: POKE 2,20: POKE 3,50: POKE P1,198: POKE P2,198: CALL UFO: RETURN 
5050  REM 

=== KILLED DEMON SOUND ===
5060  POKE 1,9: POKE 2,50: POKE 3,1: POKE P1,198: POKE P2,230: CALL UFO: RETURN 
5500  REM 

=== TREASURE SOUND ===
5510  FOR I = 150 TO 1 STEP  - 10: POKE 1,40: POKE 2,I: POKE 3,100 + I: POKE 4,3: CALL TRL: NEXT : RETURN 
5550  REM 

=== MAGIC POTION SOUND ===
5560  POKE 1,18: POKE 2,158: POKE 3,128: POKE P1,198: POKE P2,230: CALL UFO: RETURN 
5600  REM 

=== CROSSBOW SOUND ===
5610  POKE 1,3: POKE 2,230: POKE 3,200: POKE P1,230: POKE P2,230: CALL UFO: RETURN 
5650  REM 

=== CROSSBOW MISSED ===
5660  POKE 0,10: CALL WTN: RETURN 
5700  REM 

=== ATTACK PLAYER SOUND ===
5710  POKE 1,2: POKE 2,230: POKE 3,200: POKE P1,230: POKE P2,230: CALL UFO: RETURN 
9999  END 
10000  REM 

  SCREEN-DRAWING ROUTINES
10010  REM 
WIPE SCREEN
10020  FOR X = 0 TO 26: FOR Y = 0 TO 14:SC(BD,X,Y) = 0: NEXT Y,X: RETURN 
10050  REM 
HORIZONTAL WALL
10051  REM  S=START, E=END, Y=DIST. FROM TOP
10060  ROT= 0: FOR I = S TO E: DRAW 2 AT I * 10,Y * 10:SC(BD,I,Y) = 2: NEXT 
10070  RETURN 
10100  REM 
VERTICAL WALL
10101  REM  S=START, E=END, X=DIST. FROM LEFT 
10110  ROT= 0: FOR I = S TO E: DRAW 2 AT X * 10,I * 10:SC(BD,X,I) = 2: NEXT 
10140  RETURN 
10200  REM 

  SCREENSTORE INIT ROUTINE
10201  REM  POKE SN,SCREEN NO. * 32
10210 SN = 768:PUTPAGE = 769:BRINGPAGE = 800
10220  FOR I = 0 TO 64: READ Q: POKE PU + I,Q: NEXT : RETURN 
10230  DATA 169,0,133,60,169,32,133,61,169,248,133,62,169,63,133
10240  DATA 63,169,0,133,66,173,0,3,133,67,56,32,17,195,96,0
10250  DATA 169,0,133,66,169,32,133,67,169,0,133,60,173,0,3,133
10260  DATA  61,169,248,133,62,24,173,0,3,105,31,133,63,24,32,17,195,96
10300  REM 

   INITIALIZE LEVEL
10310  FOR BD = 0 TO 1: ON BD + 1 GOSUB 15000,15100,15200,15300,15400
10320  POKE SN,(BD + 1) * 32: CALL PUTPIC: NEXT 
10330  RETURN 
11000  REM 
PLACE MONSTERS RANDOMLY
11001  REM   T=TYPE,N=# OF MONS
11010 Q = NM + N:NM = NM + 1: FOR M = NM TO Q:MX(M) =  INT (25 *  RND (1) + 2):MY(M) =  INT (12 *  RND (1) + 2): IF SC(BD,MX(M),MY(M)) THEN M = M - 1: NEXT 
11015 SM(BD,M) = T:XM(BD,M) = MX(M):YM(BD,M) = MY(M)
11020  XDRAW T AT MX(M) * 10,MY(M) * 10:MS(M) = T:SC(BD,MX(M),MY(M)) = T: NEXT 
11030 NM = Q:NM(BD) = NM: RETURN 
15000  REM 

  GENERATE LEVEL
15010 LD = 0: FOR BD = 0 TO 3: GOSUB 15200: IF LD THEN D =  - LD:R =  - BD: GOSUB 15100
15020 D =  INT (5 *  RND (1) - 2): IF D = 0 OR D =  - LD THEN 15020
15030 R =  - BD - 2: GOSUB 15100:LD = D: POKE SN,(BD + 1) * 32: CALL PUTPIC: NEXT 
15090  RETURN 
15100  REM 

  PLACE DOOR


15110  HCOLOR= 0: ON D + 3 GOTO 15120,15130,15130,15140,15150
15120  DRAW 2 AT 130,0:SC(BD,13,0) = R: RETURN 
15130  DRAW 2 AT 260,70:SC(BD,26,7) = R: RETURN 
15140  DRAW 2 AT 0,70:SC(BD,0,7) = R: RETURN 
15150  DRAW 2 AT 130,140:SC(BD,13,14) = R: RETURN 
15200  REM 

   RANDOMLY GENERATED SCREEN
15210  CALL  - 3086: IF LV > 1 THEN  GOSUB 10000
15213 C =  INT (6 *  RND (1) + 1): IF C = 4 AND  RND (1) < LV / 50 THEN 15213
15215  HCOLOR= C: IF C = 4 AND LV < 3 THEN 15213
15220 XX = 9:YY = 5: GOSUB 15250:XX = 18: GOSUB 15250:YY = 10: GOSUB 15250:XX = 9: GOSUB 15250:S = 0:E = 26:Y = 0: GOSUB 10050:Y = 14: GOSUB 10050:E = 14:X = 0: GOSUB 10100:X = 26: GOSUB 10100
15230 NM = 0:T = 7:N =  INT (5 *  RND (1) + 2): GOSUB 11000:T = 8:N =  INT (4 *  RND (1) + 1): GOSUB 11000:T = 0
15240 X =  INT (25 *  RND (1) + 1):Y =  INT (13 *  RND (1) + 1): IF SC(BD,X,Y) THEN 15240
15245  IF  RND (1) > .3 THEN  XDRAW 3 AT X * 10,Y * 10:T = T + 1:SC(BD,X,Y) = 3: IF  RND (1) > LV / 10 AND T < 4 THEN 15240
15249  RETURN 
15250 R =  INT (4 *  RND (1) + 1): ON R GOTO 15260,15270,15280:S = XX - 9:E = XX:Y = YY: GOTO 10050
15260 S = XX:E = XX + 8:Y = YY: GOTO 10050
15270 S = YY - 4:E = YY:X = XX: GOTO 10100
15280 S = YY:E = YY + 4:X = XX: GOTO 10100
20000  REM 

  NEXT LEVEL


20010  POKE 35,19: VTAB 1: HOME 
20020  POKE 49233,0: INVERSE :A$ = " GRUNT ": GOSUB 110: NORMAL : PRINT : PRINT 
20030 LV = LV + 1: PRINT 
20040 A$ = "THOU ART ABOUT TO ENTERETH": GOSUB 110: PRINT :A$ = " LEVEL " +  STR$ (LV) + " ": INVERSE : GOSUB 110
20050  NORMAL : PRINT : PRINT : PRINT : PRINT 
20060 I =  INT (7 *  RND (1) + 1): ON I GOTO 20100,20110,20120,20130,20140,20150,20160,20170,20180,20190,20200
20100 A$ = "THOU DOETH WELL!": GOSUB 110:A$ = "(BUT THY LUCK MAY RUNNETH OUT!)": GOSUB 110: GOTO 21000
20110 A$ = "YE BE STRONG...  BUT BE YE": GOSUB 110:A$ = "STRONG ENOUGH FOR LEVEL " +  STR$ (LV) + "?": GOSUB 110: GOTO 21000
20120 A$ = "THOU ART GOOD, YOUNG KNIGHT.": GOSUB 110:A$ = "BUT IF THOU WOULDST REACH": GOSUB 110:A$ = "LEVEL " +  STR$ (LV + 10) + ", THOU MUST BE MUCH BETTER.": GOSUB 110: GOTO 21000
20130 A$ = "REACHETH NOT TOO FAR, ADVENTURER,": GOSUB 110:A$ = "LEST YE LOSE THY HAND!": GOSUB 110: GOTO 21000
20140 A$ =  CHR$ (34) + "COME INTO LEVEL " +  STR$ (LV) + "...": GOSUB 110:A$ = "WE'D LOVE TO HAVE YOU FOR DINNER!" +  CHR$ (34): GOSUB 110: GOTO 21000
20150 A$ = "AS THOU VENTURETH FURTHER, EACH": GOSUB 110:A$ = "DOOR ABOVE SLAMETH SHUT BEHIND THEE.": GOSUB 110: GOTO 21000
20160 A$ = "STOUT OF HEART AND BOLD OF MIND,": GOSUB 110:A$ = "WILL NOT HELP THEE, THOU SHALT FIND": GOSUB 110
20165 A$ = "AS DEMON'S HEELS THY BODY GRIND": GOSUB 110:A$ = "THOU SHALT STINK OF LEMON RIND.": GOSUB 110: GOTO 21000
21000  POKE 35,24: GOTO 30200
25000  REM 

   GAME OVER


25010  HOME : VTAB 22:A$ = "THY GAME IS OVER.": GOSUB 110:A$ = "SCORE: " +  STR$ (SR): GOSUB 110
25020  POKE 8150,SR -  INT (SR / 256) * 256: POKE 8151, INT (SR / 256)
25030  PRINT D$"RUNGRUNT.SCORES"
30000  REM 

   PROGRAM START-UP
30010  HGR : TEXT : PRINT  CHR$ (4)"PR#0": HOME 
30020 A$ = "G.R.U.N.T.": VTAB 7: GOSUB 110:H = 5:W =  LEN (A$) + 6: VTAB 4: GOSUB 150
30030  PRINT : PRINT : PRINT :A$ = "A GAME OF MAGIC,": GOSUB 110:A$ = "FANTASY, AND ADVENTURE": GOSUB 110
30040 LV = 1:LF = 100:CB = 10:MP = 2:SR = 0:UP$ = "I":DN$ = "K":LF$ = "J":RT$ = "L":MG$ = "P":FR$ = " "
30050  PRINT : PRINT : PRINT :A$ = "by": GOSUB 110:A$ = "JOE J. STROUT": GOSUB 110
30080  VTAB 22:A$ = "ONE MOMENT... SUMMONING THE DEMONS": GOSUB 110
30100 SH = 3072:D$ =  CHR$ (4): IF  NOT FL THEN  PRINT D$"BLOADGRUNT.SHAPES,A"SH: POKE 232,SH -  INT (SH / 256) * 256: POKE 233, INT (SH / 256)
30105  IF  NOT FL THEN  PRINT D$"BLOADSOUND,A5376"
30108 TN = 5376:UFO = TN + 14:QTN = TN + 36:WTN = TN + 53:TRL = TN + 69:P1 = UF + 17:P2 = UF + 7
30110  SCALE= 1: ROT= 0: HCOLOR= 3: GOSUB 10200
30190  HGR : TEXT : POKE 230,32
30200 QQ = 1: GOSUB 15000:BD = 3:S =  - 1:PX = 10:PY = 16: GOSUB 1600:QQ = 0
30210 PX =  INT (4 *  RND (1) + 2):PY =  INT (4 *  RND (1) + 2): IF SC(BD,PX,PY) THEN 30210
30215  IF LV = 1 THEN  GOSUB 40000
30220  POKE 49232,0: POKE 49235,0: POKE 49236,0: POKE 49239,0
30230  FOR I = 0 TO 208 STEP 16: ROT= I: XDRAW 5 AT PX * 10 + 5,PY * 10 + 5: FOR J = 1 TO I * 2: NEXT : XDRAW 5 AT PX * 10 + 5,PY * 10 + 5: NEXT 
30240 PR = 16: ROT= PR: XDRAW 5 AT PX * 10 + 5,PY * 10 + 5
30330  GOSUB 3000
39999  RETURN 
40000  REM 
  OPTIONS

40010  HOME : VTAB 12:A$ = "PRESSETH <RETURN> TO START THE GAME,": GOSUB 110:A$ = "OR <C> TO CHANGE OPTIONS": GOSUB 110: PRINT : PRINT 
40020  GET A$: IF A$ <  >  CHR$ (13) AND A$ <  > "C" THEN 40020
40030  IF A$ =  CHR$ (13) THEN  RETURN 
40040  HOME :A$ = " GRUNT OPTIONS ": INVERSE : GOSUB 100: NORMAL : PRINT : PRINT : PRINT 
40045  POKE 34,2: POKE 35,22
40050  PRINT "WOULDST THOU LIKETH TO USETH A JOYSTICK": PRINT "(Y/N) ? ";
40060  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 40060
40070  PRINT A$"AY": IF A$ = "Y" THEN JS = 1: PRINT : GOTO 40150
40080 H$ =  CHR$ (8): PRINT : PRINT : PRINT "THEN SELECTETH THY KEYS:": PRINT "          UP = I"H$;: GET A$:UP$ = A$: IF A$ =  CHR$ (13) THEN UP$ = "I"
40090  PRINT UP$: PRINT "        DOWN = K"H$;: GET A$:DN$ = A$: IF A$ =  CHR$ (13) THEN DN$ = "K"
40100  PRINT DN$: PRINT "        LEFT = J"H$;: GET A$:LF$ = A$: IF A$ =  CHR$ (13) THEN LF$ = "J"
40110  PRINT LF$: PRINT "       RIGHT = L"H$;: GET A$:RT$ = A$: IF A$ =  CHR$ (13) THEN RT$ = "L"
40120  PRINT RT$: PRINT "MAGIC POTION = P"H$;: GET A$:MG$ = A$: IF A$ =  CHR$ (13) THEN MG$ = "P"
40130  PRINT MG$: PRINT "    CROSSBOW =  "H$;: GET A$:FR$ = A$: IF A$ =  CHR$ (13) THEN FR$ = " "
40140  PRINT FR$: PRINT : PRINT 
40150  PRINT "WOULDST THOU DARETH TO BEGIN ON A": PRINT "HIGHER LEVEL (Y/N) ? ";
40160  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 40160
40170  PRINT A$"AY": PRINT : IF A$ = "N" THEN 40200
40180  INPUT "ENTERETH LEVEL (1-10) : ";LV: IF LV < 1 OR LV > 10 THEN 40180
40190 LV =  INT (LV)
40200  PRINT : PRINT "WOULDST THOU LIKETH SOUND (Y/N) ? ";
40210  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 40210
40220  PRINT A$"AY": IF A$ = "N" THEN  POKE TN,96: POKE UF,96: POKE QT,96: POKE WT,96: POKE TR,96
40230  PRINT : PRINT : TEXT : RETURN 
