10  ROT= 0: SCALE= 1
20 SH = 25000:D$ =  CHR$ (4)
30  IF  NOT FLAG THEN  PRINT D$"BLOAD GBUSTERS.SHAPES,A"SH
40  POKE 232,SH -  INT (SH / 256) * 256: POKE 233, INT (SH / 256)
41  REM SHAPE 1 = LEFT DUDE
42  REM SHAPE 2 = GHOST
43  REM SHAPE 3 = TRAP
44  REM SHAPE 4 = LEFT BEAM
45  REM SHAPE 5 = RIGHT DUDE
46  REM SHAPE 6 = RIGHT BEAM
50  HGR :TR = 140: XDRAW 3 AT TR,140
60 X(1) = 200:X(2) = 80:RT(1) = 0:RT(2) = 1: XDRAW 1 AT X(1),150: XDRAW 5 AT X(2),150
70 LF$ =  CHR$ (8):RT$ =  CHR$ (21)
80 GX = 140:GY = 70: XDRAW 2 AT GX,GY
100  REM === MAIN LOOP ===
110  REM  PLAYER 1 (KEYBOARD)
120  GOSUB 500:KEY =  PEEK (49152): IF KEY < 128 THEN 150
130  POKE 49168,0:K$ =  CHR$ (KEY - 128):Z = 1: IF K$ = LF$ THEN ZD =  - 3: GOSUB 300: GOTO 150
140  IF K$ = RT$ THEN ZD = 3: GOSUB 300: GOTO 150
145  IF K$ = " " THEN  GOSUB 350
148  IF K$ = "T" THEN 1000
150  REM  PLAYER 2 (JOYSTICK)
160  GOSUB 500:JX =  INT ( PDL (0) / 85 - 1):BT = ( PEEK (49249) > 127) + ( PEEK (49250) > 127): IF  NOT JX AND  NOT BT THEN 100
170 Z = 2: IF BT THEN  GOSUB 350: GOTO 100
180 ZD = 3 * JX: GOSUB 300: GOTO 100
300  REM --- MOVE DUDE Z ---
310  XDRAW 1 + 4 * RT(Z) AT X(Z),150:X(Z) = X(Z) + ZD:RT(Z) = ZD > 0: XDRAW 1 + 4 * RT(Z) AT X(Z),150: RETURN 
350  REM --- FIRE DUDE Z ---
360 BM = 4 + 2 * RT(Z): XDRAW BM AT X(Z) - 2 * ( - RT(Z)),140: FOR I = 1 TO 25
370  XDRAW BM: NEXT :BX = X(Z) + ( - 1 + 2 * RT(Z)) * (140 - GY): IF BX > GX - 10 AND BX < GX + 5 THEN 600
380  XDRAW BM AT X(Z) - 2 * ( - RT(Z)),140: FOR I = 1 TO 25: XDRAW BM: NEXT 
400 SG = 5 *  SGN (GX - BX): REM  SCARE GHOST
410  RETURN 
500  REM === MOVE GHOST ===
505  XDRAW 2 AT GX,GY: IF SG THEN GX = GX + SG:GY = GY +  INT (7 *  RND (1) - 3):SG = SG -  SGN (SG): GOTO 520
510 GX = GX +  INT (7 *  RND (1) - 3):GY = GY +  INT (4 *  RND (1) - 2): IF GX > 10 AND GX < 260 AND GY > 20 AND GY < 130 THEN  XDRAW 2 AT GX,GY: RETURN 
520  IF GX < 10 THEN GX = 250
530  IF GX > 260 THEN GX = 20
540  IF GY < 20 THEN GY = GY + 4
550  IF GY > 130 THEN GY = GY - 4
560  XDRAW 2 AT GX,GY: RETURN 
600  REM === GHOST'S BEEN HIT! ===
610  PRINT  CHR$ (7): FOR U = 2 TO 6 STEP 2: XDRAW 2 AT GX,GY: SCALE= U: XDRAW 2 AT GX,GY: NEXT 
620  FOR U = 5 TO 1 STEP  - 2: XDRAW 2 AT GX,GY: SCALE= U: XDRAW 2 AT GX,GY: NEXT 
630  FOR U = 255 TO 0 STEP  - 16: XDRAW 2 AT GX,GY: ROT= U: XDRAW 2 AT GX,GY: NEXT 
680  XDRAW 2 AT GX,GY: SCALE= 1: ROT= 0: XDRAW 2 AT GX,GY
690  GOTO 380
1000  REM === FIRE TRAP ===
1010  FOR I = 1 TO 7: IF I = 4 THEN  NEXT 
1020  XDRAW 2 AT GX,GY: HCOLOR= I: GOSUB 1100: IF GX <  > TR + 7 THEN GX = GX +  SGN ((TR + 7) - GX): GOTO 1090
1030 Z = Z + 1: IF Z > (GY - 20) THEN GY = GY + 1: IF GY > 130 THEN 1200
1090  XDRAW 2 AT GX,GY: NEXT : GOTO 1010
1100  REM --- DRAW WEBBING ---
1110  HPLOT TR + 5,140 TO TR - 25,Z: HPLOT TR + 5,140 TO TR - 10,Z: HPLOT TR + 5,140 TO TR,Z: HPLOT TR + 5,140 TO TR + 5,Z
1120  HPLOT TR + 5,140 TO TR + 10,Z: HPLOT TR + 5,140 TO TR + 20,Z: HPLOT TR + 5,140 TO TR + 35,Z
1130  IF Z THEN  HCOLOR= 0: HPLOT 0,Z TO 279,Z
1150  RETURN 
1200  REM  WELL CLOSE THE TRAP ALREADY!
1210  HCOLOR= 0: FOR I = Z TO 139: HPLOT TR - 25,I TO TR + 35,I: NEXT 
1230  HCOLOR= 3: HPLOT TR,140 TO TR,130
1235  FOR I = 1 TO 250: NEXT 
1240  HCOLOR= 0: HPLOT TR,140 TO TR,130: HCOLOR= 3: HPLOT TR,140 TO TR + 8,135
1245  FOR I = 1 TO 250: NEXT 
1250  HCOLOR= 0: HPLOT TR,140 TO TR + 8,135
1260  HCOLOR= 3: HPLOT TR - 1,140 TO TR + 5,140
1300  HOME : VTAB 24: PRINT "THIS HAS BEEN A  JOE STROUT  PRODUCTION."
