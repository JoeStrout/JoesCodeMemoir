10  REM 

 WATOR


15  DIM FISH(30,20),FMOVE(30,20),SHARK(30,20),SMOVE(30,20),STARVE(30,20)
20  GOSUB 10000
100  REM 

  MAIN LOOP


110  FOR LX = 10 TO 30: FOR LY = 2 TO 20: IF FISH(LX,LY) THEN X = LX:Y = LY: GOSUB 1010: GOTO 130
120  IF SHRK(LX,LY) THEN X = LX:Y = LY: GOSUB 1100
130  NEXT LY,LX: GOSUB 1200: GOTO 100
1010  REM /// FISH SWIM & BREED
1011  REM /// X & Y = LOC. OF FISH
1020  IF FMOVE(X,Y) THEN  RETURN 
1030 FX = X:FY = Y: FOR I = 1 TO 8:X = FX +  INT (3 *  RND (1) - 1):Y = FY +  INT (3 *  RND (1) - 1): GOSUB 1500: IF FISH(X,Y) THEN  NEXT : RETURN 
1040  IF SHARK(X,Y) THEN FISH(FX,FY) = 0:STARVE(X,Y) = 0: RETURN 
1050 FISH(X,Y) = FISH(FX,FY) + 1:FISH(FX,FY) = 0: IF FISH(X,Y) > FBREED THEN FISH(X,Y) = 1:FISH(FX,FY) = 1
1060  RETURN 
1100  REM /// SHARK SWIM & BREED
1101  REM /// X & Y = LOC. OF SHARK
1110  IF SMOVE(X,Y) THEN  RETURN 
1120 SX = X:SY = Y: FOR I = 1 TO 8:X = SX +  INT (3 *  RND (1) - 1):Y = SY +  INT (3 *  RND (1) - 1): GOSUB 1500: IF  NOT FISH(X,Y) THEN  NEXT : FOR I = 1 TO 8:X = SX +  INT (3 *  RND (1) - 1):Y = SY +  INT (3 *  RND (1) - 1): GOSUB 1500: IF SHARK(X,Y) THEN  NEXT : RETURN 
1130 STARVE(X,Y) = STARVE(SX,SY) + 1 *  NOT FISH(X,Y): IF STARVE(X,Y) > 3 THEN SHRK(SX,SY) = 0: RETURN 
1140 FISH(X,Y) = 0:SHRK(X,Y) = SHRK(SX,SY) + 1:SHRK(SX,SY) = 0: IF SHRK(X,Y) > SBREED THEN SHRK(X,Y) = 1:SHRK(SX,SY) = 1:STARVE(SX,SY) = 0
1160  RETURN 
1200  REM /// UPDATE DISPLAY
1205 S = 0:F = 0
1210  FOR Y = 2 TO 20: VTAB Y: HTAB 10: FOR X = 10 TO 30: IF FISH(X,Y) THEN  PRINT ".";:F = F + 1: GOTO 1240
1220  IF SHRK(X,Y) THEN  PRINT "o";:S = S + 1: GOTO 1240
1230  PRINT " ";
1240  NEXT X,Y
1250  VTAB 24: HTAB 5: PRINT "FISH: "F"  ";: HTAB 25: PRINT "SHARKS: "S"  ";: VTAB 1: PRINT 
1260  RETURN 
1500  REM /// X & Y WRAPAROUND
1510  IF X < 10 THEN X = 30
1520  IF X > 30 THEN X = 10
1530  IF Y < 2 THEN Y = 20
1540  IF Y > 20 THEN Y = 2
1550  RETURN 
10000  REM 

INITIALIZATION

10010  TEXT : HOME : INVERSE : HTAB 17: PRINT "+     +": HTAB 17: PRINT " WATOR ": HTAB 17: PRINT "+     +": NORMAL : VTAB 10
10020  HTAB 19: PRINT "BY:": HTAB 19: PRINT "JOE STROUT": VTAB 23: HTAB 6: PRINT "[ PRESS ANY KEY TO BEGIN ]": POKE 49168,0: WAIT 49152,128
10030  POKE 49168,0: VTAB 9: CALL  - 958
10040  INPUT "HOW MANY FISH IN THE SEA?";NFISH: PRINT : INPUT "HOW LONG BEFORE A FISH CAN BREED?";FBREED
10050  PRINT : PRINT : INPUT "HOW MANY SHARKS IN THE SEA?";NSHRKS: PRINT : INPUT "HOW LONG BEFORE A SHARK BREEDS?";SBREED
10100  HOME : HTAB 9: PRINT "+||||||W|A|T|O|R||||||+": FOR I = 1 TO 19: HTAB 9: PRINT "-";: HTAB 31: PRINT "-": NEXT 
10110  HTAB 9: PRINT "+|||||||||||||||||||||+"
10120  VTAB 24: HTAB 5: PRINT "FISH: "NF"  ";: HTAB 25: PRINT "SHARKS: "NS"  ";: VTAB 1: PRINT 
10200  FOR I = 1 TO NFISH
10210 X =  INT (21 *  RND (1) + 10):Y =  INT (18 *  RND (1) + 2): IF FISH(X,Y) THEN 10210
10220 FISH(X,Y) =  INT (FBREED *  RND (1) + 1): HTAB X: VTAB Y: PRINT ".": NEXT 
10230  FOR I = 1 TO NSHRKS
10240 X =  INT (21 *  RND (1) + 10):Y =  INT (18 *  RND (1) + 2): IF FISH(X,Y) OR SHARK(X,Y) THEN 10240
10250 SHARK(X,Y) =  INT (SBREED *  RND (1) + 1): HTAB X: VTAB Y: PRINT "o": NEXT 
10260  RETURN 
