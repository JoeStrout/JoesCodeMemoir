
 1  GOTO 10
 5  PRINT  CHR$ (4)"SAVEPARSER": END 
 10  REM 


PARSER.7



 20  DEF  FN B(X) = (OL(X) - 200) * (OL(X) > 200 AND OL(X) < 300)
 30  DEF  FN C(X) = (OL(X) = RM) OR (OL(X) = 101) OR (OL(X) = 102) OR (OL( FN B(X)) = RM) OR (OL( FN B(X)) = 101)
 40  GOTO 1000
 100  REM 

**** GENERAL SUBROUTINES ****

 101  REM  >>>> PRINT STRING A$ <<<<<
 105  REM    SW = SCREEN WIDTH
 106  REM   NL = NUMBER OF. LINES AFTERWARDS 
 110 H =  PEEK (36) + 1: IF  LEN (A$) < SW - H THEN  PRINT A$;:SC = SC + 1: GOTO 140
 120  FOR X = SW - H TO 1 STEP  - 1: IF  MID$ (A$,X,1) <  > " " THEN  NEXT 
 130  PRINT  LEFT$ (A$,X - 1):A$ =  MID$ (A$,X + 1):SC = SC + 1: IF SC < 15 THEN 110
 135  GOSUB 180: GOTO 110
 140  IF  NOT NL THEN  RETURN 
 150  FOR X = 1 TO NL: PRINT :SC = SC + 1: IF SC < 15 THEN  NEXT :NL = 0: RETURN 
 160  GOSUB 180: NEXT :NL = 0: RETURN 
 180  PRINT MR$: WAIT 49152,128: POKE 49168,0: VTAB  PEEK (37): PRINT  SPC( 10): PRINT : VTAB  PEEK (37):SC = 1: RETURN 
 200  REM //// A$=NEXT WORD OF STRING C$ \\\\
 205 A$ = ""
 210 Z$ =  LEFT$ (C$,1): IF Z$ = " " OR Z$ = "." OR Z$ = "," THEN C$ =  MID$ (C$,2): GOTO 210
 220 Z$ =  RIGHT$ (C$,1): IF Z$ = " " OR Z$ = "." OR Z$ = "," THEN C$ =  LEFT$ (C$, LEN (C$) - 1): GOTO 220
 230 C$ = C$ + " ": IF C$ = " " THEN A$ = "":C$ = A$: RETURN 
 235  IF  LEFT$ (C$,1) = QT$ THEN A$ = QT$: FOR I = 2 TO  LEN (C$):A$ = A$ +  MID$ (C$,I,1): IF  RIGHT$ (A$,1) = QT$ THEN WT = 2:WM = 0:W$ = A$: RETURN 
 236  IF  LEFT$ (A$,1) = QT$ THEN  NEXT :WT = 2:WM = 0:W$ = A$: RETURN 
 240  FOR I = 1 TO  LEN (C$):Z$ =  MID$ (C$,I,1): IF Z$ <  > " " AND Z$ <  > "," AND Z$ <  > "." THEN  NEXT 
 250 A$ =  LEFT$ (C$,I - 1)
 260 W$ = A$: GOSUB 300: IF WT < 5 AND WT > 0 THEN  RETURN 
 265  IF WT = 0 THEN  PRINT "I don't know the word '"W$"'.": POP : GOTO 1000
 270  IF WT = 7 THEN  NEXT I: GOTO 250
 280  IF WT = 6 THEN  RETURN 
 290 C$ =  MID$ (C$,I + 1): GOTO 200
 300  REM //// WT = TYPE OF WORD W$ \\\\
 310 WT = 0
 315  IF  LEFT$ (W$,1) = QT$ OR  VAL (W$) OR  LEFT$ (W$,1) = "0" THEN WT = 2:WM = 0: RETURN 
 320  FOR T = 1 TO 17: IF W$ = WD$(T) THEN WT = WT(T):WM = WM(T):T = TW + 1: GOTO 390
 330  NEXT : FOR T = 1 TO NO: IF W$ = OB$(T) THEN WT = 2:WM = T:T = TW + 1: GOTO 390
 340  NEXT : FOR T = 1 TO NC: IF W$ = CN$(T) THEN WT = 2:WM = 100 + T:T = TW + 1: GOTO 390
 350  NEXT : FOR T = 18 TO TW: IF W$ = WD$(T) THEN WT = WT(T):WM = WM(T):T = TW + 1: GOTO 390
 390  NEXT : RETURN 
 400  REM //// PREPARE COMMAND C$ FOR BREAKDOWN \\\\
 410  IF C$ = "" THEN  PRINT : PRINT "Beg pardon?": POP : GOTO 1010
 412  IF  RIGHT$ (C$,1) = "." OR  RIGHT$ (C$,1) = " " THEN C$ =  LEFT$ (C$, LEN (C$) - 1): GOTO 412
 415 I = 1
 420 A$ =  MID$ (C$,I,1): IF A$ = "." OR A$ = "," THEN C$ =  LEFT$ (C$,I - 1) + " AND " +  MID$ (C$,I + 1): GOTO 450
 430  IF A$ = " " THEN Z = Z + 1: IF Z > 1 THEN C$ =  LEFT$ (C$,I - 1) +  MID$ (C$,I + 1):Z = Z - 1:I = I - 1: GOTO 450
 440  IF A$ <  > " " THEN Z = 0
 450 I = I + 1: IF I <  =  LEN (C$) THEN 420
 460  RETURN 
 500  REM //// INPUT W/COMMAS & COLONS \\\\
 510  CALL  - 657:A$ = "": FOR X = 512 TO 767: IF  PEEK (X) <  > 141 THEN A$ = A$ +  CHR$ ( PEEK (X) - 128): NEXT 
 520 X = 768: NEXT : RETURN 
 600  REM //// PRINT ROUTINE \\\\
 605  IF F$ = "OBJ DESC" AND R > NO - NP THEN A$ = OB$(R): GOSUB 850:A$ = "   You see your " + A$ + ".":NL = 1: GOTO 100
 610  PRINT : PRINT D$"OPEN"F$",L207"
 620 H =  PEEK (1403) + 1: PRINT : PRINT D$"READ"F$",R"R: INPUT A$: VTAB  PEEK (37): HTAB H: IF  RIGHT$ (A$,1) = "@" THEN 640
 630  GOSUB 100: PRINT : PRINT D$"CLOSE"F$: RETURN 
 640  REM 
 690 R =  VAL ( MID$ (A$, LEN (A$) - 2)):A$ =  LEFT$ (A$, LEN (A$) - 3): GOSUB 100: GOTO 620
 700  REM //// DESCRIBE ROOM RM \\\\
 710 R = RM:F$ = RD$: GOSUB 600
 720  FOR O = 1 TO NO: IF OL(O) <  > RM THEN  NEXT : GOTO 770
 730  IF  NOT OS(O) THEN OS(O) = 1:R = O:F$ = OD$: GOSUB 600: GOTO 760
 740  IF OT(O) <  > 7 THEN A$ = OB$(O): GOSUB 850: PRINT "   You see "A$".": GOTO 760
 750 A$ = OB$(O): GOSUB 870: PRINT "   You see "A$"."
 760  NEXT 
 770  FOR I = 1 TO NC: IF CR(I) <  > RM THEN  NEXT : RETURN 
 780  IF  NOT CS(I) THEN CS(I) = 1:R = I:F$ = CD$: GOSUB 600: GOTO 790
 785 A$ = CN$(I): GOSUB 870: PRINT "   "A$" is here."
 790  NEXT : RETURN 
 800  REM //// GET S$ IF NOT SPECIFIED \\\\
 840  PRINT : PRINT V$;: INPUT " WHAT? ";S$:W$ = S$: GOSUB 300:S = WM: RETURN 
 850  REM //// CONVERT UPPER -> LOWER \\\\
 860 B$ = "": FOR L = 1 TO  LEN (A$):Z =  ASC ( MID$ (A$,L,1)):Z = Z + 32 * (Z < 91 AND Z > 64):B$ = B$ +  CHR$ (Z): NEXT :A$ = B$: RETURN 
 870  REM //// UPPER -> LOWER, SANS 1st CHAR \\\\
 875  IF A$ = "PRINCESS LISA" THEN A$ = "Princess Lisa": RETURN 
 880 B$ =  LEFT$ (A$,1): FOR L = 2 TO  LEN (A$):Z =  ASC ( MID$ (A$,L,1)):Z = Z + 32 * (Z < 91 AND Z > 64):B$ = B$ +  CHR$ (Z): NEXT :A$ = B$: RETURN 
 900  REM //// CONVERT INDIRECT OBJECTS \\\\
 910  IF S < 100 THEN  RETURN 
 920 OP$ = S$:OP = S:P$ = "TO":P = 3
 930  IF C$ = "" OR C$ = " " THEN 800
 940  GOSUB 200:S$ = A$:S = WM:C$ =  MID$ (C$,I + 1): RETURN 
 1000  REM 

**** MAIN LOOP ****
 1010  PRINT :SC = 0: PRINT ">";: GOSUB 500:C$ = A$: IF C$ = "G" OR C$ = "AGAIN" THEN C$ = CZ$
 1020  GOSUB 400
 1040 CZ$ = C$: GOSUB 200:V$ = A$:V = WM: IF WT <  > 1 THEN  PRINT "You must supply a verb!": GOTO 1000
 1050 C$ =  MID$ (C$,I + 1): GOSUB 200:S$ = A$:S = WM: IF WT = 3 THEN C$ = S$ + " " + C$:S$ = "":S = 0: GOTO 1070
 1055  IF S$ = "IT" THEN S$ = LS$:S = LS
 1060 P$ = "":OP$ = P$:P = 0:OP = P: IF WT = 4 THEN S$ = "":S = 0: GOTO 1400
 1070 C$ =  MID$ (C$,I + 1): GOSUB 200: IF WT = 3 THEN P$ = A$:C$ =  MID$ (C$,I + 1): GOSUB 200:OP$ = A$:W$ = A$: GOSUB 300:OP = WM
 1080  IF OP$ = "IT" THEN OP$ = LS$:OP = LS
 1085  IF OP$ <  > "" THEN LS$ = OP$:LS = OP
 1090  IF P$ <  > "" OR C$ = "" THEN 1400
 1100 Q$ = C$: REM   LOOKING AHEAD FOR PREPOSITION
 1110 C$ =  MID$ (C$,I + 1): GOSUB 200: IF WT <  > 1 AND WT <  > 3 AND C$ <  > "" THEN 1110
 1120  IF WT = 1 OR C$ = "" THEN C$ = Q$: GOTO 1400
 1130 P$ = W$:C$ =  MID$ (C$,I + 1): GOSUB 200:OP$ = A$:C$ = Q$: GOTO 1080
 1400  REM 
 1420 LS$ = S$:LS = S: IF OP$ <  > "" THEN LS$ = OP$:LS = OP
 1430 QQ = I:VB = V
 1440  PRINT 
 1445  IF S = 7 THEN  PRINT "As you approach the statue, it suddenly comes to life and swings at you!":CP = CP + 1:CP(CP) = 18:CN$(18) = "STATUE":OB$(7) = "*":OL(7) = 0:CR(18) = RM: GOTO 1500
 1450  ON V GOSUB 2000,3000,4000,5000,6000,7000,8000,9000,10000,11000,12000,13000,14000,15000,16000,16500,17000,18000,19000,20000,21000,23000,24000
 1460  GOSUB 29000: REM  MONSTER'S ACTIONS
 1490 I = QQ
 1500  REM **** COMMANDS RETURN TO HERE ****
 1505 V =  PEEK (37) + 1:SD = SD + 1: INVERSE : VTAB 1: HTAB 56: PRINT  INT (SN)"  ";: HTAB 71: PRINT SD"  ";: NORMAL : VTAB V: HTAB 1: PRINT 
 1506  GOSUB 1600:V = VB:MV = 0
 1508  GOSUB 200
 1509 C$ =  MID$ (C$,I + 1)
 1510  IF C$ = " " OR C$ = "" THEN 1000
 1520  GOSUB 200:W$ = A$: GOSUB 300: IF WT = 1 THEN V$ = A$:V = WM: GOTO 1050
 1530  IF WT = 2 THEN S$ = A$:S = WM: GOTO 1060
 1540  IF WT = 4 THEN C$ =  MID$ (C$,I + 1): GOTO 1510
 1550  PRINT "PROBLEM IN THE 1500'S.": END 
 1600  REM 

**** SPECIAL CHECKS AFTER COMMAND ****


 1605  IF ZZ(1) THEN AC = AC - 1:ZZ(1) = ZZ(1) - 1: IF  NOT ZZ(1) THEN  PRINT "The glow around your body has faded to nothingness."
 1607  IF ZZ(2) THEN ZZ(2) = ZZ(2) - 1
 1608  IF ZZ(3) THEN ST = ST - 1:SD = SD + 1:ZZ(3) = ZZ(3) - 1: IF  NOT ZZ(3) THEN  PRINT "The last of the magic strength has left your body.  (What a letdown.)"
 1610  IF ZZ(4) THEN ZZ(4) = ZZ(4) + .5: IF ZZ(4) =  INT (ZZ(4)) THEN CM(ZZ(4) - 1) =  - CM(ZZ(4) - 1):ZZ(4) = 0
 1620  IF (SD = 4 OR SD = 8) AND (RM = 2 OR RM = 19) THEN  PRINT "If you're having trouble with the doors,you might find clues in your registra-  tion packet.": PRINT 
 1999  RETURN 
 2000  REM 

**** GET COMMAND ****


 2010  IF S$ = "" THEN  GOSUB 800
 2030  IF OL(S) = RM THEN 2100
 2040  IF OL(S) = 101 OR OL( FN B(S)) = 101 THEN  PRINT "You've already got that.": RETURN 
 2050  IF OL(S) = 102 THEN  PRINT "You're already wearing that!": RETURN 
 2060  IF S$ = "ALL" THEN  FOR S = 1 TO NO: ON  FN C(S) AND OL(S) <  > 101 AND OL( FN B(S)) <  > 101 AND OL(S) <  > 102 GOSUB 2100: NEXT S: RETURN 
 2090  IF  NOT  FN C(S) THEN A$ = S$: GOSUB 850: PRINT "You can't see any "A$" here!": RETURN 
 2100  REM --> SPECIAL EFFECTS OF GETTING OBJECT <--
 2910  IF OT(S) = 5 THEN  PRINT "You'll need to put that in a container to carry it.": RETURN 
 2920  IF OT(S) = 7 THEN A$ = OB$(S): GOSUB 870: PRINT "Get "A$"???  Please try to keep absurdity to a minimum!": RETURN 
 2930  IF CR > MC THEN  PRINT "Sorry, you can't carry any more.":S = S + NO * (S$ = "ALL"): RETURN 
 2940  IF OW(S) > ST THEN  PRINT "That's too heavy for you.": RETURN 
 2950 OL(S) = 101:CR = CR + 1:A$ = OB$(S): GOSUB 870: PRINT A$" taken."
 2960  RETURN 
 3000  REM 

**** DROP COMMAND ****


 3010  IF S$ = "" THEN  GOSUB 800
 3015  IF  VAL (S$) THEN  PRINT "You can't just drop your money!": RETURN 
 3020  IF OL(S) = 101 OR OL(S) = 102 THEN 3100
 3030  IF OL(S) > 200 AND OL(S) < 300 THEN X = OL(S) - 200: IF OL(X) = 101 OR OL(X) = 102 THEN 3100
 3050  IF S$ = "ALL" THEN  FOR S = 1 TO NO: ON (OL(S) = 101 OR OL(S) = 102) GOSUB 3100: NEXT : RETURN 
 3090  PRINT "You don't have that.": RETURN 
 3100  REM --> SPECIAL EFFECTS OF DROPPING OBJECT <--
 3930  IF OT(S) = 2 AND OL(S) = 102 THEN AC = AC - O1(S):EN = EN - O2(S): REM ARMOUR
 3940  IF S = RW THEN RW = 0
 3950 A$ = OB$(S): GOSUB 870: PRINT A$" dropped.":CR = CR - 1
 3960 OL(S) = RM: RETURN 
 4000  REM 

**** MOVE COMMAND ****


 4010  IF V$ = "MOVE" OR V$ = "GO" THEN V$ = S$:V = S
 4020 D = 1 * (V$ = "N" OR V$ = "NORTH") + 2 * (V$ = "E" OR V$ = "EAST") + 3 * (V$ = "S" OR V$ = "SOUTH") + 4 * (V$ = "W" OR V$ = "WEST") + 5 * (V$ = "NE" OR V$ = "NORTHEAST")
 4030 D = D + 6 * (V$ = "SE" OR V$ = "SOUTHEAST") + 7 * (V$ = "SW" OR V$ = "SOUTHWEST") + 8 * (V$ = "NW" OR V$ = "NORTHWEST") + 9 * (V$ = "U" OR V$ = "UP") + 10 * (V$ = "D" OR V$ = "DOWN")
 4040  IF DR(RM,D) < 1 THEN  PRINT "You can't go that way.": PRINT : GOTO 700
 4050  IF DR(RM,D) < 101 OR DR(RM,D) > 200 THEN 4200
 4060  PRINT "The door ";: IF D < 9 THEN  PRINT "to the ";
 4070  PRINT DI$(D)" is locked.": PRINT : RETURN 
 4200  REM --> SPECIAL DOORS HERE <--
 4210  IF DR(RM,D) = 100 THEN 39000: REM HOME
 4250  IF DR(RM,D) > 200 AND DR(RM,D) < 300 THEN  PRINT "The door refuses to open.": RETURN 
 4300  IF DR(RM,D) <  > 20 THEN 4350
 4310 CR(3) = 22:CR(4) = 30:CR(5) = 36:CR(6) = 31:CR(7) = 28:CR(11) = 35
 4320 OL(6) = 318:O1(6) = 34
 4350  IF RM <  > 3 OR D = 1 THEN 4400
 4360  FOR I = 1 TO NO: IF OT(I) = 8 AND (OL(I) = 101 OR OL( FN B(I)) = 101) THEN I = NO + 1: NEXT :A$ = "You find a magical field blocking your way.  The sign on the wall changes briefly to read, 'THIS MEANS YOU.'": GOTO 100
 4370  NEXT 
 4400 :
 4800 RM = DR(RM,D)
 4810 V =  PEEK (37) + 1: VTAB 1: HTAB 5: INVERSE : PRINT RN$(RM); TAB( 42)" ";: NORMAL 
 4820  VTAB V: HTAB 1: PRINT 
 4830  PRINT RN$(RM):NV(RM) = NV(RM) + 1: IF (NV(RM) = 1 AND  NOT SB) OR VR THEN  GOSUB 700: GOTO 4850
 4840  GOSUB 720: REM IF VISITED BEFORE, JUST LIST OBJECTS
 4850  IF CP < 1 OR (ZZ(2) AND ZZ(2) / 2 =  INT (ZZ(2) / 2)) THEN CP = 0: GOTO 4950
 4855  FOR I = 1 TO CP:C = CP(I):A = CD(C) +  INT (CH / 3) - 4: IF A > 3 OR A <  - 3 THEN 4870
 4860 CP(I) = CP(CP):CP = CP - 1: NEXT : GOTO 4950
 4870 A$ = CN$(C): GOSUB 870: IF CH(C) < 3 THEN  PRINT A$" was too weak to follow you.": GOTO 4860
 4880 CR(C) = RM: PRINT "     "A$" ";: IF A < 0 THEN  PRINT "pursues you.": GOTO 4890
 4881  ON  INT (4 *  RND (1) + 1) GOTO 4882,4884,4886,4888
 4882  PRINT "follows you.": GOTO 4890
 4884  PRINT "sticks with you.": GOTO 4890
 4886  PRINT "is close on your heels.": GOTO 4890
 4888  PRINT "keeps up with you."
 4890  NEXT 
 4900 MV = 1: REM  USE TO KNOW WHEN YOU'VE JUST MOVED
 4950  FOR I = 1 TO NC: IF CR(I) <  > RM THEN  NEXT : RETURN 
 4960  FOR J = 1 TO CP: IF CP(J) <  > I THEN  NEXT :CP = CP + 1:CP(CP) = I: NEXT : RETURN 
 4970 J = CP + 1: NEXT : NEXT : RETURN 
 4990  RETURN 
 5000  REM 

**** INVENTORY ****


 5010  PRINT "You are carrying:"
 5020  IF  NOT CR AND  NOT GP THEN  PRINT "   Zilch!!!"
 5030  IF CR AND  NOT GP THEN  PRINT "   -No money"
 5040  IF GP THEN  PRINT "   -"GP" G.P."
 5100  FOR O = 1 TO NO: IF OL(O) = 101 THEN A$ = OB$(O): GOSUB 870: PRINT "   -";A$: GOTO 5200
 5110  IF OL(O) = 102 THEN A$ = OB$(O): GOSUB 870: PRINT "   -"A$" (being worn)": GOTO 5200
 5170  IF O = RW THEN  VTAB  PEEK (37): PRINT "   -"A$" (ready weapon)"
 5190  NEXT : RETURN 
 5200  IF OT(O) <  > 1 THEN 5170
 5210 Z = 0: FOR X = 1 TO NO: REM  SEARCH FOR CONTAINED OBJECTS
 5220  IF OL(X) <  > 200 + O THEN  NEXT : GOTO 5190
 5230  IF  NOT Z THEN A$ = OB$(O): GOSUB 850: PRINT "It looks like the "A$" contains:":Z = 1
 5240 A$ = OB$(X): GOSUB 870: PRINT "     -"A$: NEXT : GOTO 5190
 6000  REM 

**** LOOK ****


 6010  IF S$ = "" AND OP$ = "" THEN 6200
 6015  IF S$ = "" THEN S$ = OP$:S = OP
 6016  IF P$ = "IN" OR P$ = "INSIDE" OR P$ = "INTO" THEN 6100
 6018  IF S > 100 THEN S = S - 100: GOTO 6030
 6020  IF  FN C(S) THEN R = S:F$ = OD$: GOTO 600: REM  DESCRIBE OBJECT
 6030  FOR I = 1 TO NC: IF CN$(I) <  > S$ THEN  NEXT :A$ = S$: GOSUB 850: PRINT "You can't see any "A$" here!": RETURN 
 6040  IF CR(I) = RM THEN R = I:F$ = CD$: GOTO 600: REM  DESCRIBE CHARACTER
 6050 A$ = S$: GOSUB 870: PRINT "You can't see "A$" here!": RETURN 
 6100  REM  LOOK INSIDE CONTAINER
 6110 Z = 0:A$ = S$: GOSUB 850: PRINT "It looks like the "A$" contains:"
 6120  FOR I = 1 TO NO: IF OL(I) = 200 + S THEN A$ = OB$(I): GOSUB 850: PRINT  TAB( 7)"-"A$:Z = 1
 6130  NEXT : IF  NOT Z THEN  PRINT  TAB( 7)" Zilch!!!"
 6140  RETURN 
 6200  FOR D = 1 TO 10: IF DR(RM,D) =  > 0 THEN  NEXT : GOTO 6500: REM   SEARCH FOR SECRET DOORS  
 6210  REM --> SPECIAL SECRET DOORS HERE <--
 6440 SN = SN + 20 +  INT (50 *  RND (1) + 1)
 6450  IF D < 9 THEN  PRINT "You find a secret door to the "DI$(D)"!": PRINT 
 6455  IF D > 8 THEN  PRINT "You find a secret door "DI$(D)"!": PRINT 
 6460 DR(RM,D) =  ABS (DR(RM,D)):D = 11: NEXT 
 6500  FOR I = 1 TO NO: IF OL(I) <  >  - RM THEN  NEXT : GOTO 6900: REM  SEARCH FOR HIDDEN OBJECTS 
 6510  REM --> SPECIAL HIDDEN OBJECTS HERE <--
 6840 SN = SN + 20 + OV(I) / 2
 6850  PRINT "You found something!":OL(I) = RM:OS(I) = 1:R = I:F$ = OD$: GOSUB 600
 6860 I = NO + 1: NEXT I: RETURN 
 6900  GOTO 700: REM  DESCRIBE ROOM
 7000  REM 

**** WEAR ****


 7010  IF S$ = "" THEN  GOSUB 800
 7020  IF OL(S) = 102 THEN  PRINT "You're already wearing that.": RETURN 
 7030  IF  NOT  FN C(S) THEN  PRINT "There's no such item nearby!": RETURN 
 7040  IF OT(S) <  > 2 THEN  PRINT "You can't wear that.": RETURN 
 7100  REM --> SPECIAL EFFECTS OF WEARING <--
 7940  IF OT(S) = 2 THEN AC = AC + O1(S):EN = EN + O2(S): REM ARMOUR
 7950 A$ = OB$(S): GOSUB 850: PRINT "Okay, you're now wearing the "A$"."
 7955  IF OL(S) = RM THEN CR = CR + 1
 7960 OL(S) = 102: RETURN 
 8000  REM 

**** REMOVE ****


 8010  IF S$ = "" THEN  GOSUB 800
 8020  IF OL(S) <  > 102 THEN  PRINT "You're not wearing that.": RETURN 
 8100  REM --> SPECIAL EFFECTS OF REMOVING <--
 8940  IF OT(S) = 2 THEN AC = AC - O1(S):EN = EN - O2(S): REM ARMOUR
 8950 A$ = OB$(S): GOSUB 850: PRINT "Okay, you're no longer wearing the "A$"."
 8960 OL(S) = 101: RETURN 
 9000  REM 

**** UNLOCK ****


 9010  IF S$ <  > "DOOR" THEN A$ = "Sorry, but Zeus has forbidden earthlings to unlock anything but doors, until they all promise to be nice for a change.": GOTO 100
 9020  FOR D = 1 TO 10: IF DR(RM,D) < 100 OR DR(RM,D) > 200 THEN  NEXT : PRINT "None of the doors here are locked!": RETURN 
 9030  FOR I = 1 TO NO: IF O1(I) <  > RM THEN  NEXT : PRINT "There's no key to this door!": GOTO 9990
 9040  IF OL(I) <  > 101 THEN  PRINT "You don't have the key to this door.": GOTO 9980
 9100  REM --> SPECIAL EFFECTS OF UNLOCKING <--
 9950 DR(RM,D) = DR(RM,D) - 100
 9960  PRINT "Okay, the door ";: IF D < 9 THEN  PRINT "to the ";
 9970  PRINT DI$(D);" is unlocked."
 9980 I = NO + 1: NEXT I
 9990 D = 11: NEXT D: RETURN 
 10000  REM 

**** FREE ****
 10010  IF S$ = "" THEN  INPUT "FREE WHOM? ";S$: GOSUB 300:S = WM
 10020  IF OT(S) <  > 7 THEN  PRINT "Nonsensical babbling is a classic symptom of a warped mind.": RETURN 
 10030  IF OL(S) <  > RM THEN A$ = S$: GOSUB 870: PRINT "You can't see "A$" here!": RETURN 
 10100  REM -> SPECIAL FREES <--
 10950  FOR I = 1 TO NC: IF CN$(I) <  > S$ THEN  NEXT : PRINT "Oops!  There's no character by that name!": RETURN 
 10960 CR(I) = RM:OL(S) = 0:CS(I) = CS(I) + 1:OB$(S) = OB$(S) + "*"
 10970 A$ = S$: GOSUB 870: PRINT A$" is now free!": RETURN 
 11000  REM 

**** SELL ****


 11010  IF S$ = "" THEN  GOSUB 800
 11013  IF S > 100 THEN  GOSUB 900
 11015  IF  NOT  FN C(S) THEN A$ = S$: GOSUB 850: PRINT "You don't have the "A$"!": RETURN 
 11020  IF OP$ = "" THEN  PRINT "SELL "S$;: INPUT " TO WHOM? ";W$: GOSUB 300:OP$ = W$:OP = WM
 11023  IF OP < 100 THEN  PRINT "Think again, bonehead.": RETURN 
 11025  IF CR(OP - 100) <  > RM THEN A$ = OP$: GOSUB 870: PRINT "You can't see "A$" here!": RETURN 
 11030 A$ = OP$: GOSUB 870
 11100  REM 
 11110 C = OP - 100: REM  CHECK ALL CHARS THAT'LL BUY
 11120  IF C = 2 OR C = 6 OR C = 7 OR C = 12 OR C = 19 OR C = 21 THEN 11900
 11130 X =  INT (4 *  RND (1) + 1): ON X GOTO 11140,11150,11160,11170
 11140  PRINT "'No thanks.'": GOTO 11200
 11150  PRINT A$" isn't interested.": GOTO 11200
 11160  PRINT A$" doesn't seem to be in a buying mood.": GOTO 11200
 11170  PRINT "'Sorry,' admits "A$", 'I'm broke.'": GOTO 11200
 11200  RETURN 
 11900  REM  SELL ITEM
 11910  PRINT "'Good deal,' says "A$", handing you "OV(S)" G.P. for it."
 11920 GP = GP + OV(S):OL(S) = 200 + OP
 11930  RETURN 
 12000  REM **** BRIEF, SUPERBRIEF, & VERBOSE ****

 12010 SB = (V$ = "SUPERBRIEF"):VR = (V$ = "VERBOSE")
 12020 A$ = V$: GOSUB 870: PRINT A$" descriptions.": RETURN 
 13000  REM 

**** BUY ****
 13010  IF S$ = "" THEN  GOSUB 800
 13015  IF S > 100 THEN  GOSUB 900
 13020  IF OL(S) = 101 OR OL(S) = 102 OR OL((OL(S) - 200) * (OL(S) > 200 AND OL(S) < 300)) = 101 THEN  PRINT "You've already got that!": RETURN 
 13030  IF OP$ = "" THEN OP = (OL(S) - 300) * (OL(S) > 300):OP$ = CN$(OP): IF OP = 0 THEN  PRINT "Nobody here seems to have any.": RETURN 
 13035  IF OP > 100 THEN OP = OP - 100
 13040 A$ = OP$: GOSUB 870: IF CR(OP) <  > RM THEN  PRINT "You can't see "A$" here!": RETURN 
 13050  IF (OL(S) - 300) * (OL(S) > 300) <  > OP THEN  PRINT A$" doesn't seem to have any.": RETURN 
 13060  IF OI(S) > GP THEN A$ = "When " + A$ + " sees that you don't have enough money, he becomes very angry and tells you kindly to stick a tree up your nose and light it.": GOTO 100
 13950  PRINT "'Good deal,' "A$" says, taking your money."
 13960 OL(S) = 101:GP = GP - O1(S): RETURN 
 13999  RETURN 
 14000  REM 

**** SAY OR CHANT ****


 14020  IF  LEFT$ (S$,1) = QT$ THEN S$ =  MID$ (S$,2)
 14030  IF  RIGHT$ (S$,1) = QT$ THEN S$ =  LEFT$ (S$, LEN (S$) - 1)
 14040  IF  RIGHT$ (S$,1) = "." OR  RIGHT$ (S$,1) = "!" OR  RIGHT$ (S$,1) = "?" THEN S$ =  LEFT$ (S$, LEN (S$) - 1)
 14100  REM --> SPECIAL THINGS TO SAY HERE <---
 14200  REM --> HELLO
 14210  IF S$ <  > "HELLO" AND S$ <  > "HI" THEN 14300
 14300  REM 
 14310  IF  LEFT$ (S$,6) = "PLEASE" AND RM = 2 THEN DR(RM,3) = 3: PRINT : PRINT "The gate creaks open.": RETURN 
 14950  PRINT :A$ = "Okay, " + QT$ + S$ + QT$ + ".": GOTO 100
 15000  REM 

**** GIVE ****


 15010  IF S$ = "" THEN  GOSUB 800
 15013  IF S > 100 THEN  GOSUB 900
 15015  IF  VAL (S$) AND  VAL (S$) <  = GP THEN 15030
 15020  IF  NOT  FN C(S) THEN A$ = S$: GOSUB 850: PRINT "You don't have the "A$"!": RETURN 
 15030  IF OP$ = "" THEN  PRINT "GIVE "S$;: INPUT " TO WHOM? ";W$: GOSUB 300:OP$ = W$:OP = WM
 15040  IF OP < 100 THEN  PRINT "Think again, vacuum skull.": RETURN 
 15050 A$ = OP$: GOSUB 870
 15060  IF CR(OP - 100) <  > RM THEN  PRINT "You can't see "A$" here!": RETURN 
 15070 C = OP - 100: REM  CHECK ALL CHARS THAT'LL ACCEPT
 15100  IF C = 6 THEN  PRINT "The beggar thanks you profusely.":OL(S) = 306:GP = GP -  VAL (S$):CD(6) = CD(6) +  VAL (S$): RETURN 
 15900  IF CD(OP - 100) < 0 THEN  PRINT A$" growls in your face!": RETURN 
 15910  PRINT A$" politely refuses your offer.": RETURN 
 16000  REM 

**** CONSUME ****


 16010  IF S$ = "" THEN  GOSUB 800
 16020  IF OT(S) <  > 4 AND OT(S) <  > 5 THEN  PRINT "That's not edible.  (Ever tried food?)": RETURN 
 16030  IF  NOT  FN C(S) THEN  PRINT "You don't have that.": RETURN 
 16100  REM -->  SPECIAL FX HERE <---
 16110  IF S <  > 13 THEN 16200
 16120 A$ = "As you drink the blue liquid, your feel a tingling sensation.  The feelings that come with the MALHALITOS spell run through your body with incredible speed and power.  Nothing else happens."
 16130 NL = 2: GOSUB 100: FOR I = 1 TO 5
 16140  POKE 1,0: POKE 2,253: POKE 3,255: POKE 4,20: POKE 5,30: POKE 6,3: CALL 835
 16150  NEXT :SP(1) = SP(1) * 3: IF SP(1) > 55 THEN SP(1) = 55
 16160 OL(13) = 0: RETURN 
 16200 :
 16460 O1(S) = O1(S) - 1:A$ = OB$(S): GOSUB 850: IF O1(S) < 1 THEN  PRINT "You consume the last of the "A$".":OL(S) = 0: GOTO 16470
 16465  PRINT "You consume some of the "A$"."
 16470 HP = HP + O2(S): IF O2(S) =  > 0 THEN  PRINT "    You feel good.": IF HP > HN THEN HP = HN
 16480  IF O2(S) < 0 THEN  PRINT "    You feel sick.": IF HP < 1 THEN  PRINT "     You curl up in a ball and die.": GOTO 39000
 16490  RETURN 
 16500  REM 

**** READ ****


 16510  IF S$ = "" THEN  GOSUB 800
 16515  IF  NOT  FN C(S) THEN  PRINT "You can't see that here.": RETURN 
 16520  IF OT(S) <  > 3 THEN F$ = OD$:R = S: GOTO 600
 16990 F$ = EF$:R = O1(S): GOTO 600
 17000  REM 

**** WAIT ****



 17010  PRINT : PRINT "Time passes.": RETURN 
 18000  REM 

**** READY ****
 18010  IF S$ = "" THEN  GOSUB 800
 18020  IF OT(S) <  > 8 THEN  PRINT "That's not a weapon!": RETURN 
 18030  IF  NOT  FN C(S) THEN  PRINT "You don't have that!": RETURN 
 18300  REM 
 18900 A$ = OB$(S): GOSUB 850: PRINT "Okay, you've got your "A$" ready."
 18910 RW = S: RETURN 
 19000  REM 

**** DIAGNOSE ****


 19010 A = HP / HN: IF A > .8 THEN ST$ = "You are still in good shape.": GOTO 19100
 19020  IF A > .5 THEN ST$ = "You are in pain.": GOTO 19100
 19030  IF A > .35 THEN ST$ = "You are hurting badly.": GOTO 19100
 19040  IF A > .2 THEN ST$ = "You are in deep trouble.": GOTO 19100
 19050 ST$ = "It looks like your number is up."
 19100  PRINT : PRINT ST$: IF  NOT HE THEN  RETURN 
 19110  PRINT "   You are also sick.": RETURN 
 20000  REM 

**** ATTACK ****
 20005  IF (S$ = "DOOR" OR S$ = "GATE") AND RM = 19 THEN  PRINT "As you raise your weapon, the gate swings open in a hurry.":DR(19,3) = 20: RETURN 
 20010  IF S < 100 OR S > 199 THEN  PRINT : PRINT "Temper, temper...": PRINT : RETURN 
 20020  IF CR(S - 100) <  > RM THEN A$ = CN$(S - 100): GOSUB 870: PRINT A$" is not in this room!": PRINT : RETURN 
 20030  IF  NOT RW THEN  PRINT "You don't have a weapon ready!": PRINT : RETURN 
 20100 A$ = CN$(S - 100): GOSUB 870: PRINT "   You attack "A$"... ";
 20110 C = O1(RW):DM = S - 100:H = WA(C) / 100 + (DX - 16) / 50:CD(DM) = CD(DM) -  INT (4 *  RND (1))
 20120  GOSUB 20700: POKE 4,VM: GOSUB 20800: IF A = 0 THEN  PRINT : RETURN 
 20130  REM --> SPECIAL EFFECTS OF HITTING MONSTER <--
 20200  REM  HIT!
 20210 D =  INT (O2(RW) *  RND (1) + 1) +  INT (LV *  RND (1)) +  INT ((ST - 16) / 3):D = D * A
 20220 B =  INT ( RND (1) * CA(DM)):D = D - B: IF D < 1 THEN D = 0: PRINT  TAB( 6)"Blow taken with no damage!": PRINT : RETURN 
 20230  IF B > D AND D > 0 THEN  PRINT  TAB( 6)"Blow absorbed with minimal damage!"
 20240  IF B > 0 AND B <  = D AND D > 0 THEN  PRINT  TAB( 6)"Blow absorbed with reduced damage!"
 20500  REM 
  ATTACK SUBROUTINES

 20501  REM 
 INFLICT DAMAGE D ON MONSTER DM
 20510 CH(DM) = CH(DM) - D:CM(DM) = CM(DM) -  INT (3 *  RND (1)): IF CH(DM) < 1 THEN 20600
 20520 A = CH(DM) / (CO(DM) * 4):A$ = CN$(DM): GOSUB 870: IF A > .8 THEN  PRINT  TAB( 7)A$" is still in good shape.": GOTO 20590
 20530  IF A > .5 THEN  PRINT  TAB( 7)A$" is in pain.": GOTO 20590
 20540  IF A > .4 THEN  PRINT  TAB( 7)A$" is hurting badly.": GOTO 20590
 20550  IF A > .2 THEN  PRINT  TAB( 7)A$" is in deep trouble!": GOTO 20590
 20560  PRINT  TAB( 7)"It looks like "A$"'s number is up."
 20590  RETURN 
 20600  REM 
 KILL CHARACTER DM
 20610 A$ = CN$(DM): GOSUB 870: PRINT : VTAB  PEEK (37): INVERSE : HTAB 9: PRINT A$" is dead!": NORMAL : PRINT :SN = SN + CO(DM) ^ 2 * 10
 20615  IF DM = 6 AND CH > 12 THEN CH = CH - 2
 20620 CR(DM) = 0: FOR J = 1 TO CP: IF CP(J) <  > DM THEN  NEXT 
 20630 CP(J) = CP(CP):J = CP + 1: NEXT :CP = CP - 1
 20640  FOR J = 1 TO CP:A = 1 - 2 * ( SGN (CD(CP(J))) =  SGN (CD(DM))):CD(CP(J)) = CD(CP(J)) + A *  INT (2 *  RND (1) + 1)
 20650  NEXT : FOR J = 1 TO NO: IF OL(J) = 300 + DM THEN OL(J) = RM
 20660  NEXT 
 20690  RETURN 
 20700  REM 
  FIND HIT SEVERITY WITH 2-HIT % OF H
 20710 R =  RND (1): IF R > .9 OR R > H THEN  PRINT "Complete miss!":A = 0: POKE 4,6: POKE 5, INT (256 *  RND (1)): POKE 7, INT (256 *  RND (1)): CALL 807: RETURN 
 20720  IF R < .05 OR R < H / 10 THEN  PRINT "Critical hit!!!" CHR$ (7):A = 2: FOR J = 1 TO 3: POKE 4, INT (50 *  RND (1) + 175): POKE 5,25: POKE 7,2: CALL 807: NEXT : RETURN 
 20730  IF R < H / 5 THEN  PRINT "Severe hit!!" CHR$ (7):A = 1.5: FOR J = 1 TO 2: POKE 4, INT (50 *  RND (1) + 175): POKE 5,25: POKE 7,2: CALL 807: NEXT : RETURN 
 20740  IF R > H / 1.25 THEN  PRINT "Glancing blow!":A = .5: POKE 4,200: POKE 5,10: POKE 7,200: CALL 807: RETURN 
 20750  PRINT "A hit!":A = 1: POKE 4, INT (50 *  RND (1) + 175): POKE 5,25: POKE 7, INT (30 *  RND (1) + 1): CALL 807: RETURN 
 20800  REM   ADJUST ABILITIES FROM FIGHTING
 20810 B = .5 * ( NOT A) +  INT (5 *  RND (1)) / 2 * A:B = B * (ST - 9) / 8
 20820 WA(O1(RW)) = WA(O1(RW)) + B * ( RND (1) * 100 > WA(O1(RW))): IF WA(O1(RW)) > 65 + ST THEN WA(O1(RW)) = WA(O1(RW)) - B
 20830  IF RC = 2 OR RC = 4 THEN B = B / 2: IF RC = 2 THEN B = B / 2
 20840  FOR I = 1 TO 4:SA(I) = SA(I) - B * ( RND (1) * 100 < SA(I)): NEXT 
 20850 SN = SN +  INT (A * 3 + 1): RETURN 
 20999  RETURN 
 21000  REM 

   ****  CAST  ****


 21005  IF S$ = "" THEN  GOSUB 800
 21010  IF  NOT SP(S) THEN  PRINT "You don't know the "S$" spell!": RETURN 
 21015  IF  INT (10 *  RND (1) + 1) < EN THEN  PRINT "    Your armour interferes with the magic!": GOTO 21050
 21020 T =  INT ((S - 1) / 5) + 1:C = (SA(T) + SP(S)) / 200:R =  RND (1)
 21030 F = (R > C OR R > .95): GOSUB 22800: IF  NOT F THEN 21099
 21040  PRINT "   The spell failed!!!": REM   MAGICAL SIDE EFFECTS HERE 
 21050  POKE 0,104: POKE 1,244: POKE 2,164: POKE 3,13: POKE 5,120: POKE 6,3: CALL 835
 21060 SD = SD +  INT (5 *  RND (1) + 1) * (30 *  RND (1) > IN): REM  AGING
 21090  RETURN 
 21099  ON S GOTO 21100,21150,21200,21250,21300,21350,21400,21450,21500,21550,21600,22650,21700,21750,21800,21850,21900,21950,22000,22050
 21100  REM 
 MALHALITOS
 21110  IF  NOT CP THEN  PRINT "Nothing happens.": RETURN 
 21120 B = 1: FOR C = 1 TO CP: IF CD(CP(C)) > 0 THEN  NEXT : PRINT  RIGHT$ ("Nothing happens. ",B * 16 + 1): RETURN 
 21130 DM = CP(C):A$ = CN$(DM): GOSUB 870: PRINT  TAB( 4)A$" chokes!": POKE 0,0: POKE 1,1: POKE 2,255: POKE 3,255: POKE 5,30: POKE 6,10: CALL 835
 21140 D =  INT (4 *  RND (1) + 1) +  INT (4 *  RND (1) + 1): GOSUB 20500:B = 0: NEXT : RETURN 
 21150  REM 
 SERVOME
 21160  IF ZZ(1) THEN  PRINT "Nothing happens.": RETURN 
 21170  PRINT "A multicolored glow surrounds your body!":AC = AC + LV + 1:ZZ(1) = LV
 21180  POKE 0,251: POKE 1,50: POKE 2,126: POKE 3,62: POKE 5,38: POKE 6,13: CALL 835: RETURN 
 21200  REM 
 ZAPPO
 21205  POKE 0,1: POKE 1,3: POKE 2,100: POKE 3,255: POKE 5,88: POKE 6,3: CALL 835
 21210  IF  NOT CP THEN  PRINT "A bolt of light splashes harmlessly off the wall.": RETURN 
 21220  GOSUB 22900
 21230 D =  INT (2 * LV *  RND (1) + 1):A$ = CN$(DM): GOSUB 870: PRINT  TAB( 4)"A bolt of light strikes "A$"!": GOSUB 20500
 21240  RETURN 
 21350  REM 
  MEDIHEP
 21360  IF HN - HP < 3 THEN  PRINT "You feel pretty good already; the spell doesn't do much.": RETURN 
 21370 A =  INT (LV / 2 *  RND (1) + 1) +  INT (LV / 2 *  RND (1) + 1):HP = HP + A: IF HP > HN THEN HP = HN
 21380  PRINT "Some of your wounds clear up."
 21390  POKE 0,255: POKE 1,253: POKE 2,0: POKE 3,1: POKE 5,88: POKE 6,6: CALL 835: RETURN 
 21400  REM 
 ZAZUP
 21410  IF ZZ(3) THEN  PRINT "Nothing happens.": RETURN 
 21420 ZZ(3) = LV:ST = ST + LV: PRINT "You can feel great strength surging through you!"
 21430  POKE 0,230: POKE 1,255: POKE 2,200: POKE 3,0: POKE 5,35: POKE 6,30: CALL 835
 21450  REM 
 ZIPPO
 21460  IF ZZ(2) THEN  PRINT "Nothing happens.": RETURN 
 21470 ZZ(2) = LV + 1: PRINT "Things around you seem to be moving more slowly."
 21490  RETURN 
 21500  REM 
 PORTAMAZ
 21510 A = RM: PRINT "The scenery fades around you in a swirl of color.  Your stomach lurches, and": PRINT "you suddenly find yourself in...": PRINT :RM =  INT (NR *  RND (1) + 1)
 21520  IF A = RM THEN  PRINT "The very same room!": RETURN 
 21530 CP = 0: GOTO 4810
 21550  REM 
 KOPPAUT
 21560  PRINT "Nothing happens.": RETURN 
 21600  REM 
 POOLAY
 21610  IF ZZ(4) THEN  PRINT "Nothing happens.": RETURN 
 21620  GOSUB 22900:A$ = CN$(DM): GOSUB 870: IF CM(DM) < 0 THEN  PRINT A$"'s face is a vivid mask of horror!": GOTO 21630
 21625  PRINT A$" wears an expression of sudden fierceness!"
 21630  POKE 0,81: POKE 1,250: POKE 2,72: POKE 3,8: POKE 5,255: POKE 6,1: CALL 835
 21640 CM(DM) =  - CM(DM):ZZ(4) = DM: RETURN 
 21650  REM 
 NUDAMON
 21660  GOSUB 22900
 21670 A$ = CN$(DM): GOSUB 870: PRINT  TAB( 4)A$" wears an expression of great surprise, and drops...":Z = 0: FOR I = 1 TO NO: IF OL(I) = 300 + DM THEN OL(I) = RM:A$ = OB$(I): GOSUB 850: PRINT  TAB( 8)"-"A$
 21680  NEXT : IF  NOT Z THEN  PRINT  TAB( 8)"Nothing!"
 21690  RETURN 
 21700  REM 
 CHAUBAIBZ
 21710  GOSUB 22900
 21720 A =  INT (NR *  RND (1) + 1): IF A = RM THEN 21720
 21730 CR(DM) = A:CP(C) = CP(CP):CP = CP - 1:A$ = CN$(DM): GOSUB 870: PRINT  TAB( 4)A$" disappears in a poof of smoke!"
 21735  NEXT : IF  NOT Z THEN  PRINT  TAB( 8)"Nothing!"
 21740  RETURN 
 21750  REM  
FROOD
 21760  GOSUB 22900
 21770 CD(DM) = CD(DM) +  INT (LV *  RND (1) + 1):A$ = CN$(DM): GOSUB 870
 21780  PRINT  TAB( 4)A$" looks at you with a strange expression that says, ": PRINT  TAB( 8)Q$"Charmed, I'm sure."Q$
 21790  RETURN 
 21800  REM 
 EPHOKE
 21810  PRINT "A bright bluish light fills the room, forcing the darkness out of every corner.": PRINT 
 21820  REM  EFFECTS OF LIGHT GO HERE
 21830  FOR I = 1 TO 5: POKE 0,50: POKE 1,2: POKE 2,100: POKE 3,254: POKE 5,50: POKE 6,2: CALL 835: NEXT 
 21849  RETURN 
 21850  REM 
 SEZME
 21860  FOR I = 1 TO 10: IF DR(RM,I) < 100 OR DR(RM,I) > 200 THEN  NEXT : PRINT "Nothing happens.": RETURN 
 21870  PRINT "You hear a click from the door ";: IF I < 9 THEN  PRINT "to the ";
 21880  PRINT DI$(I)"!":Z =  PEEK (49200) +  PEEK (49200) ^ 3:DR(RM,I) = RM(RM,I) - 100
 21890 I = 11: NEXT : RETURN 
 21900  REM 
 ANIMUS
 21910  PRINT "Nothing happens.": RETURN 
 22800  REM   ADJUST ABILITIES FROM SPELLCASTING
 22810 B = .5 * F +  INT (5 *  RND (1)) / 2 * ( NOT F):B = B * (IN - 9) / 8: REM INT. BONUS 
 22820 SP(S) = SP(S) + B * ( RND (1) * 100 > SP(S)):SA(T) = SA(T) + B * .25 * ( RND (1) * 100 > SA(T)): IF (RC = 2 AND SP(S) > 80) OR (RC = 4 AND SP(S) > 90) OR SP(S) > 100 THEN SP(S) = SP(S) - B
 22830  IF (RC = 2 AND SA(T) > 50) OR (RC = 4 AND SA(T) > 65) OR SA(T) > 80 THEN SA(T) = SA(T) - B * .25
 22840 B = B * (RC <  > 2): IF RC = 4 THEN B = B / 2
 22850  FOR I = 1 TO 5:WA(I) = WA(I) - B * ( RND (1) * 100 < WA(I)): NEXT 
 22860  IF F AND  RND (1) > .5 THEN SN = SN + 1
 22870  IF  NOT F THEN SN = SN +  INT (5 *  RND (1))
 22880  RETURN 
 22900  REM   SELECT ENEMY FOR SPELL EFFECTS
 22910  IF  NOT CP THEN  PRINT "Nothing happens.": POP : RETURN 
 22920  IF OP > 100 AND OP < 200 THEN DM = OP - 100: IF CR(DM) = RM THEN  RETURN 
 22930  FOR I = 1 TO 10:C =  INT (CP *  RND (1) + 1):DM = CP(C): IF CD(DM) > 0 THEN  NEXT : PRINT "Nothing happens.": POP : RETURN 
 22940 I = 11: NEXT : REM  MAGIC-IMMUNE MONSTERS HERE
 22990  RETURN 
 22999  RETURN 
 23000  REM 

****  PUT  ****


 23010  IF  NOT S THEN  GOSUB 800
 23030  IF  NOT OP THEN  THEN  PRINT "PUT "S$;: INPUT " INTO WHAT? ";:OP$:W$ = OP$: GOSUB 300:OP = WM
 23040  IF  NOT  FN C(S) THEN A$ = S$: GOSUB 850: PRINT "There's no "A$" here.": RETURN 
 23050  IF  NOT  FN C(OP) THEN A$ = OP$: GOSUB 850: PRINT "There's no "A$" here.": RETURN 
 23060  IF OT(S) = 7 OR S > 100 OR S < 1 OR OP > 100 OR OP < 1 THEN  PRINT "Nonsensical babbling is a sign of madness.": RETURN 
 23070 A$ = OP$: GOSUB 850:OP$ = A$:A$ = S$: GOSUB 850: IF OT(OP) <  > 1 THEN  PRINT "The "OP$" is not a container.": RETURN 
 23075  IF OL(S) = 200 + OP THEN  PRINT "It's already there!": RETURN 
 23080 Z = 0: FOR I = 1 TO NO:Z = Z + OW(I) * (OL(I) = 200 + OP): NEXT : IF Z + OW(S) > O1(OP) THEN  PRINT "The "A$" won't fit into the "OP$".": RETURN 
 23100  REM --> SPECIAL FX HERE <--
 23900  PRINT "Okay, the "A$" is now in the "OP$"."
 23910 OL(S) = 200 + OP:RW = RW * (RW <  > S): RETURN 
 24000  REM 

****  VOLUME  ****


 24010  IF S$ = "" THEN  INPUT "ENTER VOLUME (0-20) : ";S$
 24020 S =  INT ( VAL (S$)): IF S < 0 OR S > 20 THEN  PRINT "Sound volume must be from 0 to 20.": RETURN 
 24030 VM = S: IF S THEN  POKE 4,S: POKE 768,169: POKE 807,173: PRINT "Volume is now "VM".": RETURN 
 24040  POKE 768,96: POKE 807,96: PRINT "Sound is now at volume 0 (off)."
 24050  RETURN 
 29000  REM 

   **** MONSTER'S ACTIONS ****


 29010  IF MV OR  NOT CP OR (ZZ(2) AND ZZ(2) / 2 =  INT (ZZ(2) / 2)) THEN  RETURN 
 29020  FOR I = 1 TO CP: IF CD(CP(I)) +  INT (CH / 3) - 4 >  - 4 THEN  NEXT : RETURN 
 29030 I = CP + 1: NEXT 
 29040  FOR I = 1 TO CP:C = CP(I):R = CD(C) +  INT (CH / 3) - 4: IF R >  - 3 AND R < 3 THEN 29900
 29050  IF CM(C) >  INT (5 *  RND (1) - 2) THEN 29100
 29060  FOR J = 1 TO 10:D =  INT (10 *  RND (1) + 1): IF DR(RM,D) < 1 OR DR(RM,D) > 100 THEN  NEXT : GOTO 29100
 29070 J = 11: NEXT :A$ = CN$(C): GOSUB 870: PRINT "   "A$" flees ";: IF D < 9 THEN  PRINT "to the ";
 29080  PRINT DI$(D)"!": PRINT :CR(C) = DR(RM,D):CP(I) = CP(CP):I = I - 1:CP = CP - 1: IF I < CP THEN 29900
 29090  GOTO 29910
 29100  REM   MONSTER ATTACKS!
 29110 AM = C: FOR J = 1 TO 10:DM =  INT ((CP + 1) *  RND (1)): IF (R > 0 AND  NOT DM) OR (DM AND  SGN (CD(CP(DM)) + 1) =  SGN (R)) THEN  NEXT : GOTO 29900
 29120 J = 11: NEXT :A$ = CN$(C): GOSUB 870: IF  NOT DM THEN 29300
 29130 DM = CP(DM): PRINT "   "A$" attacks ";:A$ = CN$(DM): GOSUB 870: PRINT A$"-  ";
 29140 H = CO(C) -  INT (CO(C)): GOSUB 20700: POKE 4,VM
 29200 D = 0: FOR J = 1 TO CO(C):D = D +  INT (4 *  RND (1)): REM  special bonuses here
 29250  NEXT : IF A = 0 THEN 29900
 29260 D = D * A:CM(C) = CM(C) +  INT (A *  RND (1) + .5):CM(DM) = CM(DM) -  INT (A *  RND (1) + .5)
 29270 B =  INT ( RND (1) * CA(DM)):D = D - B: IF D < 1 THEN D = 0: PRINT  TAB( 6)"Blow taken with no damage!": GOTO 29900
 29280  IF B > D AND D > 0 THEN  PRINT  TAB( 6)"Blow absorbed with minimal damage!"
 29285  IF B > 0 AND B <  = D AND D > 0 THEN  PRINT  TAB( 6)"Blow absorbed with reduced damage!"
 29290  GOSUB 20500: GOTO 29900
 29300  PRINT "   "A$" attacks you!  ...";
 29310 H = CO(C) -  INT (CO(C)): GOSUB 20700: POKE 4,VM:D = 0: FOR J = 1 TO CO(C):D = D +  INT (4 *  RND (1))
 29320  NEXT : IF A = 0 THEN 29900
 29330 D = D * A:B =  INT ( RND (1) * AC):D = D - B: IF D < 1 AND B > 0 THEN D = 0: PRINT  TAB( 6)"Armour completely absorbs the blow!": GOTO 29900
 29340  IF B > D AND D > 0 THEN  PRINT  TAB( 6)"Blow mostly absorbed by armour."
 29350  IF B > 0 AND B <  = D AND D > 0 THEN  PRINT  TAB( 6)"Damage reduced by armour."
 29360 HP = HP - D: IF HP < 0 THEN  PRINT : VTAB  PEEK (37): INVERSE : HTAB 9: PRINT "You are slain!": NORMAL : PRINT : GOTO 39000
 29370 A = HP / HN: IF A > .8 THEN ST$ = "You are still in good shape.": GOTO 29450
 29380  IF A > .5 THEN ST$ = "You are in pain.": GOTO 29450
 29390  IF A > .35 THEN ST$ = "You are hurting badly.": GOTO 29450
 29400  IF A > .2 THEN ST$ = "You are in deep trouble!": GOTO 29450
 29410 ST$ = "It looks like your number is up."
 29450  PRINT  TAB( 7)ST$: GOTO 29900
 29900  IF I < CP THEN  PRINT : NEXT 
 29910  RETURN 
 39000  REM 
   SEND PLAYER HOME
 39010  REM  if any reason not to, then just  RETURN
 39020  IF CR(11) = RM AND HP =  > 1 THEN  PRINT "For the safe return of Princess Lisa, King Schumm awards you 100 gold and": PRINT "500 experience points.":EX = EX + 500:GP = GP + 100
 39070  IF HP =  > 1 THEN  PRINT "You successfully shimmer back to the Shire."
 39075  ONERR  GOTO 39080
 39080  PRINT : PRINT : HTAB 28: PRINT "Insert Shire Main Disk.": HTAB 27: PRINT "Press <C> to continue: ";
 39085  GET A$: IF A$ <  > "C" THEN 39085
 39090  PRINT "Continue": PRINT D$"RUN BACK"
                                                                                                        