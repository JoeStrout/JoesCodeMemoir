
 0 D$ =  CHR$ (4): PRINT D$"OPEN FILE EDIT.TXT": PRINT D$"WRITE FILE EDIT.TXT": POKE 33,73: LIST : PRINT D$"CLOSE": TEXT : END 
 1  GOTO 10
 5  PRINT  CHR$ (4)"SAVE FILE EDIT": END 
 10  REM 

    FILE EDIT.4


 20  DIM DI(4,200),RN$(60),DR(60,10),CN$(50),CD(50),CR(50),CM(50),CH(50),CA(50),CO(50)
 25  DIM OB$(60),OL(60),OT(60),OV(60),OW(60),O1(60),O2(60)
 90  GOSUB 40000: GOTO 2000
 100  REM 

//// GENERAL SUBROUTINES\\\\
 110  REM 

//// PRINT ROUTINE \\\\
 111  REM R=RECORD TO START AT
 120  PRINT :Q = R:MT = 0
 130 H =  PEEK (1403) + 1: PRINT : PRINT D$"READ"F$(F)",R"R: INPUT A$: VTAB  PEEK (37): HTAB H: IF  RIGHT$ (A$,1) = "@" THEN 150
 140  PRINT A$: PRINT D$:R = Q: RETURN 
 150 R =  VAL ( MID$ (A$, LEN (A$) - 2)):B$ =  LEFT$ (A$, LEN (A$) - 3): PRINT B$;: GOTO 130
 400  REM 

//// INPUT W/DEFAULT A$ \\\\
 410  PRINT A$;: FOR I = 1 TO  LEN (A$): PRINT  CHR$ (8);: NEXT :H =  PEEK (1403) + 1
 420  INPUT "";B$: IF B$ = "" THEN B$ = A$: VTAB  PEEK (37): HTAB H: PRINT B$
 430  RETURN 
 450  REM 

//// INPUT W/NUMERIC DEFAULT \\\\
 460  PRINT A;: FOR I = 1 TO  LEN ( STR$ (A)): PRINT  CHR$ (8);: NEXT :H =  PEEK (1403) + 1
 470  INPUT "";B$: IF B$ = "" THEN B$ =  STR$ (A): VTAB  PEEK (37): HTAB H: PRINT B$
 480 B =  VAL (B$): RETURN 
 2000  REM 

//// MAIN MENU \\\\
 2010  TEXT : HOME : INVERSE : HTAB 35: PRINT " MAIN MENU ": NORMAL : PRINT : PRINT : PRINT 
 2020  PRINT  TAB( 20)"1. EDIT DESCRIPTION(S)": PRINT  TAB( 20)"2. EDIT INFORMATION"
 2030  PRINT  TAB( 20)"3. LIST NAMES"
 2040  PRINT  TAB( 20)"4. QUIT WITHOUT SAVING INFO": PRINT  TAB( 20)"5. QUIT AND SAVE"
 2050  PRINT : PRINT "SELECT (1-5): ";
 2060  GET A$: IF A$ < "1" OR A$ > "5" THEN 2060
 2065  PRINT A$
 2070  ON  VAL (A$) GOSUB 3000,4000,6000,9000,10000
 2080  GOTO 2000
 3000  REM 

//// EDIT DESCRIPTION(S) \\\\
 3010  HOME : INVERSE : HTAB 30: PRINT " EDIT DESCRIPTION(S) ": NORMAL : PRINT : PRINT : PRINT 
 3020  FOR I = 1 TO NF: PRINT  TAB( 10);I"."F$(I): NEXT : PRINT 
 3030  PRINT "SELECT FILE TO EDIT: ";
 3040  GET A$: IF (A$ < "0" OR  VAL (A$) > NF) AND A$ <  >  CHR$ (27) THEN 3040
 3050  IF A$ =  CHR$ (27) OR A$ = "0" THEN  PRINT : PRINT D$"CLOSE": RETURN 
 3060 F =  VAL (A$): PRINT F: VTAB 2: CALL  - 958: PRINT F$(F)
 3070  PRINT : PRINT : INPUT "ENTER RECORD NUMBER: ";R
 3080  PRINT D$"OPEN"F$(F)",L207": PRINT : PRINT : PRINT "FILE "F$(F)", RECORD "R":": GOSUB 110
 3090  PRINT : PRINT "DO YOU WANT TO REPLACE THIS DESCRIPTION? ";
 3100  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 3100
 3110  PRINT A$: IF A$ = "N" THEN 3000
 3120  PRINT : PRINT "ENTER DESCRIPTION FOR FILE "F$(F)", RECORD "R":"
 3130  FOR I = 1 TO 200:DI(F,I) = DI(F,I) * (DI(F,I) <  > R): NEXT : REM  ZAP REC MARKERS
 3140 R1 = R: FOR I = R + 1 TO 200: IF DI(F,I) > 0 THEN  NEXT 
 3150 R2 = I:I = 201: NEXT : REM  R1=RECORD TO WRITE TO, R2=REC TO JUMP TO
 3155  PRINT "R1="R1"    R2="R2
 3160  PRINT :S$ = "":A$ = S$:C = 0:H$ = "_ " +  CHR$ (8) +  CHR$ (8)
 3170  GET A$: IF A$ =  CHR$ (21) THEN A$ = " "
 3175  PRINT A$;H$;:S$ = S$ + A$:C = C + 1: IF A$ =  CHR$ (8) AND  LEN (S$) < 3 THEN C = 0:S$ = "": GOTO 3170
 3178  IF A$ =  CHR$ (8) THEN C = C - 2:S$ =  LEFT$ (S$, LEN (S$) - 2)
 3180  IF A$ =  CHR$ (13) THEN 3210
 3190  IF C < 200 AND A$ <  >  CHR$ (20) THEN 3170
 3200  PRINT  CHR$ (7);:H =  PEEK (1403) + 1:V =  PEEK (37) + 1:S$ = S$ +  STR$ (R2) + "@"
 3210  REM   STORE STRING
 3215  PRINT "(WRITING TO RECORD "R1")"
 3220  PRINT : PRINT D$"WRITE"F$(F)",R"R1: PRINT Q$;S$;Q$
 3230  PRINT D$: IF  RIGHT$ (S$,1) <  > "@" THEN 3260
 3240 S$ = "":C = 0:DI(F,R1) = R:DI(F,R2) = R:R1 = R2: FOR I = 1 TO 200: IF DI(F,I) > 0 THEN  NEXT 
 3250 R2 = I:I = 201: NEXT I: VTAB V: HTAB H: GOTO 3170
 3260 DI(F,R1) = R: VTAB 3: CALL  - 958: PRINT : GOTO 3080
 4000  REM 

//// EDIT INFORMATION \\\\
 4010  TEXT : HOME : HTAB 33: INVERSE : PRINT " EDIT INFORMATION ": NORMAL 
 4020  PRINT : PRINT : PRINT  TAB( 10)"1. EDIT ROOM INFO": PRINT  TAB( 10)"2. EDIT OBJECT INFO": PRINT  TAB( 10)"3. EDIT CHARACTER INFO"
 4030  PRINT "SELECT (1-3): ";
 4040  GET A$: IF (A$ < "0" OR A$ > "3") AND A$ <  >  CHR$ (27) THEN 4040
 4050  IF A$ = "0" OR A$ =  CHR$ (27) THEN  RETURN 
 4060  PRINT A$:F =  VAL (A$): PRINT : PRINT : INPUT "ENTER RECORD NUMBER: ";R
 4070  ON F GOTO 4100,4300,4600
 4100  REM 
/// EDIT ROOM INFO \\\
 4110  HOME : INVERSE : HTAB 37: PRINT " ROOM "R" ": NORMAL : PRINT : PRINT 
 4120  PRINT D$"OPEN"F$(F)",L207": GOSUB 100: PRINT D$"CLOSE": PRINT 
 4130 A$ = RN$(R): PRINT "NAME: ";: GOSUB 400:RN$(R) = B$
 4135  IF R > NR THEN NR = R:RN$(R + 1) = "END"
 4140  PRINT : PRINT "DOORS:": FOR D = 1 TO 10: PRINT "   "DR$(D)": ";:A = DR(R,D): GOSUB 450:DR(R,D) = B: NEXT 
 4150  PRINT : PRINT : GOTO 4000
 4300  REM 
/// EDIT OBJECT INFO \\\
 4310  HOME : INVERSE : HTAB 36: PRINT " OBJECT "R" ": NORMAL : PRINT : PRINT 
 4315  IF NO < R THEN NO = R: REM  UPDATE # OF OBJECTS
 4320  PRINT D$"OPEN"F$(F)",L207": GOSUB 100: PRINT D$"CLOSE": PRINT 
 4330 A$ = OB$(R): PRINT "NAME: ";: GOSUB 400:OB$(R) = B$
 4340 A = OL(R): PRINT "LOCATION: ";: GOSUB 450:OL(R) = B
 4350 A = OT(R): PRINT "TYPE: ";: GOSUB 450:OT(R) = B
 4355 A = OV(R): PRINT "VALUE: ";: GOSUB 450:OV(R) = B
 4360 A = OW(R): PRINT "WEIGHT/SIZE: ";: GOSUB 450:OW(R) = B
 4370  IF OT(R) = 1 THEN  PRINT "CAPACITY: ";: GOTO 4420
 4375  IF OT(R) = 2 THEN  PRINT "AC ADJUSTMENT: ";: GOTO 4420
 4380  IF OT(R) = 3 THEN  PRINT "EFFECT NO.: ";: GOTO 4420
 4390  IF OT(R) = 4 THEN  PRINT "SIPS/BITES: ";: GOTO 4420
 4400  IF OT(R) = 6 THEN  PRINT "LOCATION OF DOOR: ";: GOTO 4420
 4405  IF OT(R) = 7 THEN  PRINT "KEY NUMBER: ";: GOTO 4420
 4407  IF OT(R) = 8 THEN  PRINT "WEAPON CLASS: ";: GOTO 4420
 4408  IF OT(R) = 9 THEN  PRINT "SPELL NO.: ";: GOTO 4420
 4410  PRINT "OTHER INFORMATION #1: ";
 4420 A = O1(R): GOSUB 450:O1(R) = B
 4440  IF OT(R) = 2 THEN  PRINT "ENCUMBERANCE: ";: GOTO 4500
 4450  IF OT(R) = 4 THEN  PRINT "HEALING EFFECT: ";: GOTO 4500
 4470  IF OT(R) = 8 THEN  PRINT "MAX. DAMAGE: ";: GOTO 4500
 4472  IF OT(R) = 9 THEN  PRINT "CHARGES: ";: GOTO 4500
 4490  PRINT "OTHER INFORMATION #2: ";
 4500 A = O2(R): GOSUB 450:O2(R) = B
 4590  GOTO 4000
 4600  REM 
/// EDIT CHARACTER INFO \\\
 4610  HOME : INVERSE : HTAB 34: PRINT " CHARACTER "R" ": NORMAL : PRINT : PRINT 
 4615  IF R > NC THEN NC = R: REM  UPDATE # OF CHARS
 4620  PRINT D$"OPEN"F$(F)",L207": GOSUB 100: PRINT D$"CLOSE": PRINT 
 4630 A$ = CN$(R): PRINT "NAME: ";: GOSUB 400:CN$(R) = B$
 4640 A = CD(R): PRINT "DISPOSITION: ";: GOSUB 450:CD(R) = B
 4650 A = CR(R): PRINT "LOCATION: ";: GOSUB 450:CR(R) = B
 4660 A = CM(R): PRINT "COURAGE: ";: GOSUB 450:CM(R) = B
 4670 A = CH(R): PRINT "HIT POINTS: ";: GOSUB 450:CH(R) = B
 4680 A = CA(R): PRINT "ARMOUR CLASS: ";: GOSUB 450:CA(R) = B
 4690 A = CO(R): PRINT "OFFENSIVE LEVEL: ";: GOSUB 450:CO(R) = B
 4990  PRINT : PRINT : GOTO 4000
 5000  REM 

//// EDIT RESPONSE FILE \\\\
 6000  REM 

//// LIST NAMES \\\\
 6010  HOME : INVERSE : HTAB 35: PRINT " LIST NAMES ": PRINT : PRINT : NORMAL 
 6020  PRINT "LIST <R>OOMS": PRINT "     <O>BJECTS": PRINT "     <C>HARACTERS"
 6030  PRINT : PRINT "     <_>"; CHR$ (8) CHR$ (8);
 6040  GET A$: IF A$ <  > "R" AND A$ <  > "O" AND A$ <  > "C" AND A$ <  >  CHR$ (27) THEN 6040
 6050  IF A$ =  CHR$ (27) THEN  RETURN 
 6055  PRINT A$: PRINT : PRINT 
 6060  IF A$ <  > "R" THEN 6300
 6070 A = 1
 6080  IF RN$(A) = "END" THEN 6900
 6090  PRINT "ROOM #"A":  ";RN$(A):A = A + 1: GOTO 6080
 6300  IF A$ <  > "O" THEN 6600
 6310 A = 1
 6320  IF OB$(A) = "" THEN 6900
 6330  PRINT "OBJECT #"A":  ";OB$(A):A = A + 1: GOTO 6320
 6600 A = 1
 6610  IF CN$(A) = "" THEN 6900
 6620  PRINT "CHARACTER #"A":  ";CN$(A):A = A + 1: GOTO 6610
 6900  PRINT : PRINT "PRESS ANY KEY: ";: GET A$: RETURN 
 9000  REM 
//// QUIT W/O SAVING \\\\
 9010  PRINT : PRINT : FLASH : PRINT "NOTE";: NORMAL : PRINT ": Any changes you have made to the infomation files"
 9020  PRINT : PRINT "will be lost!"
 9030  PRINT : PRINT "DO YOU REALLY WANT TO DO THIS (Y/N)? ";
 9040  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 9040
 9050  IF A$ = "N" THEN  RETURN 
 9060  PRINT "OK. 'BYE.": END 
 10000  REM 

//// QUIT AND SAVE \\\\
 10010  REM  FIRST SAVE DESCRIPTION INFORMATION
 10020  PRINT D$"OPENDESC.INFO": PRINT D$"DELETEDESC.INFO": PRINT D$"OPENDESC.INFO": PRINT D$"WRITEDESC.INFO"
 10030  FOR X = 1 TO NF: FOR Y = 1 TO 200: PRINT DI(X,Y): NEXT Y,X
 10040  PRINT D$"CLOSE"
 10050  PRINT D$"OPENROOM INFO": PRINT D$"DELETEROOM INFO": PRINT D$"OPENROOM INFO": PRINT D$"WRITEROOM INFO"
 10060  FOR RM = 1 TO NR
 10070  PRINT RN$(RM): FOR D = 1 TO 10: PRINT DR(RM,D): NEXT 
 10080  NEXT : PRINT "END"
 10090  PRINT D$"CLOSE": PRINT D$"OPENOBJ INFO": PRINT D$"DELETEOBJ INFO": PRINT D$"OPENOBJ INFO": PRINT D$"WRITEOBJ INFO"
 10100  PRINT NO: FOR I = 1 TO NO
 10110  PRINT OB$(I): PRINT OL(I): PRINT OT(I): PRINT OV(I): PRINT OW(I): PRINT O1(I): PRINT O2(I)
 10120  NEXT : PRINT D$"CLOSE"
 10130  PRINT D$"OPENCHAR INFO": PRINT D$"DELETECHAR INFO": PRINT D$"OPENCHAR INFO": PRINT D$"WRITECHAR INFO"
 10140  PRINT NC: FOR I = 1 TO NC
 10150  PRINT CN$(I): PRINT CD(I): PRINT CR(I): PRINT CM(I): PRINT CH(I): PRINT CA(I): PRINT CO(I)
 10160  NEXT : PRINT D$"CLOSE"
 10500  PRINT : PRINT "PRESS <C> TO CONTINUE EDITING,": PRINT "  OR ANY OTHER KEY TO QUIT: ";
 10510  GET A$: IF A$ = "C" OR A$ = "c" THEN  RETURN 
 10520  PRINT A$: END 
 40000  REM //// INIT ROUTINE \\\\
 40010 F$(1) = "ROOM DESC":F$(2) = "OBJ DESC":F$(3) = "CHAR DESC":F$(4) = "EFFECTS":NF = 4
 40020 D$ =  CHR$ (4):Q$ =  CHR$ (34): PRINT D$"PR#3": PRINT 
 40100  PRINT D$"OPENDESC.INFO": PRINT D$"READDESC.INFO"
 40110  FOR X = 1 TO NF: FOR Y = 1 TO 200: INPUT DI(X,Y): NEXT Y,X
 40120  PRINT D$"CLOSE"
 40130  PRINT D$"OPENROOM INFO": PRINT D$"READROOM INFO":NR = 1
 40140  INPUT RN$(NR): IF RN$(NR) = "END" THEN 40160
 40150  FOR D = 1 TO 10: INPUT DR(NR,D): NEXT :NR = NR + 1: GOTO 40140
 40160  PRINT D$"CLOSE":NR = NR - 1: PRINT D$"OPENOBJ INFO": PRINT D$"READOBJ INFO"
 40170  INPUT NO: FOR I = 1 TO NO
 40180  INPUT OB$(I): INPUT OL(I): INPUT OT(I): INPUT OV(I): INPUT OW(I): INPUT O1(I): INPUT O2(I)
 40190  NEXT : PRINT D$"CLOSE"
 40200  FOR D = 1 TO 10: READ DR$(D): NEXT : DATA " N"," E"," S"," W",NE,SE,SW,NW," U"," D"
 40210  PRINT D$"OPENCHAR INFO": PRINT D$"READCHAR INFO"
 40220  INPUT NC: FOR I = 1 TO NC
 40230  INPUT CN$(I): INPUT CD(I): INPUT CR(I): INPUT CM(I): INPUT CH(I): INPUT CA(I): INPUT CO(I)
 40240  NEXT : PRINT D$"CLOSE"
 41999  RETURN 
 42000  REM  PATCH IN DI(*,*) VALUES
 42010  FOR I = 1 TO 60:DI(1,I) = I: NEXT 
 42020  FOR I = 1 TO 75:DI(2,I) = I: NEXT 
 42030  FOR I = 1 TO 30:DI(3,I) = I: NEXT 
 42040  FOR I = 1 TO 50:DI(4,I) = I: NEXT 
 49999  RETURN 
 50000  REM //// FILE DUMP \\\\
 50010 F$ = "EFFECTS"
 50020 R = 1
 50030 D$ =  CHR$ (4): PRINT D$"OPEN"F$",L207"
 50040  PRINT D$"READ"F$",R"R: PRINT "RECORD #"R":": INPUT A$: PRINT A$
 50050 R = R + 1: GOTO 50040
 55000  REM //// FILE EMPTIER \\\\
 55010 F$ = "EFFECTS"
 55020 MR = 50: REM  MAX ENTRIES 
 55030 D$ =  CHR$ (4): PRINT D$"OPEN"F$",L207":R = 1
 55040  PRINT D$"WRITE"F$",R"R: PRINT "[ EMPTY. ]":R = R + 1: IF R <  = MR THEN 55040
 55050  PRINT D$"CLOSE": END 
                                                                                                                                                              