10  REM        WORK OF
15  REM        THE MAGE                            
20  TEXT : HOME : DIM FR$(400),RD$(100),RD(100,4),OD$(200),OB$(200),RO(200),OL(200),OV(200),OT(200),VR(200),TP(200)
21 H$ =  CHR$ (8)
24  DIM RT(20):RT(1) = 150:RT(2) = 420:RT(3) = 520:RT(4) = 610:RT(5) = 710:RT(6) = 810:RT(7) = 920:RT(8) = 1020:RT(9) = 1120:RT(10) = 1220:RT(11) = 1420
25 RT(12) = 1520:RT(13) = 1610:RT(14) = 1720:RT(15) = 1810
29  GOTO 100
30  HTAB TB: INPUT "";D$: IF D$ <  > "" THEN X =  VAL (D$): RETURN 
35 X = DF: VTAB  PEEK (37): HTAB TB: PRINT DF: RETURN 
37  NEXT : GOTO 350
40  HTAB TB: INPUT "";D$: IF D$ = "" THEN X$ = DF$: VTAB  PEEK (37): HTAB TB: PRINT DF$: RETURN 
45 X$ = D$: CALL  - 958: RETURN 
50  POKE 33,14: POKE 32,25: POKE 34,7: POKE 35,17: HOME : INVERSE : PRINT "             ": RETURN 
100  HOME : INVERSE : PRINT "MAIN MENU": NORMAL : VTAB 6: PRINT  TAB 9);"{R}  ROOMS": PRINT : PRINT  TAB 9);"{O}  OBJECTS": PRINT : PRINT  TAB 9)"{S}  SPECIFICS": PRINT 
103  PRINT  TAB 8);"{ESC} SAVE/QUIT": PRINT  TAB 9);"{L}  LOAD FILE"
105  PRINT : PRINT : PRINT "SELECT==>";: GET MM$: IF MM$ <  > "O" AND MM$ <  > "R" AND MM$ <  > "L" AND MM$ <  > "S" AND MM$ <  >  CHR$ (27) THEN 100
108  IF MM$ =  CHR$ (27) THEN MM$ = "ESC"
120  PRINT MM$: FOR I = 1 TO 10:BUZZ =  PEEK ( - 16336): NEXT 
125  IF MM$ = "ESC" THEN 450
130  IF MM$ = "S" THEN 350
140  IF MM$ = "O" THEN 250
145  IF MM$ = "L" THEN 700
150  REM ======ROOMS============
152  HOME : PRINT NR" ROOMS": PRINT : PRINT "    {RETURN} TO ADD ON                         {E}   TO EDIT": PRINT : PRINT "SELECT==>";: GET AE$: IF AE$ =  CHR$ (13) THEN NR = NR + 1:R = NR: GOTO 160
155  PRINT : INPUT "ROOM:";R$: IF R$ = "" THEN  VTAB  PEEK (37): GOTO 155
157 R =  VAL (R$)
160  HOME : INVERSE : PRINT "ROOM "R":": NORMAL : PRINT : PRINT "DESCRIPTION:"
165 TB = 1
170 DF$ = RD$(R): PRINT DF$: VTAB 4: GOSUB 40: IF X$ = "" THEN 160
175 RD$(R) = X$:TB = 5
180  PRINT : PRINT : PRINT "DOORS:": PRINT :DF = RD(R,1): PRINT "  N-"DF;H$;: GOSUB 30:RD(R,1) = X:DF = RD(R,2): PRINT "  E-"DF;H$;: GOSUB 30:RD(R,2) = X
190 DF = RD(R,3): PRINT "  S-"DF;H$;: GOSUB 30:RD(R,3) = X:DF = RD(R,4): PRINT "  W-"DF;H$;: GOSUB 30:RD(R,4) = X
195  PRINT : VTAB 20
200  PRINT  TAB 9);"{RETURN}  NEXT ROOM                       {M}     MAIN MENU": PRINT : PRINT "  SELECT==>";: GET RSM$: IF RSM$ <  >  CHR$ (13) AND RSM$ <  > "M" AND RSM$ <  >  CHR$ (27) THEN  PRINT : VTAB 20: GOTO 200
220  IF RSM$ =  CHR$ (13) THEN NR = NR + 1:R = NR: GOTO 160
230  GOTO 100
250  REM ========OBJECTS=========
252  HOME : PRINT NO" OBJECTS": PRINT : PRINT : PRINT "      {RETURN} ADD ON                            {E}   EDIT": PRINT : PRINT "SELECT==>";: GET AE$: IF AE$ =  CHR$ (13) THEN NO = NO + 1:O = NO: GOTO 260
253  IF AE$ <  > "E" THEN 252
255  INPUT "OBJECT NUMBER:";O$: IF O$ = "" THEN  VTAB  PEEK (37): GOTO 255
257 O =  VAL (O$)
260  HOME : INVERSE : PRINT "OBJECT "O":": NORMAL : PRINT : PRINT "KEYWORD:";OB$(O):TB = 9: VTAB 3:DF$ = OB$(O): GOSUB 40: IF X$ = "" THEN 260
265 OB$(O) = X$
270 TB = 1: VTAB 5: PRINT "DESCRIPTION:": PRINT OD$(O): VTAB 6:DF$ = OD$(O): GOSUB 40: IF X$ = "" THEN X$ = "X"
275 OD$(O) = X$:TB = 12
280 VT =  PEEK (37) + 1: GOSUB 50: PRINT " ROOM NUMBER ": PRINT "             ": TEXT : NORMAL : VTAB VT: PRINT : PRINT "  LOCATION:";RO(O);:DF = RO(O): GOSUB 30:RO(O) = X
285 VT =  PEEK (37) + 1: GOSUB 50: PRINT "0 - NORMAL   ": PRINT "1 - CONTAINER": PRINT "2 - MONSTER  ": PRINT "3 - WEAPON   ": PRINT "4 - SPELL    ": PRINT "             ": TEXT : NORMAL : VTAB VT
290  PRINT "      TYPE:"TP(O);:DF = TP(O): GOSUB 30:TP(O) = X:VT =  PEEK (37) + 1: GOSUB 50: PRINT "    VALUE    ": PRINT "  IN COINS   ": PRINT "             ": NORMAL : TEXT : VTAB VT: IF TP(O) < 2 THEN  PRINT "     VALUE:"OV(O);:DF = OV(O): GOSUB 30
291 VT =  PEEK (37) + 1: GOSUB 50: IF TP(O) > 2 AND OV(O) = 0 THEN OV(O) = 3
292  IF TP(O) = 3 THEN  PRINT "   DAMAGE    ": PRINT "  ( 1 - 5 )  ": PRINT "             ": TEXT : NORMAL : VTAB VT: PRINT "    DAMAGE:"OV(O);:DF = OV(O): GOSUB 30:OV(O) = X
293  IF TP(O) = 2 AND OV(O) = 0 THEN OV(O) = 6
294  IF TP(O) = 4 THEN  PRINT "    SPELL    ": PRINT "    LEVEL    ": PRINT "  ( 1 - 5 )  ": PRINT "             ": TEXT : NORMAL : VTAB VT: PRINT "     LEVEL:"OV(O);:DF = OV(O): GOSUB 30:OV(O) = X
295  IF TP(O) = 2 THEN  PRINT "  TOUGHNESS  ": PRINT "     OF      ": PRINT "   MONSTER   ": PRINT "             ": TEXT : NORMAL : VTAB VT: PRINT "HIT POINTS:";OV(O);:DF = OV(O): GOSUB 30:OV(O) = X
298 VT =  PEEK (37) + 1: GOSUB 50: IF TP(O) <  > 2 THEN  PRINT "0 - NORMAL   ": PRINT "1 - HIDDEN   ": PRINT "2 - TOO HEAVY": PRINT "3 -RIDICULOUS": PRINT "             ": NORMAL : TEXT : VTAB VT + (TP(O) < 3) * 2
300  IF TP(O) <  > 2 THEN  PRINT " TAKE CODE:"OT(O);:DF = OT(O): GOSUB 30:OT(O) = X:VT =  PEEK (37) + 1: GOSUB 50: TEXT : VTAB VT
302  IF TP(O) = 2 AND OT(O) = 0 THEN OT(O) = 2
305  IF TP(O) = 2 THEN  PRINT "    SKILL    ": PRINT "     OF      ": PRINT "   MONSTER   ": PRINT "  ( 1 - 5 )  ": PRINT "             ": TEXT : NORMAL : VTAB VT: PRINT "     LEVEL:"OT(O);:DF = OT(O): GOSUB 30:OT(O) = X
308 VT =  PEEK (37) + 1: GOSUB 50: PRINT "   OBJECT    ": PRINT "  CONNECTED  ": PRINT " TO THIS ONE ": PRINT "             ": NORMAL : TEXT : VTAB VT: PRINT "      LINK:"OL(O);:DF = OL(O): GOSUB 30:OL(O) = X
310  PRINT : VTAB 20: PRINT  TAB 9);"{RETURN}  NEXT OBJECT                      {M}    MAIN MENU": PRINT : PRINT "  SELECT==>";: GET OSM$: PRINT OSM$: IF OSM$ <  > "M" AND OSM$ <  >  CHR$ (13) THEN  VTAB 20: GOTO 310
320  IF OSM$ = "M" THEN 100
330 O = O + 1: IF NO < O THEN NO = O
340  GOTO 260
350  REM =======SPECS===========
360  HOME : INVERSE : PRINT "SPECIFICS": NORMAL : VTAB 3
365  PRINT "[1] SPECIALIZED VERB  [9] PLAYERS ATTACK[2] NEW ROOM          [A] MONSTER DEATH [3] --GET--           [B] PLAYER DEATH  [4] --DROP--          [C] --WEAR--      [5] --LOOK--          [D] --REMOVE--"
370  PRINT "[6] --OPEN--          [E] --MEMORIZE--  [7] --INV--           [F] --INVOKE--    [8] --READY--"
380  PRINT : PRINT "{ESC} EDIT": PRINT "{M} MAIN MENU": PRINT : PRINT "SELECT==>";: GET SSM$: IF SSM$ = "M" THEN 100
381  IF SSM$ =  CHR$ (27) THEN 415
383  IF  ASC (SSM$) > 57 THEN SM =  ASC (SSM$) - 55: GOTO 390
385 SM =  VAL (SSM$)
390  INVERSE : PRINT SM: NORMAL 
400  VTAB 15: HTAB 1: CALL  - 978: PRINT "ENTER IT IN APPLESOFT:": PRINT : PRINT "]";RT(SM);" ";: CALL  - 657: PRINT : PRINT "REGISTERED."
402 A$ = "": FOR X = 512 TO 767: IF  PEEK (X) <  > 141 THEN A$ = A$ +  CHR$ ( PEEK (X) - 128): NEXT 
405 LN$ = A$: IF A$ = "" THEN 350
410 NF = NF + 1:FR$(NF) =  STR$ (RT(SM)) + LN$:RT(SM) = RT(SM) + 1: GOTO 350
415  REM --------EDIT-----------
420  VTAB  PEEK (37) - 2: HTAB 1: PRINT "{RETURN} NEXT FILE {C} CHANGE  {M} MENU": CALL  - 958: CALL  - 868
425  FOR I = 1 TO NF: VTAB 15: HTAB 1: CALL  - 958: IF  LEN (FR$(I)) > 2 THEN  PRINT FR$(I);: GET A$: PRINT : IF A$ <  >  CHR$ (13) THEN 430
427  NEXT : GOTO 350
430  IF A$ = "C" THEN SM = 0:RT(0) =  VAL ( LEFT$ (FR$(I),4)): VTAB 17: HTAB 3: PRINT FR$(I):FR$(I) = "": GOTO 400
440  IF A$ = "M" THEN 100
445 I = I - 1: NEXT : GOTO 350
450  REM ========SAVE============
460  HOME : INVERSE : PRINT "SAVE/QUIT": NORMAL 
470  VTAB 4: PRINT  TAB 9)"{Q}  QUIT": PRINT : PRINT  TAB 6);"{ANY KEY}  SAVE & QUIT": VTAB 18: PRINT "  SELECT==>";: GET SQ$: IF SQ$ =  CHR$ (27) THEN 100
480  PRINT SQ$: IF SQ$ = "Q" THEN  PRINT : PRINT : END 
490  PRINT : PRINT : INPUT "FILENAME:";FL$:OP$ = "OPEN" + FL$:DL$ = "DELETE" + FL$:WT$ = "WRITE" + FL$
495 C$ = ","
500  PRINT "INSERT DISK AND PRESS {RETURN}": GET A$: IF A$ <  >  CHR$ (13) THEN  VTAB  PEEK (37): GOTO 500
510  PRINT : PRINT OP$: PRINT DL$: PRINT OP$: PRINT WT$
512  PRINT "LOAD WHIZARDS"
515  PRINT "10REM  THIS IS WRITTEN OVER"
520  FOR I = 1 TO NF
522  IF FR$(I) <  >  CHR$ (13) THEN  PRINT FR$(I)
523  NEXT 
525  PRINT "REM"
530  PRINT "20499DATA";NR;C$;NO
540  FOR I = 1 TO NR
550  PRINT 20499 + I;"DATA";Q$;RD$(I);Q$;C$;RD(I,1);C$;RD(I,2);C$;RD(I,3);C$;RD(I,4)
560  NEXT 
570  FOR I = 1 TO NO
580  PRINT 21000 + I;"DATA";OB$(I);C$;Q$;OD$(I);Q$;C$;RO(I);C$;TP(I);C$;OV(I);C$;OT(I);C$;OL(I)
590  NEXT 
595  PRINT "RUN"
600  PRINT "CLOSE"
605  GOTO 100
700  REM =========LOAD==========
710  HOME : INPUT "FILENAME:";FL$:D$ =  CHR$ (4)
720 OP$ = D$ + "OPEN" + FL$:RED$ = D$ + "READ" + FL$
730  PRINT OP$: PRINT RED$
740  INPUT G$: INPUT G$:NF = 1
750  GET G$: IF G$ <  >  CHR$ (13) THEN FR$(NF) = FR$(NF) + G$: GOTO 750
753  IF FR$(NF) = "REM" THEN FR$(NF) = "":NF = NF - 1: GOTO 760
755 FR$(NF) = FR$(NF) + G$: IF  ASC (FR$(NF)) <  > 13 THEN NF = NF + 1: GOTO 750
758 FR$(NF) = "": GOTO 750
760  INPUT G$,NO:NR =  VAL ( MID$ (G$,10,3))
770  FOR I = 1 TO NR: INPUT G$,RD(I,1),RD(I,2),RD(I,3),RD(I,4)
780 RD$(I) =  MID$ (G$,10, LEN (G$) - 9)
790  NEXT : FOR I = 1 TO NO: INPUT G$,OD$(I),RO(I),TP(I),OV(I),OT(I),OL(I)
800 OB$(I) =  MID$ (G$,10, LEN (G$) - 9)
810  NEXT : PRINT D$;"CLOSE"
820  FOR I = 1 TO NF
830 V =  VAL ( LEFT$ (FR$(I),4))
840  FOR Q = 1 TO 13: IF V = RT(Q) THEN RT(Q) = RT(Q) + 1
850  NEXT : NEXT 
860  GOTO 100
