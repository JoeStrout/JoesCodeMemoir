1  REM  ===QUESTRON===
2  REM ->SCREEN CREATE<-
3  REM  ==============
10  DEF  FN PK(X) = (8156 + SC * 724) + 40 * Y + X
20  TEXT : HOME : PRINT "ENTER NAME OF FILE TO LOAD (X IF NONE):": INPUT "";A$: IF A$ <  > "X" THEN  PRINT  CHR$ (4)"BLOAD"A$
30  PRINT : PRINT : INPUT "ENTER SCREEN NO. TO EDIT:";A$:SC =  VAL (A$): VTAB 21: HTAB 16: PRINT "SCREEN "SC
50 LF$ =  CHR$ (8):RT$ =  CHR$ (21):UP$ =  CHR$ (11):DN$ =  CHR$ (10)
60  GOSUB 5000:X = 20:Y = 12
90  VTAB 21: HTAB 1: PRINT "SCREEN "SC":" TAB 12)"N="NX(1) TAB 18)"E="NX(2) TAB 24)"S="NX(3) TAB 30)"W="NX(4);
100  REM 
110  NORMAL : HTAB X: VTAB Y + 2
120  GET A$: IF A$ = LF$ THEN  INVERSE : GOSUB 5060:X = X - 1: GOTO 100
130  IF A$ = RT$ THEN  INVERSE : GOSUB 5060:X = X + 1: GOTO 100
140  IF A$ = UP$ THEN  INVERSE : GOSUB 5060:Y = Y - 1: GOTO 100
150  IF A$ = DN$ THEN  INVERSE : GOSUB 5060:Y = Y + 1: GOTO 100
155  IF A$ =  CHR$ (27) THEN 200
160  POKE  FN PK(X), ASC (A$) + 128: IF  ASC (A$) < 32 THEN  HTAB X: VTAB Y + 2: PRINT " "; CHR$ (8);
170 A$ = RT$: GOTO 130
200  REM 
205  POKE 35,24: POKE 34,21: HOME 
210  HTAB 1: VTAB 22: PRINT "(C)LEAR (E)DIT (D)OORS (S)AVE (Q)UIT"
220  GET A$: IF A$ <  > "C" AND A$ <  > "E" AND A$ <  > "D" AND A$ <  > "S" AND A$ <  > "Q" THEN 220
230  IF A$ = "Q" THEN  TEXT : END 
240  IF A$ = "C" THEN  HTAB 1: VTAB 3: FOR Y = 1 TO 18: FOR X = 1 TO 40: PRINT " ";: POKE  FN PK(X),1: NEXT : NEXT : GOTO 100
250  IF A$ = "E" THEN  HOME : POKE 34,2: POKE 35,20: GOTO 90
260  IF A$ <  > "D" THEN 300
270  FOR I = 1 TO 4: PRINT  MID$ ("NESW",I,1);: INPUT "-";NX(I)
280  POKE 8191 + SC * 724 + I,NX(I): NEXT 
290  HOME :A$ = "E": GOTO 250
300  IF A$ <  > "S" THEN 200
310  INPUT "ENTER FILENAME:";FL$: IF  VAL (FL$) <  > 0 THEN 310
320  INPUT "ENTER NO. OF SCREENS:";A$:NS =  VAL (A$): IF NS = 0 THEN 200
330  PRINT  CHR$ (4)"BSAVE"FL$",A8192,L"724 * NS: GOTO 200
4999  END 
5000  REM  SUBROUTINES
5010  REM  LOAD SCREEN SC
5015  POKE 34,2: POKE 35,20: HOME : INVERSE 
5020 LOC = 8192 + SC * 724:NX(1) =  PEEK (LOC):NX(2) =  PEEK (LOC + 1):NX(3) =  PEEK (LOC + 2):NX(4) =  PEEK (LOC + 3):LOC = LOC + 4
5030  FOR Y = 1 TO 18: FOR X = 1 TO 40: REM  IF FNPK(X)>128THEN?" ";:NEXT : NEXT: GOTO 5050
5040  HTAB X: VTAB Y + 2: PRINT  CHR$ ( PEEK ( FN PK(X)));: NEXT : NEXT : NORMAL 
5050  RETURN 
5060  HTAB X: VTAB Y + 2: PRINT  CHR$ ( PEEK ( FN PK(X)));: RETURN 
