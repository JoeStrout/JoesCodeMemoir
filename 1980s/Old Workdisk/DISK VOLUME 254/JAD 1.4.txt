10  REM JOE'S GREAT ADVENTURE
11  REM  MAIN PROGRAM
12  REM  VERSION 1.4
17  PRINT  CHR$ (4)"BLOADSAMPLE1"
18  PRINT : PRINT  CHR$ (4)"BLOADDGN1"
20 NP = 2:PA$(1) = "GOOD":PC$(1) = "TECH":PF(1) = 10:PH(1) = 9:PN$(1) = "HAN SOLO":PS$(1) = "OK":PA(1) = 4
30 PA$(2) = "NEUTRAL":PC$(2) = "DOCTOR":PF(2) = 8:PH(2) = 4:PN$(2) = "JOE DOCTOR":PS$(2) = "STONED":PA(2) = 6
40  GOSUB 30000: GOTO 2000
99  GOTO 99
100  REM ===SYSTEM ROUTINES===
110  REM ---MENU GET---
120  REM ZC$(1-ZNC)=CHOICES
130  POKE 49168,0: WAIT 49152,128:A$ =  CHR$ ( PEEK (49152) - 128): POKE 49168,0: FOR I = 1 TO ZNC: IF A$ <  > ZC$(I) THEN  NEXT : GOTO 130
140 ZCH = I: RETURN 
150  REM ---PICTURE FLICK---
160  REM WITH MENU GET
170  POKE 49168,0
180 Z =  PEEK (49152): IF Z < 128 THEN  POKE 49234,0: POKE 49236,0: POKE 49239,0: POKE 49232,0: FOR I = 1 TO 3: NEXT : POKE 49233,0: GOTO 180
190 A$ =  CHR$ (Z - 128): POKE 49168,0: FOR I = 1 TO ZNC: IF A$ <  > ZC$(I) THEN  NEXT : GOTO 180
200 ZCH = I: RETURN 
210  REM ---CHARACTER UPDATE---
220  FOR I = 1 TO NP: HTAB 2: VTAB 17 + I: CALL  - 868: PRINT PN$(I) TAB 14) LEFT$ (PC$(I),3)"-" LEFT$ (PA$(I),1)"  "PA(I) TAB 27 -  LEN ( STR$ (PH(I))))PH(I)"/"PF(I) TAB 36 -  LEN (PS$(I)) / 2)PS$(I);
230  INVERSE : HTAB 40: PRINT " ";: NORMAL : NEXT : RETURN 
240  REM ---SET UP SCREEN---
250  TEXT : HOME : PRINT  CHR$ (4)"PR#0"
260  INVERSE : VTAB 1: PRINT  TAB 33)" ";: NORMAL : PRINT " __ __ ";: INVERSE 
270  FOR I = 1 TO 6: HTAB 1: VTAB I + 1: PRINT " ";: HTAB 18: PRINT " ";: HTAB 33: PRINT " ": NEXT : PRINT  TAB 33)" "
280  FOR I = 1 TO 15: HTAB 1: PRINT " ";: HTAB 40: PRINT " ";: NEXT 
290  VTAB 16: PRINT  TAB 40)" ": VTAB 24: PRINT  TAB 39)" ";
300  POKE 2039,32
310  NORMAL : VTAB 2: HTAB 19: PRINT "ACTIVE EFFECTS";: HTAB 34: PRINT "|  N  |";
320  HTAB 34: PRINT "|     |";: HTAB 34: PRINT "|     |";: HTAB 34: PRINT "W     E";: HTAB 34: PRINT "|     |";: HTAB 34: PRINT "|     |";: HTAB 34: PRINT "|__S__|";
330  HTAB 2: VTAB 17: PRINT "  NAME      CLASS  AC    HP    STATUS";
340  RETURN 
350  REM ---SET MAP WINDOW---
360  POKE 33,5: POKE 32,34: POKE 34,2: POKE 35,7: RETURN 
370  REM ---SET DESC WINDOW---
380  POKE 33,38: POKE 32,1: POKE 34,8: POKE 35,15: RETURN 
390  REM ---SET MENU WINDOW---
400  POKE 33,16: POKE 32,1: POKE 34,1: POKE 35,7: RETURN 
410  REM ---DRAW MAP---
420  GOSUB 350: HOME : TEXT 
430  IF  FN EF(R) THEN  REM GO FOR EFFECTS HERE!
440  INVERSE : FOR Z = 1 TO 4:B1 =  FN B1(Z):B2 =  FN B2(Z): IF  NOT B1 AND  NOT B2 THEN 540
450  ON Z GOTO 460,480,500,520
460  HTAB 35: VTAB 3: PRINT "+++++";: IF  FN CR(Z) = 255 OR B2 = 0 THEN 540: REM  NO VISIBLE DOOR 
470  HTAB 37: VTAB 3: NORMAL : PRINT "-";: INVERSE : GOTO 540
480  FOR I = 3 TO 7: HTAB 35: VTAB I: PRINT "+";: NEXT : IF  FN CR(Z) = 255 OR B2 = 0 THEN 540
490  HTAB 35: VTAB 5: NORMAL : PRINT "|";: INVERSE : GOTO 540
500  FOR I = 3 TO 7: HTAB 39: VTAB I: PRINT "+";: NEXT : IF  FN CR(Z) = 255 OR B2 = 0 THEN 540
510  HTAB 39: VTAB 5: NORMAL : PRINT "|";: INVERSE : GOTO 540
520  HTAB 35: VTAB 7: PRINT "+++++";: IF  FN CR(Z) = 255 OR B2 = 0 THEN 540
530  HTAB 37: VTAB 7: NORMAL : PRINT "-": INVERSE 
540  NEXT : NORMAL : HTAB 37: VTAB 5: PRINT "*";: RETURN 
550  REM ---MAIN MENU---
560  GOSUB 390: HOME : PRINT "N)ORTH    E)AST": PRINT "S)OUTH    W)EST": PRINT "C)AMP   I)NSPECT"
570 ZNC = 6: FOR I = 1 TO 6:ZC$(I) =  MID$ ("NWESCI",I,1): NEXT 
580  GOSUB 110: IF ZCH > 4 THEN 580: REM  NO CAMP/INSPECT YET
590 D = ZCH: IF  FN CR(D) = 255 OR ( FN B1(D) AND  FN B2(D)) THEN  PRINT  CHR$ (7): GOTO 550: REM  HIT WALL OR LOCKED DOOR
620 R =  FN AR(D): RETURN 
700  REM ---COMBAT 1-STEP---
701  REM NEED D(EFENDER) AND A(TTACKER) PREFIXES WITH
702  REM  S(TRENGTH), A(RMOUR), D(EXDERITY), N$(AME)
703  REM    W(EAPON DAMAGE)
704  REM ASSUMES DESC WINDOW ALREADY SET
710  HOME : PRINT AN$;" ATTACKS "DN$
720 ZR =  FN RN(100):ZT = (AD + DA) * 3: IF ZR > ZT THEN  PRINT : PRINT "A MISS!": RETURN 
730 ZN% = ZT / ZR: IF ZN% > 4 THEN ZN% = 4
740  PRINT : PRINT "AND HITS "ZN" TIMES FOR ";:ZD% = ZN + AW + AS / 5: PRINT ZD%" DAMAGE!"
750  RETURN 
2000  REM ===MAIN LOOP===
2010  GOSUB 550: IF  FN EF(R) THEN 2500
2020  GOSUB 410: REM  CHECK FOR MONSTERS HERE
2030  GOTO 2000
2500  REM ===ROOM EFFECTS===
30000  REM ====INITIALIZATION====
30010  DEF  FN CR(D) =  PEEK (BASE + R * 5 + D): DEF  FN B1(D) =  FN CR(D) > 127: DEF  FN B2(D) = ( FN CR(D) -  FN B1(D) * 128) > 63
30020  DEF  FN AR(D) = ( FN CR(D) -  FN B1(D) * 128) -  FN B2(D) * 64: DEF  FN EF(R) =  PEEK (BASE + R * 5)
30030 BASE = 16384
30040 R = 0: GOSUB 240: GOSUB 210: GOSUB 410
30050  DEF  FN RN(X) =  INT (X *  RND (1) + 1)
31000  RETURN 
