5  DIM VB$(20),OB$(200),OD$(200),RO(200),OT(200),OL(200),OV(200),VR(200),RD$(100),RD(100,4),OC(20)
10  PRINT " THIS IS NOT THE START PROGRAM.          TRY TO EXEC AN ADVENTURE FILE INSTEAD.": END : REM   FILE ERASES THIS LINE
20  GOSUB 10000: REM  INIT VARIABLES
30 R = 1: GOTO 490
100  REM =======INPUT========
105  PRINT : PRINT :MIN = MIN + 1: IF MIN = 60 THEN HR = HR + 1:MIN = 0: IF HR = 24 THEN HR = 0
110  INPUT "  COMMAND==>";C$: IF C$ = "" THEN C$ = OC$: VTAB  PEEK (37): HTAB 13: PRINT C$
120 OC$ = C$:N$ = "":N = 0
125  PRINT : PRINT 
130  FOR VL = 1 TO  LEN (C$): IF  MID$ (C$,VL,1) = " " THEN V$ =  LEFT$ (C$,VL - 1):N$ =  RIGHT$ (C$, LEN (C$) - VL): GOTO 150
140  NEXT :V$ = C$
150  REM 
295  FOR V = 1 TO NV: IF V$ <  > VB$(V) THEN  NEXT : PRINT "I DON'T KNOW HOW TO "V$".": GOTO 100
296  IF N$ = "" THEN 300
297  FOR N = 1 TO NO: IF N$ <  > OB$(N) THEN  NEXT : PRINT "I DON'T UNDERSTAND "; CHR$ (34);N$; CHR$ (34);".": GOTO 100
298  IF RO(N) <  > R AND  INT (RO(N)) <  > 0 OR OT(N) = 1 AND TP(N) <  > 2 THEN  PRINT "I DON'T SEE THAT HERE.": GOTO 100
300  ON V GOTO 400,400,400,400,400,310,500,600,700,800,900,1000,1100,1500,1600
310  PRINT : PRINT "ARE YOU SURE YOU WANT TO QUIT? (Y/N)": GET A$: PRINT  TAB 20);A$: IF A$ <  > "Y" THEN 100
311  TEXT : END 
400  REM  --------MOVE----------
405  IF V$ = "GO" THEN V$ =  LEFT$ (N$,1): GOTO 150
410  IF RD(R,V) = 0 THEN  PRINT "YOU CAN'T GO THAT WAY.": GOTO 100
415 R = RD(R,V)
420  REM   SPECS START HERE
490  TEXT : HOME : PRINT RD$(R): PRINT "---------------------------------------": POKE 34, PEEK (37): FOR I = 1 TO NO: IF RO(I) = R AND OT(I) <  > 1 AND TP(I) <  > 2 THEN  PRINT "  YOU SEE "OB$(I)"."
492  IF TP(I) = 2 AND RO(I) = R THEN  PRINT "  "OB$(I)" IS HERE."
495  NEXT : GOTO 100
500  REM  --------GET-----------
505  IF  INT (RO(N)) = 0 THEN  PRINT "YOU ALREADY HAVE THAT.": GOTO 100
507  IF TP(N) = 2 THEN  PRINT "YOU CAN'T GET A MONSTERR!": GOTO 100
510  IF OT(N) = 2 THEN  PRINT "IT'S TOO HEAVY.": GOTO 100
515  IF OT(N) = 3 THEN  PRINT "THAT'S RIDICULOUS!": GOTO 100
520  REM  OB-SPECS START HERE
590 NC = NC + 1:OC(NC) = N:RO(N) = 0: PRINT "GOT IT.": GOTO 100
600  REM  --------DROP----------
605  IF  INT (RO(N)) <  > 0 THEN  PRINT "YOU DON'T HAVE THAT.": GOTO 100
610  REM  OB-SPECS START HERE
690 RO(N) = R: FOR I = 1 TO NC: IF OC(I) = N THEN OC(I) = OC(NC):NC = NC - 1: PRINT "DROPPED.": GOTO 100
695  NEXT 
700  REM  --------LOOK----------
703  IF N <  > 0 THEN 710
705  FOR I = 1 TO NO: IF RO(I) = R AND OT(I) <  > 1 AND TP(I) <  > 2 THEN  PRINT "  YOU SEE "OB$(I)"."
707  IF TP(I) = 2 AND RO(I) = R THEN  PRINT "  "OB$(I)" IS HERE."
708  NEXT 
710  REM   SPECS START HERE
790  PRINT OD$(N): IF OL(N) <  > 0 AND OT(OL(N)) <  > 1 AND RO(OL(N)) = RO(N) OR TP(OL(N)) = 2 AND TP(N) < 2 AND RO(OL(N)) = RO(N) THEN N = OL(N): GOTO 790
795  GOTO 100
800  REM  --------OPEN----------
805  IF N = 0 THEN  PRINT "OPEN WHAT?": GOTO 100
808  IF TP(N) <  > 1 THEN  PRINT "YOU DON'T OPEN A "N$".": GOTO 100
810  REM   SPECS START HERE
890  PRINT "OPEN."
895 N = OL(N):OT(N) = 0: IF OL(N) <  > 0 THEN 895
897  GOTO 100
900  REM  ------INVENTORY-------
905  PRINT : PRINT "YOU ARE CARRYING:": IF NC = 0 THEN  PRINT "   ZILCH!!!": GOTO 100
910  FOR I = 1 TO NC
920  REM  IF U CAN THINK OF ANY  SPECS,THEY START HERE.
990  PRINT "  -"OB$(OC(I)): NEXT : GOTO 100
1000  REM ------READY-----------
1005  IF N = 0 THEN  PRINT "READY WHAT?": GOTO 100
1010  IF TP(N) <  > 3 THEN  PRINT "THAT'S NOT A WEAPON.": GOTO 100
1020  REM   SPECS START HERE
1090 WP = N: PRINT "READIED.": GOTO 100
1100  REM =======COMBAT========
1105  IF N = 0 THEN  PRINT "FIGHT WHAT?": GOTO 100
1110  IF TP(N) <  > 2 THEN  PRINT "YOU DON'T FIGHT A "N$".": GOTO 100
1115  IF WP = 0 THEN  PRINT "YOU DON'T HAVE A WEAPON READY.": GOTO 100
1120  REM   SPECS START HERE
1200  REM -----PLAYER ATTACK----
1205  PRINT : PRINT "PLAYER ATTACKS "N$"-":SCR =  INT (( FN DC(20) + ST / 2 - OT(N) * 2 + EX / 10) / 5): IF SCR < 1 THEN SCR = 1
1206  IF SCR > 7 THEN SCR = 7
1210  PRINT "   ";SCR$(SCR):EX = EX + SCR:OV(N) = OV(N) - (SCR - 1) * OV(WP)
1215  IF OV(N) > 0 THEN 1400
1220  REM  DEAD MONST SPECS HERE
1399  PRINT N$" IS DEAD.":OB$(N) = "DEAD " + OB$(N):TP(N) = 0:OT(N) = 0:OT(OL(N)) = 0
1400  FOR I = 1 TO NO: IF RO(I) = R AND TP(I) = 2 THEN  PRINT : PRINT OB$(I)" ATTACKS PLAYER-":SCR =  INT (( FN DC(20) + OT(I) * 2 - AC / 2 - EX / 10) / 5): GOTO 1405
1403  NEXT : GOTO 100
1405  IF SCR > 7 OR SCR < 1 THEN  VTAB  PEEK (37) - 1:I = I - 1: NEXT 
1410  PRINT "   ";SCR$(SCR):HP = HP - ((SCR - 1) *  INT (OT(I) / 2)): IF HP > 0 THEN 1403
1420  REM    SPECS START HERE
1490  PRINT : PRINT : PRINT "YOU MY FRIEND,ARE DYING...."
1495  FOR I = 1 TO 4000: NEXT : END 
1500  REM -------WEAR-----------
1505  IF RO(N) = R THEN  PRINT "YOU DON'T HAVE THAT.": GOTO 100
1510  IF RO(N) = .5 THEN  PRINT "YOU'RE ALREADY WEARING IT.": GOTO 100
1520  REM    SPECS HERE
1590  PRINT "YOU CAN'T WEAR A "N$".": GOTO 100
1595 RO(N) = .5: GOTO 100
1600  REM ------REMOVE----------
1605  IF RO(N) <  > .5 THEN  PRINT "YOU AREN'T WEARING THAT.": GOTO 100
1610  REM     SPECS HERE
1695  PRINT "REMOVED.":RO(N) = 0: GOTO 100
10000  REM =========INIT====================================
10005  PRINT "BLOAD APPLE.BEEP": CALL 768
10010  DEF  FN DC(X) =  INT (X *  RND (1) + 1)
10015 SCR$(1) = "A MISS.":SCR$(2) = "A GLANCING BLOW.":SCR$(3) = "A GRAZING HIT.":SCR$(4) = "A GOOD STRIKE.":SCR$(5) = "A SQUARE BLOW!":SCR$(6) = "A HARD HIT!":SCR$(6) = "A CRITICAL HIT!"
10020  READ NV: FOR I = 1 TO NV: READ VB$(I): NEXT 
10030 ST =  FN DC(10) + 10:IQ =  FN DC(10) + 10:DX =  FN DC(10) + 10:CH =  FN DC(10) + 10:HP =  FN DC(5) + 5:AC = 6
10100  READ NR,NO
10110  FOR I = 1 TO NR: READ RD$(I): FOR X = 1 TO 4: READ RD(I,X): NEXT : NEXT 
10120  FOR I = 1 TO NO: READ OB$(I),OD$(I),RO(I),TP(I),OV(I),OT(I),OL(I): NEXT 
19999  RETURN 
20000  DATA 15,N,E,S,W,GO,QUIT,GET,DROP,LOOK,OPEN,I,READY,FIGHT,WEAR,REMOVE
20499  DATA  0,0: REM  NR,NO
