10  REM  JAD DUNGEON CREATE
11  REM  VERSION 1.0
20 BASE = 16384
30  DEF  FN CR(D) =  PEEK (BASE + R * 5 + D): DEF  FN B1(D) =  FN CR(D) > 127: DEF  FN B2(D) = ( FN CR(D) -  FN B1(D) * 128) > 63: DEF  FN AR(D) = ( FN CR(D) -  FN B1(D) * 128) -  FN B2(D) * 64
35  DEF  FN EF(R) =  PEEK (BASE + R * 5)
90  GOTO 1000
100  REM ---PRINT ROOM R---
110  PRINT "ROOM "R":": FOR D = 1 TO 4: PRINT " DOOR "D"-";: IF  FN CR(D) = 255 THEN  PRINT "NONE": GOTO 130
120  PRINT  FN AR(D)" TYPE " FN B1(D) FN B2(D)
130  NEXT : PRINT "EFFECT : ";: IF  FN EF(R) = 0 THEN  PRINT "NONE": RETURN 
140  PRINT  FN EF(R): RETURN 
150  REM ---INPUT ROOM R---
160  PRINT "ROOM "R":": FOR D = 1 TO 4
170  PRINT " DOOR "D;: INPUT " (255=NONE) :";DR: IF DR = 255 THEN 200
180  INPUT "ENTER STATUS BITS (B1,B2) :";B1,B2: IF B1 THEN DR = DR + 128
190  IF B2 THEN DR = DR + 64
200  POKE BASE + R * 5 + D,DR: NEXT 
210  INPUT "ENTER EFFECT (0=NONE):";EF: POKE BASE + R * 5,EF: RETURN 
1000  REM ===MAIN LOOP===
1010  HOME : PRINT "YOU MAY S)EE AN EXISTING ROOM": PRINT "        E)NTER ONE OR": PRINT "        Q)UIT       ";
1020  GET A$: IF A$ <  > "S" AND A$ <  > "Q" AND A$ <  > "E" THEN 1020
1025  IF A$ = "Q" THEN 1500
1030  PRINT : PRINT : INPUT "ENTER ROOM NUMBER:";R: PRINT : PRINT : IF A$ = "S" THEN  GOSUB 100: GOTO 1050
1040  GOSUB 150
1050  VTAB 23: PRINT "PRESS ANY KEY TO CONTINUE:";: GET A$: GOTO 1000
1500  REM ===QUIT===
1510  TEXT : HOME : PRINT "BYE BYE!": END 
